mapscripts AquaHideout_B2F_MapScripts {
    MAP_SCRIPT_ON_TRANSITION {
        call_if_set(FLAG_TEAM_AQUA_ESCAPED_IN_SUBMARINE, AquaHideout_B2F_EventScript_PreventMattNoticing)
    }
}

raw `
.set LOCALID_MATT, 1
.set LOCALID_SUBMARINE, 4

AquaHideout_B2F_EventScript_PreventMattNoticing::
	setvar VAR_TEMP_1, 1
	return

AquaHideout_B2F_EventScript_MattNoticePlayer::
	lockall
	setvar VAR_0x8008, LOCALID_MATT
	playse SE_PIN
	applymovement VAR_0x8008, Common_Movement_ExclamationMark
	waitmovement 0
	applymovement VAR_0x8008, Common_Movement_FacePlayer
	waitmovement 0
	setvar VAR_TEMP_1, 1
	releaseall
	end

AquaHideout_B2F_EventScript_Matt::
	trainerbattle_single TRAINER_MATT, AquaHideout_B2F_Text_MattIntro, AquaHideout_B2F_Text_MattDefeat, AquaHideout_B2F_EventScript_SubmarineEscape
	msgbox AquaHideout_B2F_Text_MattPostBattle, MSGBOX_DEFAULT
	release
	end

AquaHideout_B2F_EventScript_SubmarineEscape::
	setvar VAR_0x8008, LOCALID_MATT
	setvar VAR_0x8009, LOCALID_SUBMARINE
	applymovement VAR_0x8008, Common_Movement_WalkInPlaceFasterLeft
	waitmovement 0
	delay 20
	applymovement VAR_0x8008, Common_Movement_FacePlayer
	waitmovement 0
	msgbox AquaHideout_B2F_Text_OurBossGotThroughHisPreparations, MSGBOX_DEFAULT
	closemessage
	applymovement VAR_0x8008, Common_Movement_WalkInPlaceFasterLeft
	applymovement VAR_0x8009, AquaHideout_B2F_Movement_SumbarineDepartLeft
	waitmovement 0
	removeobject VAR_0x8009
	delay 20
	applymovement VAR_0x8008, Common_Movement_FacePlayer
	waitmovement 0
	msgbox AquaHideout_B2F_Text_MattPostBattle, MSGBOX_DEFAULT
	setflag FLAG_TEAM_AQUA_ESCAPED_IN_SUBMARINE
	setflag FLAG_HIDE_LILYCOVE_CITY_AQUA_GRUNTS
	release
	end

AquaHideout_B2F_Movement_SumbarineDepartLeft:
	walk_left
	walk_left
	walk_left
	walk_left
	step_end

@ Unused
AquaHideout_B2F_Movement_SumbarineDepartRight:
	walk_right
	walk_right
	walk_right
	walk_right
	step_end

AquaHideout_B2F_EventScript_Grunt4::
	trainerbattle_single TRAINER_GRUNT_AQUA_HIDEOUT_4, AquaHideout_B2F_Text_Grunt4Intro, AquaHideout_B2F_Text_Grunt4Defeat, AquaHideout_B2F_EventScript_Grunt4Defeated
	msgbox AquaHideout_B2F_Text_Grunt4PostBattle, MSGBOX_AUTOCLOSE
	end

AquaHideout_B2F_EventScript_Grunt4Defeated::
	msgbox AquaHideout_B2F_Text_Grunt4PostBattle, MSGBOX_DEFAULT
	release
	end

AquaHideout_B2F_EventScript_Grunt6::
	trainerbattle_single TRAINER_GRUNT_AQUA_HIDEOUT_6, AquaHideout_B2F_Text_Grunt6Intro, AquaHideout_B2F_Text_Grunt6Defeat
	msgbox AquaHideout_B2F_Text_Grunt6PostBattle, MSGBOX_AUTOCLOSE
	end

AquaHideout_B2F_EventScript_Grunt8::
	trainerbattle_single TRAINER_GRUNT_AQUA_HIDEOUT_8, AquaHideout_B2F_Text_Grunt8Intro, AquaHideout_B2F_Text_Grunt8Defeat
	msgbox AquaHideout_B2F_Text_Grunt8PostBattle, MSGBOX_AUTOCLOSE
	end
`

text AquaHideout_B2F_Text_MattIntro {
	format("Hehehe…\p"
	"Got here already, did you?\n"
	"We underestimated you!\p"
	"But this is it!\p"
	"I'm a cut above the GRUNTS you've seen\n"
	"so far.\p"
	"I'm not stalling for time.\n"
	"I'm going to pulverize you!")
}

text AquaHideout_B2F_Text_MattDefeat {
	format("Hehehe…\n"
	"So, I lost, too…")
}

text AquaHideout_B2F_Text_OurBossGotThroughHisPreparations {
	format("Hehehe!\p"
	"While I was toying with you, our BOSS\n"
	"got through his preparations!")
}

text AquaHideout_B2F_Text_MattPostBattle {
	format("Hehehe!\p"
	"Our BOSS has already gone on his way to\n"
	"some cave under the sea!\p"
	"If you're going to give chase, you'd\n"
	"better search the big, wide sea beyond\l"
	"LILYCOVE.\p"
	"But will you find it then?\n"
	"Hehehe!")
}

text AquaHideout_B2F_Text_Grunt4Intro {
	format("Wahahah, I grew weary of waiting!\n"
	"You owe me a battle, too!")
}

text AquaHideout_B2F_Text_Grunt4Defeat {
	format("Tired of waiting…\n"
	"Lost and dazed…")
}

text AquaHideout_B2F_Text_Grunt4PostBattle {
	format("BOSS…\n"
	"Is this good enough?")
}

text AquaHideout_B2F_Text_Grunt6Intro {
	format("Warp panels, the HIDEOUT's pride\n"
	"and joy!\p"
	"You're clueless about where you are,\n"
	"aren't you?\p"
	"Fluster and tire out the enemy, then\n"
	"lower the boom! That's our plan!")
}

text AquaHideout_B2F_Text_Grunt6Defeat {
	format("What's wrong with you?\n"
	"You're not tired at all!")
}

text AquaHideout_B2F_Text_Grunt6PostBattle {
	format("That reminds me… I can't remember\n"
	"where I put the MASTER BALL.\p"
	"If I fail to guard it, our BOSS will\n"
	"chew me out…")
}

text AquaHideout_B2F_Text_Grunt8Intro {
	format("When I joined TEAM AQUA, the first\n"
	"thing I had to learn was how these\l"
	"warp panels connected.")
}

text AquaHideout_B2F_Text_Grunt8Defeat {
	format("I was too occupied thinking about the warp panels…")
}

text AquaHideout_B2F_Text_Grunt8PostBattle {
	format("I'll have to learn about how I can battle more effectively…")
}
