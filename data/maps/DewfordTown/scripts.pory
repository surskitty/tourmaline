mapscripts DewfordTown_MapScripts {
    MAP_SCRIPT_ON_TRANSITION {
        setflag(FLAG_VISITED_DEWFORD_TOWN)
    }
    MAP_SCRIPT_ON_LOAD {
        special(SetRoofBirds)
        special(SetStationaryWilds)
    }
}

raw `

@ Sailing to and from Dewford uses local IDs from different maps
@ e.g. Route 104's sail to Dewford script references local IDs from Dewford's map
@ All of these local IDs are labeled here
.equ LOCALID_BRINEY_DEWFORD, 2
.equ LOCALID_BOAT_DEWFORD, 4

.equ LOCALID_BOAT_R109, 1
.equ LOCALID_BRINEY_R109, 2

.equ LOCALID_BOAT_R104, 7
.equ LOCALID_BRINEY_R104, 8

DewfordTown_EventScript_Briney::
    lock
    faceplayer
    goto_if_unset FLAG_DELIVERED_STEVEN_LETTER, DewfordTown_EventScript_ReturnToPetalburgPrompt
    speakername CommonName_Text_MrBriney
    message DewfordTown_Text_WhereAreWeBound
    waitmessage
    speakername CommonName_Text_MrBriney
    multichoicedefault 21, 6, MULTI_BRINEY_ON_DEWFORD, 2, FALSE
    switch VAR_RESULT
    case 0, DewfordTown_EventScript_ChoosePetalburg
    case 1, DewfordTown_EventScript_ChooseSlateport
    case 2, DewfordTown_EventScript_CancelSailSelect
    case MULTI_B_PRESSED, DewfordTown_EventScript_CancelSailSelect
    end

DewfordTown_EventScript_ChoosePetalburg::
    msgbox DewfordTown_Text_PetalburgWereSettingSail, MSGBOX_DEFAULT
    closemessage
    goto DewfordTown_EventScript_SailToPetalburg
    release
    end

DewfordTown_EventScript_ChooseSlateport::
    msgbox DewfordTown_Text_SlateportWereSettingSail, MSGBOX_DEFAULT
    closemessage
    goto DewfordTown_EventScript_SailToSlateport
    release
    end

DewfordTown_EventScript_CancelSailSelect::
    msgbox DewfordTown_Text_JustTellMeWhenYouNeedToSetSail, MSGBOX_DEFAULT
    closemessage
    release
    end

DewfordTown_EventScript_ReturnToPetalburgPrompt::
    msgbox DewfordTown_Text_SetSailBackToPetalburg, MSGBOX_YESNO
    goto_if_eq VAR_RESULT, YES, DewfordTown_EventScript_SailBackToPetalburg
    speakername CommonName_Text_MrBriney
    msgbox DewfordTown_Text_GoDeliverIllBeWaiting, MSGBOX_DEFAULT
    release
    end

DewfordTown_EventScript_SailBackToPetalburg::
    msgbox DewfordTown_Text_PetalburgWereSettingSail2, MSGBOX_DEFAULT
    closemessage
    goto DewfordTown_EventScript_SailToPetalburg
    end

DewfordTown_EventScript_Woman::
    msgbox DewfordTown_Text_TinyIslandCommunity, MSGBOX_NPC
    end

DewfordTown_EventScript_TownSign::
    msgbox DewfordTown_Text_TownSign, MSGBOX_SIGN
    end

DewfordTown_EventScript_GymSign::
    msgbox DewfordTown_Text_GymSign, MSGBOX_SIGN
    end

DewfordTown_EventScript_HallSign::
    msgbox DewfordTown_Text_HallSign, MSGBOX_SIGN
    end

DewfordTown_EventScript_OldRodFisherman::
    lock
    faceplayer
    goto_if_set FLAG_RECEIVED_OLD_ROD, DewfordTown_EventScript_HowsFishing
    msgbox DewfordTown_Text_GettingItchToFish, MSGBOX_YESNO
    goto_if_eq VAR_RESULT, YES, DewfordTown_EventScript_GiveOldRod
    goto_if_eq VAR_RESULT, NO, DewfordTown_EventScript_NotGettingItchToFish
    end

DewfordTown_EventScript_GiveOldRod::
    msgbox DewfordTown_Text_GiveYouOneOfMyRods, MSGBOX_DEFAULT
    giveitem ITEM_OLD_ROD
    goto_if_eq VAR_RESULT, FALSE, Common_EventScript_ShowBagIsFull
    setflag FLAG_RECEIVED_OLD_ROD
    msgbox DewfordTown_Text_ThrowInFishingAdvice, MSGBOX_DEFAULT
    release
    end

DewfordTown_EventScript_NotGettingItchToFish::
    msgbox DewfordTown_Text_ThatsTooBadThen, MSGBOX_DEFAULT
    release
    end

DewfordTown_EventScript_HowsFishing::
    message DewfordTown_Text_HowsYourFishing
    waitmessage
    multichoice 20, 8, MULTI_HOWS_FISHING, TRUE
    goto_if_eq VAR_RESULT, 0, DewfordTown_EventScript_FishingExcellent
    goto_if_eq VAR_RESULT, 1, DewfordTown_EventScript_FishingNotSoGood
    end

DewfordTown_EventScript_FishingExcellent::
    msgbox DewfordTown_Text_GreatHaulInSomeBigOnes, MSGBOX_DEFAULT
    release
    end

DewfordTown_EventScript_FishingNotSoGood::
    msgbox DewfordTown_Text_FishingAdvice, MSGBOX_DEFAULT
    release
    end

DewfordTown_EventScript_SailToPetalburg::
    call EventScript_BackupMrBrineyLocation
    setobjectsubpriority LOCALID_BRINEY_DEWFORD, MAP_DEWFORD_TOWN, 0
    setobjectsubpriority OBJ_EVENT_ID_PLAYER, MAP_DEWFORD_TOWN, 0
    applymovement LOCALID_BRINEY_DEWFORD, DewfordTown_Movement_BrineyBoardBoat
    waitmovement 0
    removeobject LOCALID_BRINEY_DEWFORD
    applymovement OBJ_EVENT_ID_PLAYER, DewfordTown_Movement_PlayerBoardBoat
    waitmovement 0
    hideobjectat OBJ_EVENT_ID_PLAYER, MAP_DEWFORD_TOWN
    call Common_EventScript_PlayBrineysBoatMusic
    applymovement LOCALID_BOAT_DEWFORD, DewfordTown_Movement_SailToPetalburg
    applymovement OBJ_EVENT_ID_PLAYER, DewfordTown_Movement_SailToPetalburg
    waitmovement 0
    showobjectat OBJ_EVENT_ID_PLAYER, MAP_ROUTE104
    call Common_EventScript_StopBrineysBoatMusic
    applymovement OBJ_EVENT_ID_PLAYER, DewfordTown_Movement_ExitBoatPetalburg
    waitmovement 0
    showobjectat OBJ_EVENT_ID_PLAYER, MAP_ROUTE104
    clearflag FLAG_HIDE_BRINEYS_HOUSE_MR_BRINEY
    clearflag FLAG_HIDE_BRINEYS_HOUSE_PEEKO
    clearflag FLAG_HIDE_ROUTE_104_MR_BRINEY_BOAT
    setflag FLAG_HIDE_MR_BRINEY_BOAT_DEWFORD_TOWN
    hideobjectat LOCALID_BOAT_DEWFORD, MAP_DEWFORD_TOWN
    setvar VAR_BOARD_BRINEY_BOAT_STATE, 2
    resetobjectsubpriority OBJ_EVENT_ID_PLAYER, MAP_DEWFORD_TOWN
    warp MAP_ROUTE104_MR_BRINEYS_HOUSE, 5, 4
    copyvar VAR_BRINEY_LOCATION, VAR_0x8008
    waitstate
    release
    end

DewfordTown_EventScript_SailToSlateport::
    call EventScript_BackupMrBrineyLocation
    setobjectsubpriority LOCALID_BRINEY_DEWFORD, MAP_DEWFORD_TOWN, 0
    setobjectsubpriority OBJ_EVENT_ID_PLAYER, MAP_DEWFORD_TOWN, 1
    applymovement LOCALID_BRINEY_DEWFORD, DewfordTown_Movement_BrineyBoardBoat
    waitmovement 0
    removeobject LOCALID_BRINEY_DEWFORD
    applymovement OBJ_EVENT_ID_PLAYER, DewfordTown_Movement_PlayerBoardBoat
    waitmovement 0
    hideobjectat OBJ_EVENT_ID_PLAYER, MAP_DEWFORD_TOWN
    call Common_EventScript_PlayBrineysBoatMusic
    applymovement LOCALID_BOAT_DEWFORD, DewfordTown_Movement_SailToSlateport
    applymovement OBJ_EVENT_ID_PLAYER, DewfordTown_Movement_SailToSlateport
    waitmovement 0
    call Common_EventScript_StopBrineysBoatMusic
    showobjectat OBJ_EVENT_ID_PLAYER, MAP_ROUTE109
    applymovement OBJ_EVENT_ID_PLAYER, DewfordTown_Movement_ExitBoatSlateport
    waitmovement 0
    setobjectxyperm LOCALID_BRINEY_R109, 21, 26
    addobject LOCALID_BRINEY_R109
    setobjectsubpriority LOCALID_BRINEY_R109, MAP_ROUTE109, 0
    applymovement LOCALID_BRINEY_R109, DewfordTown_Movement_BrineyExitBoat
    waitmovement 0
    clearflag FLAG_HIDE_ROUTE_109_MR_BRINEY
    addobject LOCALID_BOAT_R109
    clearflag FLAG_HIDE_ROUTE_109_MR_BRINEY_BOAT
    setflag FLAG_HIDE_MR_BRINEY_BOAT_DEWFORD_TOWN
    hideobjectat LOCALID_BOAT_DEWFORD, MAP_DEWFORD_TOWN
    call_if_unset FLAG_DELIVERED_DEVON_GOODS, DewfordTown_EventScript_LandedSlateportDeliverGoods
    call_if_set FLAG_DELIVERED_DEVON_GOODS, DewfordTown_EventScript_LandedSlateport
    closemessage
    copyvar VAR_BRINEY_LOCATION, VAR_0x8008
    resetobjectsubpriority OBJ_EVENT_ID_PLAYER, MAP_DEWFORD_TOWN
    resetobjectsubpriority LOCALID_BRINEY_R109, MAP_ROUTE109
    copyobjectxytoperm LOCALID_BRINEY_R109
    release
    end

DewfordTown_EventScript_LandedSlateportDeliverGoods::
    msgbox DewfordTown_Text_BrineyLandedInSlateportDeliverGoods, MSGBOX_DEFAULT
    return

DewfordTown_EventScript_LandedSlateport::
    msgbox DewfordTown_Text_BrineyLandedInSlateport, MSGBOX_DEFAULT
    return

DewfordTown_EventScript_TrendyPhraseBoy::
    lock
    faceplayer
    call Common_EventScript_BufferTrendyPhrase
    msgbox DewfordTown_Text_XIsTheBiggestHappeningThingRight, MSGBOX_YESNO
    goto_if_eq VAR_RESULT, YES, DewfordTown_EventScript_ConfirmTrendyPhrase
    goto_if_eq VAR_RESULT, NO, DewfordTown_EventScript_RejectTrendyPhrase
    end

DewfordTown_EventScript_ConfirmTrendyPhrase::
    msgbox DewfordTown_Text_YeahDefinitionOfInRightNow, MSGBOX_DEFAULT
    release
    end

DewfordTown_EventScript_RejectTrendyPhrase::
    msgbox DewfordTown_Text_TellMeWhatsNewAndIn, MSGBOX_DEFAULT
    setvar VAR_0x8004, EASY_CHAT_TYPE_TRENDY_PHRASE
    call Common_ShowEasyChatScreen
    lock
    faceplayer
    goto_if_eq VAR_RESULT, TRUE, DewfordTown_EventScript_GiveNewTrendyPhrase
    goto_if_eq VAR_RESULT, FALSE, DewfordTown_EventScript_CancelNewTrendyPhrase
    end

DewfordTown_EventScript_GiveNewTrendyPhrase::
    incrementgamestat GAME_STAT_STARTED_TRENDS
    goto_if_eq VAR_0x8004, FALSE, DewfordTown_EventScript_PhraseNotTrendyEnough
    msgbox DewfordTown_Text_OfCourseIKnowAboutThat, MSGBOX_DEFAULT
    release
    end

DewfordTown_EventScript_CancelNewTrendyPhrase::
    msgbox DewfordTown_Text_HearOfAnyTrendsComeShareWithMe, MSGBOX_DEFAULT
    release
    end

DewfordTown_EventScript_PhraseNotTrendyEnough::
    msgbox DewfordTown_Text_XHuhIThinkYIsCool, MSGBOX_DEFAULT
    release
    end
`

text DewfordTown_Text_TinyIslandCommunity {
    format("Dewford is a tiny island community.\n"
    "If something gets trendy here,\l"
    "everyone picks up on it right away.")
}

text DewfordTown_Text_TownSign {
    format("Dewford Town\n"
    "“A tiny island in the blue sea.”")
}

text DewfordTown_Text_GymSign {
    format("Dewford Town Pokémon Gym\n"
    "Leader: Brawly\l"
    "“A big wave in fighting!”")
}

text DewfordTown_Text_HallSign {
    format("Dewford Hall\n"
    "“Everyone's information exchange!”")
}

text Route104_Text_LandedInDewfordDeliverLetter {
    format("Ahoy!\n"
    "We've hit land in Dewford.\p"
    "I suppose you're off to deliver that Letter to, who was it now, Steven!")
}

text DewfordTown_Text_SetSailBackToPetalburg {
    format("Have you delivered your Letter?\p"
    "Or were you meaning to sail back to Petalburg?")
}

text DewfordTown_Text_PetalburgWereSettingSail2 {
    format("Petalburg it is, then!\p"
    "Anchors aweigh!\n"
    "Peeko, we're setting sail, my darling!")
}

text DewfordTown_Text_GoDeliverIllBeWaiting {
    format("Then you go on and deliver the Letter. I'll be waiting.")
}

text DewfordTown_Text_BrineyLandedInDewford {
    format("Ahoy!\n"
    "We've hit land in Dewford!\p"
    "You just go on and tell me whenever you want to set sail again!")
}

text DewfordTown_Text_WhereAreWeBound {
    format("Ahoy!\n"
    "For you, I'll go out to sea anytime!\p"
    "Now, my friend, where are we bound?")
}

text DewfordTown_Text_PetalburgWereSettingSail {
    format("Petalburg, is it?\p"
    "Anchors aweigh!\n"
    "Peeko, we're setting sail, my darling!")
}

text DewfordTown_Text_SlateportWereSettingSail {
    format("Slateport, is it?\p"
    "Anchors aweigh!\n"
    "Peeko, we're setting sail, my darling!")
}

text DewfordTown_Text_JustTellMeWhenYouNeedToSetSail {
    format("You just tell me whenever you need to set sail again!")
}

text DewfordTown_Text_GettingItchToFish {
    format("This is a renowned fishing spot.\n"
    "Are you getting the itch to fish?")
}

text DewfordTown_Text_GiveYouOneOfMyRods {
    format("I hear you, and I like what you're saying!\p"
    "I'll give you one of my fishing Rods.")
}

text DewfordTown_Text_ThrowInFishingAdvice {
    format("And, as an added bonus, I'll even throw in a little fishing advice!\p"
    "It's easier to catch fish if you're not standing near anybody.")
}

text DewfordTown_Text_ThatsTooBadThen {
    format("Oh, is that so?\n"
    "That's too bad, then.")
}

text DewfordTown_Text_HowsYourFishing {
    format("Yo!\n"
    "How's your fishing?")
}

text DewfordTown_Text_GreatHaulInSomeBigOnes {
    format("Is that right! That's great!\n"
    "Haul in some big ones!")
}

text DewfordTown_Text_FishingAdvice {
    format("Oh, hey, don't get down on yourself!\n"
    "I'll give you a little fishing advice.\p"
    "It's easier to catch fish if you're not standing near anybody.")
}

text DewfordTown_Text_XIsTheBiggestHappeningThingRight {
    format("I like what's hip, happening, and trendy.\n"
    "I'm always checking it out.\p"
    "Listen, have you heard about this new\n"
    "“{STR_VAR_1}”?\p"
    "That's right!\n"
    "Of course you know!\p"
    "I mean, sheesh,\n"
    "“{STR_VAR_1}”…\l"
    "It's the hottest thing in cool!\p"
    "Wherever you're from,\n"
    "“{STR_VAR_1}”\l"
    "is the biggest happening thing, right?")
}

text DewfordTown_Text_TellMeWhatsNewAndIn {
    format("Hunh?\n"
    "It's not the hip and happening thing?\p"
    "Well, hey, you have to tell me,\n"
    "what's new and what's “in”?")
}

text DewfordTown_Text_OfCourseIKnowAboutThat {
    format("Hunh?\n"
    "“{STR_VAR_2}”?\p"
    "… …\p"
    "…Uh… Yeah! That's right!\n"
    "Yeah, I knew that! Knew it all along!\p"
    "Of course I know about that!\n"
    "“{STR_VAR_2},” right?\p"
    "Yeah, that's it, it's there!\n"
    "Isn't “{STR_VAR_2}”\l"
    "the coolest, or what?\p"
    "It's the hippest thing in hip.\n"
    "You think I'd not know about it?\p"
    "“{STR_VAR_1}”…\n"
    "It's, like, so five minutes ago.\p"
    "Now, “{STR_VAR_2}” is\n"
    "what's vital and in tune with the times!")
}

text DewfordTown_Text_XHuhIThinkYIsCool {
    format("Hmm…\n"
    "“{STR_VAR_2},” huh?\p"
    "But personally, I think\n"
    "“{STR_VAR_1}”\l"
    "is what's real in cool.")
}

text DewfordTown_Text_HearOfAnyTrendsComeShareWithMe {
    format("Well, if you hear of any happening new trends, come share them with me, okay?")
}

text DewfordTown_Text_YeahDefinitionOfInRightNow {
    format("Yeah, absolutely right!\p"
    "“{STR_VAR_1}” is the\n"
    "definition of “in” right now.")
}

movement DewfordTown_Movement_SailToPetalburg {
    walk_up * 2
    walk_fast_up * 15
    walk_up * 2
    walk_left * 2
    walk_fast_left * 4
    walk_faster_left * 40
    walk_fast_left * 4
    walk_fast_up * 4
    walk_faster_up * 83
    walk_fast_up * 4
    walk_fast_left * 2
    walk_faster_left * 6
    walk_fast_left * 2
    walk_fast_up * 2
    walk_faster_up * 14
    walk_fast_up * 4
    walk_up * 4
}

movement DewfordTown_Movement_SailToSlateport {
    walk_right
    walk_fast_right * 2
    walk_faster_right * 144
    walk_fast_right * 2
    walk_fast_up * 2
    walk_faster_up * 10
    walk_fast_up * 10
}

movement DewfordTown_Movement_PlayerBoardBoat {
    walk_right
    walk_up
}

movement DewfordTown_Movement_ExitBoatPetalburg {
    walk_up * 3
}

movement DewfordTown_Movement_ExitBoatSlateport {
    walk_up * 3
    walk_in_place_faster_down
}

movement DewfordTown_Movement_BrineyBoardBoat { walk_up }

movement DewfordTown_Movement_BrineyExitBoat { walk_up * 2 }
