mapscripts EverGrandeCity_HallOfFame_MapScripts {
    MAP_SCRIPT_ON_WARP_INTO_MAP_TABLE   [ VAR_TEMP_1, 0: EverGrandeCity_HallOfFame_EventScript_TurnPlayerNorth ]
    MAP_SCRIPT_ON_FRAME_TABLE           [ VAR_TEMP_1, 0: EverGrandeCity_HallOfFame_EventScript_EnterHallOfFame ]
}

raw `
.set LOCALID_WALLACE, 1


EverGrandeCity_HallOfFame_EventScript_TurnPlayerNorth::
    turnobject OBJ_EVENT_ID_PLAYER, DIR_NORTH
    end

EverGrandeCity_HallOfFame_EventScript_EnterHallOfFame::
    lockall
    applymovement LOCALID_WALLACE, EverGrandeCity_HallOfFame_Movement_WalkIntoHallOfFame1
    applymovement OBJ_EVENT_ID_PLAYER, EverGrandeCity_HallOfFame_Movement_WalkIntoHallOfFame1
    waitmovement 0
    applymovement LOCALID_WALLACE, Common_Movement_WalkInPlaceFasterRight
    applymovement OBJ_EVENT_ID_PLAYER, Common_Movement_WalkInPlaceFasterLeft
    waitmovement 0
    speakername CommonName_Text_Wallace
    msgbox EverGrandeCity_HallOfFame_Text_HereWeHonorLeagueChampions, MSGBOX_DEFAULT
    closemessage
    applymovement LOCALID_WALLACE, EverGrandeCity_HallOfFame_Movement_WalkIntoHallOfFame2
    applymovement OBJ_EVENT_ID_PLAYER, EverGrandeCity_HallOfFame_Movement_WalkIntoHallOfFame2
    waitmovement 0
    delay 20
    applymovement LOCALID_WALLACE, Common_Movement_WalkInPlaceFasterRight
    applymovement OBJ_EVENT_ID_PLAYER, Common_Movement_WalkInPlaceFasterLeft
    waitmovement 0
    msgbox EverGrandeCity_HallOfFame_Text_LetsRecordYouAndYourPartnersNames, MSGBOX_DEFAULT
    closemessage
    applymovement LOCALID_WALLACE, Common_Movement_WalkInPlaceFasterUp
    applymovement OBJ_EVENT_ID_PLAYER, Common_Movement_WalkInPlaceFasterUp
    waitmovement 0
    delay 20
    dofieldeffect FLDEFF_HALL_OF_FAME_RECORD
    waitfieldeffect FLDEFF_HALL_OF_FAME_RECORD
    delay 40
    setvar VAR_TEMP_1, 1
    call EverGrandeCity_HallOfFame_EventScript_SetGameClearFlags
    checkplayergender
    goto_if_eq VAR_RESULT, MALE, EverGrandeCity_HallOfFame_EventScript_GameClearMale
    goto_if_eq VAR_RESULT, FEMALE, EverGrandeCity_HallOfFame_EventScript_GameClearFemale
    end

EverGrandeCity_HallOfFame_EventScript_GameClearMale::
    setrespawn HEAL_LOCATION_LITTLEROOT_TOWN_BRENDANS_HOUSE_2F
    fadescreenspeed FADE_TO_BLACK, 24
    special GameClear
    waitstate
    releaseall
    end

EverGrandeCity_HallOfFame_EventScript_GameClearFemale::
    setrespawn HEAL_LOCATION_LITTLEROOT_TOWN_MAYS_HOUSE_2F
    fadescreenspeed FADE_TO_BLACK, 24
    special GameClear
    waitstate
    releaseall
    end

EverGrandeCity_HallOfFame_Movement_WalkIntoHallOfFame1:
    walk_up
    walk_up
    walk_up
    walk_up
    walk_up
    walk_up
    step_end

EverGrandeCity_HallOfFame_Movement_WalkIntoHallOfFame2:
    walk_up
    walk_up
    walk_up
    walk_up
    walk_up
    step_end
`

text EverGrandeCity_HallOfFame_Text_HereWeHonorLeagueChampions {
    format("This room…\p"
    "This is where we keep records of Pokémon that prevailed through harsh battles.\p"
    "It is here that the League Champions are honored.")
}

text EverGrandeCity_HallOfFame_Text_LetsRecordYouAndYourPartnersNames {
    format("Come on, let's record your name as a Trainer who triumphed over the Pokémon League, and the names of the partners who battled with you.")
}

