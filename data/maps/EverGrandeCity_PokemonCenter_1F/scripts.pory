mapscripts EverGrandeCity_PokemonCenter_1F_MapScripts {
    MAP_SCRIPT_ON_TRANSITION {
        setrespawn(HEAL_LOCATION_EVER_GRANDE_CITY)
        call_if_unset(FLAG_MET_SCOTT_IN_EVERGRANDE, EverGrandeCity_PokemonCenter_1F_EventScript_TryShowScott)
    }
    MAP_SCRIPT_ON_RESUME: CableClub_OnResume
}

raw `
.set LOCALID_NURSE, 1
.set LOCALID_SCOTT, 4

EverGrandeCity_PokemonCenter_1F_EventScript_TryShowScott::
    goto_if_unset FLAG_BADGE06_GET, Common_EventScript_NopReturn
    clearflag FLAG_HIDE_EVER_GRANDE_POKEMON_CENTER_1F_SCOTT
    return

EverGrandeCity_PokemonCenter_1F_EventScript_Nurse::
    setvar VAR_0x800B, LOCALID_NURSE
    call Common_EventScript_PkmnCenterNurse
    waitmessage
    waitbuttonpress
    release
    end

EverGrandeCity_PokemonCenter_1F_EventScript_Scott::
    lock
    faceplayer
    speakername CommonName_Text_Scott
    msgbox EverGrandeCity_PokemonCenter_1F_Text_ScottHappyForYou, MSGBOX_DEFAULT
    closemessage
    call_if_eq VAR_FACING, DIR_NORTH, EverGrandeCity_PokemonCenter_1F_EventScript_ScottExitNorth
    call_if_eq VAR_FACING, DIR_EAST, EverGrandeCity_PokemonCenter_1F_EventScript_ScottExit
    call_if_eq VAR_FACING, DIR_WEST, EverGrandeCity_PokemonCenter_1F_EventScript_ScottExit
    addvar VAR_SCOTT_STATE, 1
    setflag FLAG_MET_SCOTT_IN_EVERGRANDE
    playse SE_EXIT
    waitse
    removeobject LOCALID_SCOTT
    release
    end

EverGrandeCity_PokemonCenter_1F_EventScript_ScottExitNorth::
    applymovement LOCALID_SCOTT, EverGrandeCity_PokemonCenter_1F_Movement_ScottExitNorth
    waitmovement 0
    return

EverGrandeCity_PokemonCenter_1F_EventScript_ScottExit::
    applymovement LOCALID_SCOTT, EverGrandeCity_PokemonCenter_1F_Movement_ScottExit
    waitmovement 0
    return
`

movement EverGrandeCity_PokemonCenter_1F_Movement_ScottExitNorth {
    walk_left
    walk_down * 2
    walk_left
    walk_down * 2
}

movement EverGrandeCity_PokemonCenter_1F_Movement_ScottExit {
    walk_down * 2
    walk_left * 2
    walk_down * 2
}

script EverGrandeCity_PokemonCenter_1F_EventScript_Woman {
    msgbox(format("The Pokémon League is only a short distance after the Victory Road.\p"
        "If you've come this far, what choice do you have but to keep going?"), MSGBOX_NPC)
}

script EverGrandeCity_PokemonCenter_1F_EventScript_ExpertM {
    msgbox(format("The long and harrowing Victory road…\p"
        "It's like reliving the path one has traveled in life…\p"
        "Believe in your Pokémon and give it your very best!"), MSGBOX_NPC)
}

text EverGrandeCity_PokemonCenter_1F_Text_ScottHappyForYou {
    format("{PLAYER}{KUN}, you've clawed your way up to face the Pokémon League!\p"
    "I'm happy for you!\n"
    "You made my cheering worthwhile!\p"
    "{PLAYER}{KUN}, if you were to become the Pokémon League Champion…\p"
    "I'll get in touch with you then.\p"
    "Okay, {PLAYER}{KUN}.\n"
    "Go for greatness!")
}

