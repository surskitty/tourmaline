mapscripts FallarborTown_Mart_MapScripts {}

script FallarborTown_Mart_EventScript_Clerk {
    lock
    faceplayer
    message(gText_HowMayIServeYou)
    waitmessage
    pokemart(FallarborTown_Mart_Pokemart)
    msgbox(gText_PleaseComeAgain, MSGBOX_DEFAULT)
    release
}

mart FallarborTown_Mart_Pokemart {
    ITEM_GREAT_BALL
    ITEM_SUPER_POTION
    ITEM_ANTIDOTE
    ITEM_PARALYZE_HEAL
    ITEM_SUPER_REPEL
    ITEM_X_SP_ATK
    ITEM_X_SPEED
    ITEM_X_ATTACK
    ITEM_X_DEFENSE
    ITEM_DIRE_HIT
    ITEM_GUARD_SPEC
}

script FallarborTown_Mart_EventScript_Woman {
    msgbox(format("I'm having a hard time deciding if I should make my Skitty evolve or not.\p"
        "I only have to use this Moon Stone, but it's so hard to decide…\p"
        "If I make it evolve, it will become much stronger.\p"
        "But it will look so different, too."), MSGBOX_NPC)
}

script FallarborTown_Mart_EventScript_PokefanM {
    msgbox(format("This Nugget I found here…\n"
    "I suppose I'll have to sell it, seeing as how it has no other use."), MSGBOX_NPC)
}

script FallarborTown_Mart_EventScript_Skitty {
    lock
    faceplayer
    waitse
    playmoncry(SPECIES_SKITTY, CRY_MODE_NORMAL)
    msgbox("SKITTY: Miyao?", MSGBOX_DEFAULT)
    waitmoncry
    release
}

script FallarborTown_Mart_EventScript_MetronomeTutor {
	lock
	faceplayer
	msgbox(MoveTutor_Text_MetronomeTeach, MSGBOX_YESNO)
	goto_if_eq(VAR_RESULT, NO, FallarborTown_Mart_EventScript_TutorDeclined)
	msgbox(FallarborTown_Mart_Text_WhichMon, MSGBOX_DEFAULT)
	setvar(VAR_0x8005, MOVE_METRONOME)
	call(MoveTutor_EventScript_OpenPartyMenu)
	goto_if_eq(VAR_RESULT, 0, FallarborTown_Mart_EventScript_TutorDeclined)
	goto(MoveTutor_EventScript_MetronomeTaught)
}

script FallarborTown_Mart_EventScript_TutorDeclined {
	msgbox(format("Okay. I'll be here if you change your mind."), MSGBOX_DEFAULT)
	release
}

script MoveTutor_EventScript_MetronomeTaught {
	msgbox(format("When a Pokémon waggles its finger like a Metronome, all sorts of nice things happen.\p"
    "Wouldn't it be nice if we could use it, too?"), MSGBOX_DEFAULT)
	release
}

text MoveTutor_Text_MetronomeTeach {
    format("I want all sorts of things!\n"
    "But I used up my allowance…\p"
    "Wouldn't it be nice if there were a spell that made money appear when you waggle a finger?\p"
    "If you want, I can teach your Pokémon the move Metronome.\p"
    "Money won't appear, but your Pokémon will waggle a finger. Yes?")
}

text FallarborTown_Mart_Text_WhichMon {
    format("Okay! I'll teach it!\n"
    "Which Pokémon should I teach?")
}
