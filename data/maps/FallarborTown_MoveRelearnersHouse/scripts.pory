mapscripts FallarborTown_MoveRelearnersHouse_MapScripts {}

raw `
.set LOCALID_MOVE_RELEARNER, 1

FallarborTown_MoveRelearnersHouse_EventScript_MoveRelearner::
    lockall
    applymovement LOCALID_MOVE_RELEARNER, Common_Movement_FacePlayer
    waitmovement 0
    setflag FLAG_EGG_MOVES_TUTOR
    goto_if_set FLAG_TEMP_1, FallarborTown_MoveRelearnersHouse_EventScript_AskTeachMove
    msgbox FallarborTown_MoveRelearnersHouse_Text_ImTheMoveTutor, MSGBOX_DEFAULT
    setflag FLAG_TEMP_1
    goto FallarborTown_MoveRelearnersHouse_EventScript_AskTeachMove
    end

FallarborTown_MoveRelearnersHouse_EventScript_AskTeachMove::
    msgbox FallarborTown_MoveRelearnersHouse_Text_ThatsAHeartScaleWantMeToTeachMove, MSGBOX_YESNO
    switch VAR_RESULT
    case NO, FallarborTown_MoveRelearnersHouse_EventScript_ComeBackAnytime
    goto FallarborTown_MoveRelearnersHouse_EventScript_ChooseMon
    end

FallarborTown_MoveRelearnersHouse_EventScript_ChooseMon::
    msgbox FallarborTown_MoveRelearnersHouse_Text_TutorWhichMon, MSGBOX_DEFAULT
    special ChooseMonForMoveRelearner
    waitstate
    goto_if_eq VAR_0x8004, PARTY_NOTHING_CHOSEN, FallarborTown_MoveRelearnersHouse_EventScript_ComeBackAnytime
    special IsSelectedMonEgg
    goto_if_eq VAR_RESULT, TRUE, FallarborTown_MoveRelearnersHouse_EventScript_CantTeachEgg
    goto_if_eq VAR_0x8005, 0, FallarborTown_MoveRelearnersHouse_EventScript_NoMoveToTeachMon
    goto FallarborTown_MoveRelearnersHouse_EventScript_ChooseMove
    end

FallarborTown_MoveRelearnersHouse_EventScript_ChooseMove::
    msgbox FallarborTown_MoveRelearnersHouse_Text_TeachWhichMove, MSGBOX_DEFAULT
    special TeachMoveRelearnerMove
    waitstate
    goto_if_eq VAR_0x8004, 0, FallarborTown_MoveRelearnersHouse_EventScript_ChooseMon
    goto FallarborTown_MoveRelearnersHouse_EventScript_ComeBackAnytime
    end

FallarborTown_MoveRelearnersHouse_EventScript_NoMoveToTeachMon::
    msgbox FallarborTown_MoveRelearnersHouse_Text_DontHaveMoveToTeachPokemon, MSGBOX_DEFAULT
    goto FallarborTown_MoveRelearnersHouse_EventScript_ChooseMon
    end

FallarborTown_MoveRelearnersHouse_EventScript_CantTeachEgg::
    msgbox FallarborTown_MoveRelearnersHouse_Text_CantTeachEgg, MSGBOX_DEFAULT
    goto FallarborTown_MoveRelearnersHouse_EventScript_ChooseMon
    end

FallarborTown_MoveRelearnersHouse_EventScript_ComeBackAnytime::
    clearflag FLAG_EGG_MOVES_TUTOR
    msgbox FallarborTown_MoveRelearnersHouse_Text_ComeBackWithHeartScale, MSGBOX_DEFAULT
    releaseall
    end
`
text FallarborTown_MoveRelearnersHouse_Text_ImTheMoveTutor {
    format("I'm the Egg Move Tutor.\p"
    "I know all the moves that Pokémon\n"
    "learn--every one of them--and I can\l"
    "teach Pokémon those moves.\p"
    "I can teach a move to a Pokémon of yours if you'd like.")
}

text FallarborTown_MoveRelearnersHouse_Text_ThatsAHeartScaleWantMeToTeachMove {
    format("Let me guess, you want me to teach a move?")
}

text FallarborTown_MoveRelearnersHouse_Text_TutorWhichMon {
    format("Which Pokémon needs tutoring?")
}

text FallarborTown_MoveRelearnersHouse_Text_TeachWhichMove {
    format("Which move should I teach?")
}

text FallarborTown_MoveRelearnersHouse_Text_DontHaveMoveToTeachPokemon {
    format("Sorry…\p"
    "It doesn't appear as if I have any move I can teach that Pokémon.")
}

text FallarborTown_MoveRelearnersHouse_Text_ComeBackWithHeartScale {
    format("If your Pokémon need to learn a move, I'll still be here.")
}

text FallarborTown_MoveRelearnersHouse_Text_CantTeachEgg {
    format("Hunh? There isn't a single move that I can teach an Egg.")
}
