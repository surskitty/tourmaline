mapscripts FortreeCity_House2_MapScripts {}

raw `
FortreeCity_House2_EventScript_HiddenPowerGiver::
    lock
    faceplayer
    goto_if_set FLAG_RECEIVED_TM_HIDDEN_POWER, FortreeCity_House2_EventScript_ExplainHiddenPower
    call_if_unset FLAG_MET_HIDDEN_POWER_GIVER, FortreeCity_House2_EventScript_Greeting
    msgbox FortreeCity_House2_Text_CoinInWhichHand, MSGBOX_DEFAULT
    multichoice 21, 8, MULTI_RIGHTLEFT, TRUE
    switch VAR_RESULT
    case 1, FortreeCity_House2_EventScript_WrongGuess
    msgbox FortreeCity_House2_Text_CorrectTryAgainWhichHand, MSGBOX_DEFAULT
    multichoice 21, 8, MULTI_RIGHTLEFT, TRUE
    switch VAR_RESULT
    case 1, FortreeCity_House2_EventScript_WrongGuess
    msgbox FortreeCity_House2_Text_CorrectTryAgainWhichHand2, MSGBOX_DEFAULT
    multichoice 21, 8, MULTI_RIGHTLEFT, TRUE
    switch VAR_RESULT
    case 0, FortreeCity_House2_EventScript_WrongGuess
    msgbox FortreeCity_House2_Text_YourHiddenPowerHasAwoken, MSGBOX_DEFAULT
    giveitem ITEM_TM_HIDDEN_POWER
    goto_if_eq VAR_RESULT, 0, Common_EventScript_ShowBagIsFull
    setflag FLAG_RECEIVED_TM_HIDDEN_POWER
    msgbox FortreeCity_House2_Text_ExplainHiddenPower, MSGBOX_DEFAULT
    release
    end

FortreeCity_House2_EventScript_Greeting::
    msgbox FortreeCity_House2_Text_HiddenPowersArousedByNature, MSGBOX_DEFAULT
    setflag FLAG_MET_HIDDEN_POWER_GIVER
    return

FortreeCity_House2_EventScript_ExplainHiddenPower::
    msgbox FortreeCity_House2_Text_ExplainHiddenPower, MSGBOX_DEFAULT
    release
    end

FortreeCity_House2_EventScript_WrongGuess::
    msgbox FortreeCity_House2_Text_YouGuessedWrong, MSGBOX_DEFAULT
    release
    end
`

text FortreeCity_House2_Text_HiddenPowersArousedByNature {
    format("People… Pokémon…\p"
    "Their hidden powers are aroused by living in natural environments…")
}

text FortreeCity_House2_Text_CoinInWhichHand {
    format("Let this old woman see if your hidden power has awoken…\p"
    "I hold a coin in my hand.\p"
    "Now, tell me, have I palmed it in the right hand? Or in the left?")
}

text FortreeCity_House2_Text_CorrectTryAgainWhichHand {
    format("Oh! Yes, correct!\p"
    "We shall try again.\p"
    "In which hand have I palmed the coin?\n"
    "The right or left?")
}

text FortreeCity_House2_Text_CorrectTryAgainWhichHand2 {
    format("Oh! Yes, correct!\p"
    "We shall try again.\p"
    "In which hand have I palmed the coin?\n"
    "The right or left?")
}

text FortreeCity_House2_Text_YourHiddenPowerHasAwoken {
    format("Oh! Splendid!\n"
    "Your hidden power has awoken!\p"
    "Here, take this and awaken the hidden power of your Pokémon.")
}

text FortreeCity_House2_Text_ExplainHiddenPower {
    format("Hidden Power is a move that changes with the Pokémon.")
}

text FortreeCity_House2_Text_YouGuessedWrong {
    format("No, too bad.\n"
    "You guessed wrong.")
}

