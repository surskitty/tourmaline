mapscripts JaggedPass_MapScripts {
    MAP_SCRIPT_ON_RESUME {
        setstepcallback(STEP_CB_ASH)
        call_if_eq(VAR_JAGGED_PASS_STATE, 0, JaggedPass_EventScript_CheckHasMagmaEmblem)
    }
    MAP_SCRIPT_ON_TRANSITION {
        call_if_eq(VAR_JAGGED_PASS_ASH_WEATHER, 1, JaggedPass_EventScript_SetWeatherAsh)
    }
    MAP_SCRIPT_ON_LOAD {
        goto_if_le(VAR_JAGGED_PASS_STATE, 1, JaggedPass_EventScript_ConcealHideoutEntrance)
        special(SetRoofBirds)
        special(SetStationaryWilds)
    }
}

raw `
.set LOCALID_HIDEOUT_GUARD, 5

JaggedPass_EventScript_CheckHasMagmaEmblem::
	checkitem ITEM_MAGMA_EMBLEM
	goto_if_eq VAR_RESULT, TRUE, JaggedPass_EventScript_SetReadyToOpenHideout
	return

JaggedPass_EventScript_SetReadyToOpenHideout::
	setvar VAR_JAGGED_PASS_STATE, 1
	return

JaggedPass_EventScript_SetWeatherAsh::
	setweather WEATHER_VOLCANIC_ASH
	doweather
	return

JaggedPass_EventScript_ConcealHideoutEntrance::
	setmetatile 16, 17, METATILE_Lavaridge_RockWall, TRUE
	setmetatile 16, 18, METATILE_Lavaridge_RockWall, TRUE
	end

JaggedPass_EventScript_OpenMagmaHideout::
	lockall
	setvar VAR_0x8004, 1  @ vertical pan
	setvar VAR_0x8005, 1  @ horizontal pan
	setvar VAR_0x8006, 8  @ num shakes
	setvar VAR_0x8007, 5  @ shake delay
	special ShakeCamera
	waitstate
	msgbox JaggedPass_Text_BoulderShakingInResponseToEmblem, MSGBOX_DEFAULT
	closemessage
	setvar VAR_0x8004, 1   @ vertical pan
	setvar VAR_0x8005, 1   @ horizontal pan
	setvar VAR_0x8006, 16  @ num shakes
	setvar VAR_0x8007, 3   @ shake delay
	special ShakeCamera
	waitstate
	playse SE_EFFECTIVE
	setmetatile 16, 17, METATILE_Lavaridge_CaveEntrance_Top, TRUE
	setmetatile 16, 18, METATILE_Lavaridge_CaveEntrance_Bottom, FALSE
	special DrawWholeMapView
	delay 30
	setvar VAR_JAGGED_PASS_STATE, 2
	waitse
	releaseall
	end

JaggedPass_EventScript_MagmaHideoutGuard::
	lockall
	goto_if_set FLAG_BEAT_MAGMA_GRUNT_JAGGED_PASS, JaggedPass_EventScript_GuardDefeated
	waitse
	playse SE_PIN
	applymovement LOCALID_HIDEOUT_GUARD, Common_Movement_ExclamationMark
	waitmovement 0
	applymovement LOCALID_HIDEOUT_GUARD, Common_Movement_Delay48
	waitmovement 0
	applymovement LOCALID_HIDEOUT_GUARD, Common_Movement_FacePlayer
	waitmovement 0
	msgbox JaggedPass_Text_GruntIntro, MSGBOX_DEFAULT
	closemessage
	trainerbattle_no_intro TRAINER_GRUNT_JAGGED_PASS, JaggedPass_Text_GruntDefeat
	setflag FLAG_BEAT_MAGMA_GRUNT_JAGGED_PASS
	applymovement LOCALID_HIDEOUT_GUARD, Common_Movement_FaceOriginalDirection
	waitmovement 0
	releaseall
	end

JaggedPass_EventScript_GuardDefeated::
	applymovement LOCALID_HIDEOUT_GUARD, Common_Movement_FacePlayer
	waitmovement 0
	msgbox JaggedPass_Text_GoWhereverYouWant, MSGBOX_DEFAULT
	closemessage
	applymovement LOCALID_HIDEOUT_GUARD, Common_Movement_FaceOriginalDirection
	waitmovement 0
	releaseall
	end

JaggedPass_EventScript_Eric::
	trainerbattle_single TRAINER_ERIC, JaggedPass_Text_EricIntro, JaggedPass_Text_EricDefeat
	msgbox JaggedPass_Text_EricPostBattle, MSGBOX_AUTOCLOSE
	end

JaggedPass_EventScript_Diana::
	trainerbattle_single TRAINER_DIANA_1, JaggedPass_Text_DianaIntro, JaggedPass_Text_DianaDefeat, JaggedPass_EventScript_RegisterDiana
	specialvar VAR_RESULT, ShouldTryRematchBattle
	goto_if_eq VAR_RESULT, TRUE, JaggedPass_EventScript_DianaRematch
	msgbox JaggedPass_Text_DianaPostBattle, MSGBOX_DEFAULT
	release
	end

JaggedPass_EventScript_RegisterDiana::
	special PlayerFaceTrainerAfterBattle
	msgbox JaggedPass_Text_DianaRegister, MSGBOX_DEFAULT
	register_matchcall TRAINER_DIANA_1
	release
	end

JaggedPass_EventScript_DianaRematch::
	trainerbattle_rematch TRAINER_DIANA_1, JaggedPass_Text_DianaRematchIntro, JaggedPass_Text_DianaRematchDefeat
	msgbox JaggedPass_Text_DianaPostRematch, MSGBOX_AUTOCLOSE
	end

JaggedPass_EventScript_Ethan::
	trainerbattle_single TRAINER_ETHAN_1, JaggedPass_Text_EthanIntro, JaggedPass_Text_EthanDefeat, JaggedPass_EventScript_RegisterEthan
	specialvar VAR_RESULT, ShouldTryRematchBattle
	goto_if_eq VAR_RESULT, TRUE, JaggedPass_EventScript_EthanRematch
	msgbox JaggedPass_Text_EthanPostBattle, MSGBOX_DEFAULT
	release
	end

JaggedPass_EventScript_RegisterEthan::
	special PlayerFaceTrainerAfterBattle
	msgbox JaggedPass_Text_EthanRegister, MSGBOX_DEFAULT
	register_matchcall TRAINER_ETHAN_1
	release
	end

JaggedPass_EventScript_EthanRematch::
	trainerbattle_rematch TRAINER_ETHAN_1, JaggedPass_Text_EthanRematchIntro, JaggedPass_Text_EthanRematchDefeat
	msgbox JaggedPass_Text_EthanPostRematch, MSGBOX_AUTOCLOSE
	end

JaggedPass_EventScript_Julio::
	trainerbattle_single TRAINER_JULIO, JaggedPass_Text_JulioIntro, JaggedPass_Text_JulioDefeat
	msgbox JaggedPass_Text_JulioPostBattle, MSGBOX_AUTOCLOSE
	end

JaggedPass_EventScript_Autumn::
	trainerbattle_single TRAINER_AUTUMN, JaggedPass_Text_AutumnIntro, JaggedPass_Text_AutumnDefeat
	msgbox JaggedPass_Text_AutumnPostBattle, MSGBOX_AUTOCLOSE
	end
`

text JaggedPass_Text_EricIntro {
	format("Mt. Chimney's Jagged Pass…\p"
	"Now this is what I've always wanted\n"
	"in a mountain.\p"
	"This jagged bumpiness…\n"
	"It rocks my soul!")
}

text JaggedPass_Text_EricDefeat {
	format("Losing left me bitter!")
}

text JaggedPass_Text_EricPostBattle {
	format("Yes, I did lose at Pokémon…\p"
	"But, when it comes to the love of\n"
	"the mountains, I have you beat!")
}

text JaggedPass_Text_DianaIntro {
	format("This place isn't your casual hike.\n"
	"It's not suited for a picnic.")
}

text JaggedPass_Text_DianaDefeat {
	format("Ohhh, no!\n"
	"The ground is too bumpy…")
}

text JaggedPass_Text_DianaPostBattle {
	format("Did you know?\p"
	"Some people cleverly ride their bicycles up this horribly bumpy pass.")
}

text JaggedPass_Text_DianaRegister {
	format("Will you ever be back in this area?\n"
	"If you do return, I'd like a rematch.")
}

text JaggedPass_Text_DianaRematchIntro {
	format("Picnics are fun wherever you go.\n"
	"Just like Pokémon!")
}

text JaggedPass_Text_DianaRematchDefeat {
	format("I only lost because the ground is too bumpy!")
}

text JaggedPass_Text_DianaPostRematch {
	format("I'll forget about losing and just enjoy this bumpy hike.")
}

text JaggedPass_Text_EthanIntro {
	format("Jagged Pass is hard to walk on.\n"
	"It's a good place for training.")
}

text JaggedPass_Text_EthanDefeat {
	format("It was all over while we were still trying to find a good footing…")
}

text JaggedPass_Text_EthanPostBattle {
	format("If I had an Acro Bike, I'd be able to jump ledges.")
}

text JaggedPass_Text_EthanRegister {
	format("When I get more used to this bumpiness, I'll be sure to win!\p"
	"Can you register me in your Pokénav?")
}

text JaggedPass_Text_EthanRematchIntro {
	format("I got used to this bumpiness.\n"
	"I sing while I climb now.")
}

text JaggedPass_Text_EthanRematchDefeat {
	format("It's still not easy to battle on this bumpy ground…")
}

text JaggedPass_Text_EthanPostRematch {
	format("I should get an Acro Bike from Rydel in Mauville City…")
}

text JaggedPass_Text_GruntIntro {
	format("Wah!\n"
	"What are you doing here?\p"
	"What am I doing in a place like this?\p"
	"What business is it of yours?")
}

text JaggedPass_Text_GruntDefeat {
	format("Urrrgh…\p"
	"I should've ducked into our Hideout right away…")
}

text JaggedPass_Text_GoWhereverYouWant {
	format("Okay, oh-kay!\n"
	"I admit it--you're strong!\p"
	"Don't worry about me.\n"
	"Go wherever you want!")
}

text JaggedPass_Text_BoulderShakingInResponseToEmblem {
	format("Oh! This boulder is shaking in response to the Magma Emblem!")
}

text JaggedPass_Text_JulioIntro {
	format("Aiyeeh! It's awfully scary to shoot down the mountain in one go!")
}

text JaggedPass_Text_JulioDefeat {
	format("I feel like I'm falling apart…")
}

text JaggedPass_Text_JulioPostBattle {
	format("My bicycle bounced around so much, my rear end's sore…")
}

text JaggedPass_Text_AutumnIntro {
	format("I climb this hill every day.\n"
	"I have confidence in my strength!")
}

text JaggedPass_Text_AutumnDefeat {
	format("Hmm…\n"
	"What went wrong?")
}

text JaggedPass_Text_AutumnPostBattle {
	format("What is that odd rock protrusion a little up the hill from here?")
}

