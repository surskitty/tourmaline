mapscripts LavaridgeTown_MapScripts {
    MAP_SCRIPT_ON_TRANSITION {
        setflag(FLAG_VISITED_LAVARIDGE_TOWN)
        call_if_set(FLAG_WHITEOUT_TO_LAVARIDGE, LavaridgeTown_EventScript_ClearLavaridgeWhiteOut)
        call_if_set(FLAG_DEFEATED_EVIL_TEAM_MT_CHIMNEY, LavaridgeTown_EventScript_ShowMtChimneyTrainers)
        call(Common_EventScript_SetupRivalGfxId)
        call(Common_EventScript_SetupRivalOnBikeGfxId)
        call_if_eq(VAR_LAVARIDGE_TOWN_STATE, 1, LavaridgeTown_EventScript_CheckSetRivalPos)
        call_if_eq(VAR_LAVARIDGE_TOWN_STATE, 1, LavaridgeTown_EventScript_HideMapNamePopup)
    }
    MAP_SCRIPT_ON_FRAME_TABLE [
        VAR_LAVARIDGE_TOWN_STATE, 1: LavaridgeTown_EventScript_RivalGiveGoGoggles
    ]
    MAP_SCRIPT_ON_LOAD {
        special(SetRoofBirds)
    }
}

raw `
.set LOCALID_RIVAL_ON_BIKE, 7
.set LOCALID_RIVAL, 8

LavaridgeTown_EventScript_ClearLavaridgeWhiteOut::
	clearflag FLAG_WHITEOUT_TO_LAVARIDGE
	return

LavaridgeTown_EventScript_CheckSetRivalPos::
	getplayerxy VAR_0x8004, VAR_0x8005
	goto_if_eq VAR_0x8004, 9, LavaridgeTown_EventScript_SetRivalPos
	return

LavaridgeTown_EventScript_SetRivalPos::
	setobjectxyperm LOCALID_RIVAL, 11, 9
	setobjectxyperm LOCALID_RIVAL_ON_BIKE, 9, 8
	setobjectmovementtype LOCALID_RIVAL_ON_BIKE, MOVEMENT_TYPE_FACE_UP
	clearflag FLAG_HIDE_LAVARIDGE_TOWN_RIVAL
	return

LavaridgeTown_EventScript_ShowMtChimneyTrainers::
	clearflag FLAG_HIDE_MT_CHIMNEY_TRAINERS
	return

LavaridgeTown_EventScript_HideMapNamePopup::
	setflag FLAG_HIDE_MAP_NAME_POPUP
	return

LavaridgeTown_EventScript_RivalGiveGoGoggles::
	lockall
	getplayerxy VAR_0x8008, VAR_0x8009
	call_if_eq VAR_0x8008, 9, LavaridgeTown_EventScript_RivalNoticePlayer
	call_if_ne VAR_0x8008, 9, LavaridgeTown_EventScript_RivalExitHerbShop
	delay 20
	checkplayergender
	call_if_eq VAR_RESULT, MALE, LavaridgeTown_EventScript_PlayMayMusic
	call_if_eq VAR_RESULT, FEMALE, LavaridgeTown_EventScript_PlayBrendanMusic
	call_if_eq VAR_0x8008, 9, LavaridgeTown_EventScript_RivalApproachPlayer1
	call_if_ne VAR_0x8008, 9, LavaridgeTown_EventScript_RivalApproachPlayer2
	checkplayergender
	goto_if_eq VAR_RESULT, MALE, LavaridgeTown_EventScript_MayGiveGoGoggles
	goto_if_eq VAR_RESULT, FEMALE, LavaridgeTown_EventScript_BrendanGiveGoGoggles
	end

LavaridgeTown_EventScript_MayGiveGoGoggles::
	speakername CommonName_Text_May
	msgbox LavaridgeTown_Text_MayNiceBadgesTakeThis, MSGBOX_DEFAULT
	giveitem ITEM_GO_GOGGLES
	setflag FLAG_RECEIVED_GO_GOGGLES
	msgbox LavaridgeTown_Text_MayExplainGoGogglesChallengeDad, MSGBOX_DEFAULT
	goto LavaridgeTown_EventScript_RivalExit
	end

LavaridgeTown_EventScript_BrendanGiveGoGoggles::
	speakername CommonName_Text_Brendan
	msgbox LavaridgeTown_Text_BrendanNiceBadgesTakeThis, MSGBOX_DEFAULT
	giveitem ITEM_GO_GOGGLES
	setflag FLAG_RECEIVED_GO_GOGGLES
	msgbox LavaridgeTown_Text_BrendanExplainGoGogglesChallengeDad, MSGBOX_DEFAULT
	goto LavaridgeTown_EventScript_RivalExit
	end

LavaridgeTown_EventScript_RivalExit::
	closemessage
	removeobject LOCALID_RIVAL
	addobject LOCALID_RIVAL_ON_BIKE
	delay 30
	call_if_eq VAR_0x8008, 9, LavaridgeTown_EventScript_RivalExit1
	call_if_ne VAR_0x8008, 9, LavaridgeTown_EventScript_RivalExit2
	removeobject LOCALID_RIVAL_ON_BIKE
	setvar VAR_LAVARIDGE_TOWN_STATE, 2
	clearflag FLAG_HIDE_MAP_NAME_POPUP
	savebgm MUS_DUMMY
	fadedefaultbgm
	releaseall
	end

LavaridgeTown_EventScript_PlayMayMusic::
	playbgm MUS_ENCOUNTER_MAY, TRUE
	speakername CommonName_Text_May
	return

LavaridgeTown_EventScript_PlayBrendanMusic::
	playbgm MUS_ENCOUNTER_BRENDAN, TRUE
	speakername CommonName_Text_Brendan
	return

LavaridgeTown_EventScript_RivalNoticePlayer::
	applymovement LOCALID_RIVAL, Common_Movement_WalkInPlaceFasterUp
	waitmovement 0
	playse SE_PIN
	applymovement LOCALID_RIVAL, Common_Movement_ExclamationMark
	waitmovement 0
	applymovement LOCALID_RIVAL, Common_Movement_Delay48
	waitmovement 0
	return

LavaridgeTown_EventScript_RivalExitHerbShop::
	opendoor 12, 15
	waitdooranim
	addobject LOCALID_RIVAL
	applymovement LOCALID_RIVAL, LavaridgeTown_Movement_RivalExitHerbShop
	waitmovement 0
	closedoor 12, 15
	waitdooranim
	applymovement LOCALID_RIVAL, Common_Movement_WalkInPlaceFasterLeft
	waitmovement 0
	playse SE_PIN
	applymovement LOCALID_RIVAL, Common_Movement_ExclamationMark
	waitmovement 0
	applymovement LOCALID_RIVAL, Common_Movement_Delay48
	waitmovement 0
	applymovement OBJ_EVENT_ID_PLAYER, Common_Movement_WalkInPlaceFasterRight
	waitmovement 0
	return

LavaridgeTown_EventScript_RivalApproachPlayer1::
	applymovement LOCALID_RIVAL, LavaridgeTown_Movement_RivalApproachPlayer1
	waitmovement 0
	return

LavaridgeTown_EventScript_RivalApproachPlayer2::
	applymovement LOCALID_RIVAL, LavaridgeTown_Movement_RivalApproachPlayer2
	waitmovement 0
	return

LavaridgeTown_EventScript_RivalExit1::
	applymovement OBJ_EVENT_ID_PLAYER, LavaridgeTown_Movement_PlayerWatchRivalExit
	applymovement LOCALID_RIVAL_ON_BIKE, LavaridgeTown_Movement_RivalExit1
	waitmovement 0
	return

LavaridgeTown_EventScript_RivalExit2::
	applymovement LOCALID_RIVAL_ON_BIKE, LavaridgeTown_Movement_RivalExit2
	waitmovement 0
	return

LavaridgeTown_Movement_RivalExit2:
	walk_fast_right
	walk_fast_right
	walk_fast_right
	walk_fast_up
	walk_fast_up
	walk_fast_up
	walk_fast_up
	walk_fast_up
	walk_fast_up
	walk_fast_up
	step_end

LavaridgeTown_Movement_PlayerWatchRivalExit:
	delay_16
	delay_8
	walk_in_place_faster_right
	step_end

LavaridgeTown_Movement_RivalExit1:
	walk_fast_down
	walk_fast_right
	walk_fast_right
	walk_fast_right
	walk_fast_right
	walk_fast_right
	walk_fast_right
	walk_fast_right
	walk_fast_right
	step_end

LavaridgeTown_Movement_RivalApproachPlayer2:
	walk_left
	walk_left
	walk_left
	walk_left
	walk_left
	walk_left
	step_end

LavaridgeTown_Movement_RivalApproachPlayer1:
	walk_left
	walk_left
	walk_up
	step_end

LavaridgeTown_Movement_RivalExitHerbShop:
	walk_down
	step_end

LavaridgeTown_EventScript_HotSpringsTrigger::
	specialvar VAR_RESULT, GetPlayerFacingDirection
	goto_if_eq VAR_RESULT, DIR_SOUTH, LavaridgeTown_EventScript_EnteredHotSprings
	end

LavaridgeTown_EventScript_EnteredHotSprings::
	incrementgamestat GAME_STAT_ENTERED_HOT_SPRINGS
	end

LavaridgeTown_EventScript_EggWoman::
	lock
	faceplayer
	goto_if_set FLAG_RECEIVED_LAVARIDGE_EGG, LavaridgeTown_EventScript_ReceivedEgg
	msgbox LavaridgeTown_Text_HaveEggWillYouTakeIt, MSGBOX_YESNO
	goto_if_eq VAR_RESULT, NO, LavaridgeTown_EventScript_DeclineEgg
	getpartysize
	goto_if_eq VAR_RESULT, PARTY_SIZE, LavaridgeTown_EventScript_NoRoomForEgg
	msgbox LavaridgeTown_Text_HopeYoullWalkPlentyWithEgg, MSGBOX_DEFAULT
	setflag FLAG_RECEIVED_LAVARIDGE_EGG
	playfanfare MUS_OBTAIN_ITEM
	message LavaridgeTown_Text_ReceivedTheEgg
	waitfanfare
	giveegg SPECIES_CHARCADET
	release
	end

LavaridgeTown_EventScript_ReceivedEgg::
	msgbox LavaridgeTown_Text_EverySoOftenEggFoundAtDayCare, MSGBOX_DEFAULT
	release
	end

LavaridgeTown_EventScript_NoRoomForEgg::
	msgbox LavaridgeTown_Text_NoRoomForThisEgg, MSGBOX_DEFAULT
	release
	end

LavaridgeTown_EventScript_DeclineEgg::
	msgbox LavaridgeTown_Text_AsYouWishThen, MSGBOX_DEFAULT
	release
	end

`
text LavaridgeTown_Text_MayNiceBadgesTakeThis {
	format("{PLAYER}{KUN}! Long time no see!\p"
	"Oh? While I visited the hot springs,\n"
	"you got the Lavaridge Gym Badge.\p"
	"You must have collected\n"
	"good Pokémon…\l"
	"No, I believe you raised some.\p"
	"I guess it would be okay for you to\n"
	"have this.")
}

text LavaridgeTown_Text_MayExplainGoGogglesChallengeDad {
	format("With those Go-Goggles, you'll have no trouble getting through the desert near Route 111.\p"
	"But, wow, {PLAYER}{KUN}…\n"
	"Challenging Gyms…\p"
	"You're trying hard because you don't\n"
	"want to lose to me, I bet!\p"
	"{PLAYER}{KUN}, I think I should challenge\n"
	"your dad in Petalburg Gym.\p"
	"See you again!")
}

text LavaridgeTown_Text_BrendanNiceBadgesTakeThis {
	format("{PLAYER}, hey, it's been a while.\n"
	"How's it going?\p"
	"Hmm…\n"
	"That's a decent collection of Badges.\p"
	"All right, then.\n"
	"You may as well have this.")
}

text LavaridgeTown_Text_BrendanExplainGoGogglesChallengeDad {
	format("Keep those with you if you're planning on going into that desert near Route 111.\p"
	"There should be some Pokémon that are exclusively desert-dwellers.\l"
	"You might look around for those.\p"
	"As for me…\n"
	"I'm considering challenging Norman, the Gym Leader of Petalburg.\p"
	"Unlike you, {PLAYER}, your dad looks like he really is tough.\p"
	"See you around!")
}

text LavaridgeTown_Text_HaveEggWillYouTakeIt {
	format("I have here an Egg.\p"
	"I'd hoped to hatch it by covering it in hot sand by the hot springs.\l"
	"But that doesn't seem to be enough…\p"
	"I've heard it would be best if it were kept together with Pokémon and carried about.\p"
	"You are a Trainer, yes?\n"
	"And your Pokémon radiate vitality.\p"
	"So, what say you?\n"
	"Will you take this Egg to hatch?")
}

text LavaridgeTown_Text_HopeYoullWalkPlentyWithEgg {
	format("Good! I hope you'll walk plenty with this here Egg!")
}

text LavaridgeTown_Text_ReceivedTheEgg {
	format("{PLAYER} received the Egg.")
}

text LavaridgeTown_Text_NoRoomForThisEgg {
	format("Oh? You've too many Pokémon.\n"
	"There's no room for this Egg…")
}

text LavaridgeTown_Text_AsYouWishThen {
	format("As you wish, then…\p"
	"If you have a change of heart about hatching this Egg, I will be here.")
}

text LavaridgeTown_Text_EverySoOftenEggFoundAtDayCare {
	format("Every so often, an Egg will be found at the Pokémon Day Care.\p"
	"Or at least that's how the rumor goes.")
}

script LavaridgeTown_EventScript_ExpertM {
	msgbox(format("We draw as much hot water as we need, and yet the hot springs never run dry.\p"
	"Isn't it magical?\p"
	"These hot springs appear near active volcanoes. Veins of water under the ground are heated by magma to well up as hot springs."), MSGBOX_NPC)
}

script LavaridgeTown_EventScript_OldMan {
	msgbox(format("Being buried in this hot sand is…\n"
	"Sigh…\p"
	"So warm and heavenly…\p"
	"Eh? Gyaah! Ouch!\p"
	"A Pokémon nipped my backside!"), MSGBOX_SIGN)
}

script LavaridgeTown_EventScript_Twin {
	msgbox(format("I bathe in the hot springs every day.\p"
	"I want to become a beautiful and strong Gym Leader like Flannery."), MSGBOX_NPC)
}

script LavaridgeTown_EventScript_HotSpringsOldWoman1 {
	msgbox(format("If people put Pokémon in hot springs, it might be seriously strange.\p"
	"Why, it might be an electric bath, or a bubble bath, or even a lava bath…"), MSGBOX_NPC)
}

script LavaridgeTown_EventScript_HotSpringsOldWoman2 {
	msgbox(format("They're claiming that these hot springs are good for calming nervous tension, relieving aching muscles, solving romantic problems, and attracting money…"), MSGBOX_NPC)
}

script LavaridgeTown_EventScript_ExpertF {
	msgbox(format("Oh, you like hot springs, do you?\p"
	"That's surprising for one as young as you."), MSGBOX_NPC)
}

script LavaridgeTown_EventScript_TownSign {
	msgbox(format("Lavaridge Town\p"
	"“Pokémon Center Hot Springs\n"
	"An excellent place for relaxing!”"), MSGBOX_SIGN)
}

script LavaridgeTown_EventScript_GymSign {
	msgbox(format("Lavaridge Town Pokémon Gym Leader: Flannery\l"
	"“One with a fiery passion that burns!”"), MSGBOX_SIGN)
}

script LavaridgeTown_EventScript_HerbShopSign {
	msgbox(format("Pokémon Herb Shop\n"
	"“Bitter taste--better cure!”"), MSGBOX_SIGN)
}
