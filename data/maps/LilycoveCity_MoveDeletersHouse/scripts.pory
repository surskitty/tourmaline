mapscripts LilycoveCity_MoveDeletersHouse_MapScripts {}

raw `
LilycoveCity_MoveDeletersHouse_EventScript_MoveDeleter::
    lockall
    applymovement LOCALID_MOVE_DELETER, Common_Movement_FacePlayer
    waitmovement 0
    speakername LilycoveCity_MoveDeleter
    msgbox LilycoveCity_MoveDeletersHouse_Text_ICanMakeMonForgetMove, MSGBOX_YESNO
    switch VAR_RESULT
    case YES, LilycoveCity_MoveDeletersHouse_EventScript_ChooseMonAndMoveToForget
    case NO, LilycoveCity_MoveDeletersHouse_EventScript_ComeAgain
    releaseall
    end

LilycoveCity_MoveDeletersHouse_EventScript_ChooseMonAndMoveToForget::
    msgbox LilycoveCity_MoveDeletersHouse_Text_WhichMonShouldForget, MSGBOX_DEFAULT
    special ChoosePartyMon
    waitstate
    goto_if_eq VAR_0x8004, PARTY_NOTHING_CHOSEN, LilycoveCity_MoveDeletersHouse_EventScript_ComeAgain
    special IsSelectedMonEgg
    goto_if_eq VAR_RESULT, TRUE, LilycoveCity_MoveDeletersHouse_EventScript_EggCantForgetMoves
    special GetNumMovesSelectedMonHas
    goto_if_eq VAR_RESULT, 1, LilycoveCity_MoveDeletersHouse_EventScript_MonOnlyKnowsOneMove
    msgbox LilycoveCity_MoveDeletersHouse_Text_WhichMoveShouldBeForgotten, MSGBOX_DEFAULT
    fadescreen FADE_TO_BLACK
    special MoveDeleterChooseMoveToForget
    fadescreen FADE_FROM_BLACK
    goto_if_eq VAR_0x8005, MAX_MON_MOVES, LilycoveCity_MoveDeletersHouse_EventScript_ChooseMonAndMoveToForget
    special BufferMoveDeleterNicknameAndMove
    msgbox LilycoveCity_MoveDeletersHouse_Text_MonsMoveShouldBeForgotten, MSGBOX_YESNO
    switch VAR_RESULT
    case YES, LilycoveCity_MoveDeletersHouse_EventScript_TryForgetMove
    case NO, LilycoveCity_MoveDeletersHouse_EventScript_ComeAgain
    releaseall
    end

LilycoveCity_MoveDeletersHouse_EventScript_TryForgetMove::
    special IsLastMonThatKnowsSurf
    goto_if_eq VAR_RESULT, TRUE, LilycoveCity_MoveDeletersHouse_EventScript_LastMonWithSurf
    special MoveDeleterForgetMove
    playfanfare MUS_MOVE_DELETED
    waitfanfare
    msgbox LilycoveCity_MoveDeletersHouse_Text_MonHasForgottenMove, MSGBOX_DEFAULT
    releaseall
    end

`

text LilycoveCity_MoveDeletersHouse_Text_ICanMakeMonForgetMove {
    format("Uh…\n"
    "Oh, yes, I'm the Move Deleter.\p"
    "I can make Pokémon forget their moves.\p"
    "Would you like me to do that?")
}

text LilycoveCity_MoveDeletersHouse_Text_WhichMonShouldForget {
    format("Which Pokémon should forget a move?")
}

text LilycoveCity_MoveDeletersHouse_Text_WhichMoveShouldBeForgotten {
    format("Which move should be forgotten?")
}

text LilycoveCity_MoveDeletersHouse_Text_MonsMoveShouldBeForgotten {
    format("Hm! {STR_VAR_1}'s {STR_VAR_2}?\n"
    "That move should be forgotten?")
}

text LilycoveCity_MoveDeletersHouse_Text_MonHasForgottenMove {
    format("It worked to perfection!\p"
    "{STR_VAR_1} has forgotten\n"
    "{STR_VAR_2} completely.")
}

script LilycoveCity_MoveDeletersHouse_EventScript_MonOnlyKnowsOneMove {
    special(BufferMoveDeleterNicknameAndMove)
    msgbox(format("{STR_VAR_1} knows only one move so it can't be forgotten…"), MSGBOX_DEFAULT)
    releaseall
}

script LilycoveCity_MoveDeletersHouse_EventScript_EggCantForgetMoves {
    msgbox(format("What?\n"
        "I can't help you with an Egg."), MSGBOX_DEFAULT)
    releaseall
}

script LilycoveCity_MoveDeletersHouse_EventScript_ComeAgain {
    msgbox(format("Come again if there are moves that need to be forgotten."), MSGBOX_DEFAULT)
    releaseall
}

script LilycoveCity_MoveDeletersHouse_EventScript_LastMonWithSurf {
    special(BufferMoveDeleterNicknameAndMove)
    msgbox(format("Hm!\p"
        "Your {STR_VAR_1} doesn't seem willing to forget Surf."), MSGBOX_DEFAULT)
    releaseall
}

text LilycoveCity_MoveDeleter { "Move Deleter" }
