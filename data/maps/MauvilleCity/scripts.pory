mapscripts MauvilleCity_MapScripts {
    MAP_SCRIPT_ON_TRANSITION {
        setflag(FLAG_VISITED_MAUVILLE_CITY)
        clearflag(FLAG_FORCE_MIRAGE_TOWER_VISIBLE)
        clearflag(FLAG_HIDE_SLATEPORT_MUSEUM_POPULATION)
        setflag(FLAG_SYS_TV_START)
        clearflag(FLAG_MAUVILLE_GYM_BARRIERS_STATE)
        setvar(VAR_MAUVILLE_GYM_STATE, 0)
        call_if_set(FLAG_GOT_TM_THUNDERBOLT_FROM_WATTSON, MauvilleCity_EventScript_MoveWattsonBackToGym)
    }
    MAP_SCRIPT_ON_LOAD {
        special(SetRoofBirds)
    }
}

raw `
.set LOCALID_WALLY, 6
.set LOCALID_WALLYS_UNCLE, 7
.set LOCALID_SCOTT, 11

MauvilleCity_EventScript_MoveWattsonBackToGym::
    clearflag FLAG_HIDE_MAUVILLE_GYM_WATTSON
    setflag FLAG_HIDE_MAUVILLE_CITY_WATTSON
    setflag FLAG_WATTSON_REMATCH_AVAILABLE
    return

MauvilleCity_EventScript_Wally::
    lockall
    goto_if_set FLAG_DECLINED_WALLY_BATTLE_MAUVILLE, MauvilleCity_EventScript_WallyRequestBattleAgain
    applymovement LOCALID_WALLY, Common_Movement_WalkInPlaceFasterRight
    waitmovement 0
    speakername CommonName_Text_Wally
    msgbox MauvilleCity_Text_WallyWantToChallengeGym, MSGBOX_DEFAULT
    speakername CommonName_Text_WallysUncle
    msgbox MauvilleCity_Text_UncleYourePushingIt, MSGBOX_DEFAULT
    speakername CommonName_Text_Wally
    msgbox MauvilleCity_Text_WallyWeCanBeatAnyone, MSGBOX_DEFAULT
    applymovement LOCALID_WALLY, Common_Movement_FacePlayer
    waitmovement 0
    playse SE_PIN
    applymovement LOCALID_WALLY, Common_Movement_ExclamationMark
    waitmovement 0
    applymovement LOCALID_WALLY, Common_Movement_Delay48
    waitmovement 0
    speakername CommonName_Text_Wally
    msgbox MauvilleCity_Text_WallyWillYouBattleMe, MSGBOX_YESNO
    goto MauvilleCity_EventScript_BattleWallyPrompt
    end

MauvilleCity_EventScript_BattleWallyPrompt::
    call_if_eq VAR_RESULT, YES, MauvilleCity_EventScript_BattleWally
    goto_if_eq VAR_RESULT, NO, MauvilleCity_EventScript_DeclineWallyBattle
    closemessage
    switch VAR_FACING
    case DIR_NORTH, MauvilleCity_EventScript_WallyAndUncleExitNorth
    case DIR_EAST, MauvilleCity_EventScript_WallyAndUncleExitEast
    end

MauvilleCity_EventScript_WallyAndUncleExitNorth::
    applymovement OBJ_EVENT_ID_PLAYER, MauvilleCity_Movement_PlayerWatchWallyExitNorth1
    applymovement LOCALID_WALLY, MauvilleCity_Movement_WallyExitNorth1
    applymovement LOCALID_WALLYS_UNCLE, MauvilleCity_Movement_WallysUncleExitNorth1
    waitmovement 0
    applymovement LOCALID_WALLY, Common_Movement_WalkInPlaceFasterRight
    waitmovement 0
    delay 30
    applymovement OBJ_EVENT_ID_PLAYER, MauvilleCity_Movement_PlayerFaceUncleNorth
    applymovement LOCALID_WALLYS_UNCLE, MauvilleCity_Movement_WallysUncleApproachPlayerNorth
    waitmovement 0
    msgbox MauvilleCity_Text_UncleVisitUsSometime, MSGBOX_DEFAULT
    closemessage
    applymovement OBJ_EVENT_ID_PLAYER, MauvilleCity_Movement_PlayerWatchWallyExitNorth2
    applymovement LOCALID_WALLYS_UNCLE, MauvilleCity_Movement_WallysUncleExitNorth2
    applymovement LOCALID_WALLY, MauvilleCity_Movement_WallyExitNorth2
    waitmovement 0
    goto MauvilleCity_EventScript_DefeatedWally
    end

MauvilleCity_EventScript_WallyAndUncleExitEast::
    applymovement OBJ_EVENT_ID_PLAYER, MauvilleCity_Movement_PlayerWatchWallyExitEast1
    applymovement LOCALID_WALLY, MauvilleCity_Movement_WallyExitEast1
    applymovement LOCALID_WALLYS_UNCLE, MauvilleCity_Movement_WallysUncleExitEast1
    waitmovement 0
    applymovement LOCALID_WALLY, Common_Movement_WalkInPlaceFasterRight
    waitmovement 0
    delay 30
    applymovement LOCALID_WALLYS_UNCLE, MauvilleCity_Movement_WallysUncleApproachPlayerEast
    waitmovement 0
    speakername CommonName_Text_WallysUncle
    msgbox MauvilleCity_Text_UncleVisitUsSometime, MSGBOX_DEFAULT
    closemessage
    applymovement OBJ_EVENT_ID_PLAYER, MauvilleCity_Movement_PlayerWatchWallyExitEast2
    applymovement LOCALID_WALLYS_UNCLE, MauvilleCity_Movement_WallysUncleExitEast2
    applymovement LOCALID_WALLY, MauvilleCity_Movement_WallyExitEast2
    waitmovement 0
    goto MauvilleCity_EventScript_DefeatedWally
    end

MauvilleCity_EventScript_ScottApproachPlayerNorth::
    addobject LOCALID_SCOTT
    applymovement LOCALID_SCOTT, MauvilleCity_Movement_ScottApproachPlayerNorth
    waitmovement 0
    return

MauvilleCity_EventScript_ScottApproachPlayerEast::
    setobjectxyperm LOCALID_SCOTT, 12, 13
    addobject LOCALID_SCOTT
    applymovement LOCALID_SCOTT, MauvilleCity_Movement_ScottApproachPlayerEast
    waitmovement 0
    return

MauvilleCity_EventScript_ScottExitNorth::
    applymovement OBJ_EVENT_ID_PLAYER, MauvilleCity_Movement_PlayerWatchScottExitNorth
    applymovement LOCALID_SCOTT, MauvilleCity_Movement_ScottExitNorth
    waitmovement 0
    return

MauvilleCity_EventScript_ScottExitEast::
    applymovement OBJ_EVENT_ID_PLAYER, MauvilleCity_Movement_PlayerWatchScottExitEast
    applymovement LOCALID_SCOTT, MauvilleCity_Movement_ScottExitEast
    waitmovement 0
    return

MauvilleCity_EventScript_BattleWally::
    speakername CommonName_Text_Wally
    msgbox MauvilleCity_Text_WallyHereICome, MSGBOX_DEFAULT
    trainerbattle_no_intro TRAINER_WALLY_MAUVILLE, MauvilleCity_Text_WallyDefeat
    applymovement LOCALID_WALLY, Common_Movement_WalkInPlaceFasterRight
    waitmovement 0
    speakername CommonName_Text_Wally
    msgbox MauvilleCity_Text_WallyIllGoBackToVerdanturf, MSGBOX_DEFAULT
    applymovement LOCALID_WALLY, Common_Movement_FacePlayer
    waitmovement 0
    speakername CommonName_Text_Wally
    msgbox MauvilleCity_Text_ThankYouNotEnoughToBattle, MSGBOX_DEFAULT
    applymovement LOCALID_WALLY, Common_Movement_WalkInPlaceFasterRight
    waitmovement 0
    speakername CommonName_Text_WallysUncle
    msgbox MauvilleCity_Text_UncleNoNeedToBeDown, MSGBOX_DEFAULT
    return

MauvilleCity_EventScript_DeclineWallyBattle::
    setflag FLAG_DECLINED_WALLY_BATTLE_MAUVILLE
    speakername CommonName_Text_Wally
    msgbox MauvilleCity_Text_WallyMyUncleWontKnowImStrong, MSGBOX_DEFAULT
    release
    end

MauvilleCity_EventScript_WallyRequestBattleAgain::
    applymovement LOCALID_WALLY, Common_Movement_FacePlayer
    waitmovement 0
    speakername CommonName_Text_Wally
    msgbox MauvilleCity_Text_WallyPleaseBattleMe, MSGBOX_YESNO
    goto MauvilleCity_EventScript_BattleWallyPrompt
    end

MauvilleCity_Movement_WallyExitNorth1:
    walk_left
    walk_left
    walk_down
    walk_down
    walk_left
    step_end

MauvilleCity_Movement_WallyExitEast1:
    walk_down
    walk_down
    walk_left
    walk_left
    walk_left
    step_end

MauvilleCity_Movement_WallyExitNorth2:
    delay_16
    delay_16
    walk_left
    walk_left
    walk_left
    walk_left
    walk_left
    walk_left
    walk_left
    walk_left
    delay_8
    step_end

MauvilleCity_Movement_WallyExitEast2:
    delay_16
    delay_16
    walk_left
    walk_left
    walk_left
    walk_left
    walk_left
    walk_left
    walk_left
    walk_left
    delay_8
    step_end

MauvilleCity_Movement_PlayerWatchWallyExitNorth2:
    delay_16
    delay_8
    walk_in_place_faster_left
    step_end

MauvilleCity_Movement_PlayerWatchWallyExitEast2:
    delay_16
    delay_16
    delay_16
    walk_in_place_faster_left
    step_end

MauvilleCity_Movement_PlayerWatchScottExitNorth:
    delay_16
    walk_in_place_faster_left
    step_end

MauvilleCity_Movement_PlayerWatchScottExitEast:
    delay_16
    delay_16
    walk_in_place_faster_left
    step_end

MauvilleCity_Movement_PlayerWatchWallyExitEast1:
    delay_16
    walk_in_place_faster_down
    step_end

MauvilleCity_Movement_PlayerWatchWallyExitNorth1:
    delay_16
    walk_in_place_faster_left
    step_end

MauvilleCity_Movement_WallysUncleExitNorth1:
    walk_left
    walk_left
    walk_left
    walk_down
    walk_down
    step_end

MauvilleCity_Movement_WallysUncleExitEast1:
    walk_left
    walk_down
    walk_down
    walk_left
    walk_left
    step_end

MauvilleCity_Movement_PlayerFaceUncleNorth:
    delay_16
    delay_8
    delay_4
    walk_in_place_faster_down
    step_end

MauvilleCity_Movement_WallysUncleApproachPlayerNorth:
    walk_right
    walk_right
    walk_in_place_faster_up
    step_end

MauvilleCity_Movement_WallysUncleApproachPlayerEast:
    walk_right
    walk_up
    step_end

MauvilleCity_Movement_WallysUncleExitNorth2:
    walk_left
    walk_left
    walk_left
    walk_left
    walk_left
    walk_left
    walk_left
    walk_left
    step_end

MauvilleCity_Movement_WallysUncleExitEast2:
    walk_down
    walk_left
    walk_left
    walk_left
    walk_left
    walk_left
    walk_left
    walk_left
    walk_left
    step_end

MauvilleCity_Movement_ScottApproachPlayerNorth:
    walk_up
    walk_up
    walk_up
    walk_up
    walk_up
    walk_up
    walk_left
    walk_left
    walk_left
    walk_left
    walk_in_place_faster_up
    step_end

MauvilleCity_Movement_ScottApproachPlayerEast:
    walk_up
    walk_up
    walk_up
    walk_up
    walk_up
    walk_left
    walk_left
    walk_left
    walk_left
    walk_left
    walk_up
    step_end

MauvilleCity_Movement_ScottExitNorth:
    walk_left
    walk_left
    walk_left
    walk_left
    walk_left
    walk_left
    walk_left
    walk_left
    walk_left
    step_end

MauvilleCity_Movement_ScottExitEast:
    walk_down
    walk_left
    walk_left
    walk_left
    walk_left
    walk_left
    walk_left
    walk_left
    walk_left
    walk_left
    step_end

MauvilleCity_EventScript_Wattson::
    lock
    faceplayer
    speakername CommonName_Text_Wattson
    goto_if_set FLAG_GOT_TM_THUNDERBOLT_FROM_WATTSON, MauvilleCity_EventScript_ReceivedThunderbolt
    goto_if_eq VAR_NEW_MAUVILLE_STATE, 2, MauvilleCity_EventScript_CompletedNewMauville
    goto_if_set FLAG_GOT_BASEMENT_KEY_FROM_WATTSON, MauvilleCity_EventScript_BegunNewMauville
    msgbox MauvilleCity_Text_WattsonNeedFavorTakeKey, MSGBOX_DEFAULT
    giveitem ITEM_BASEMENT_KEY
    goto_if_eq VAR_RESULT, FALSE, Common_EventScript_ShowBagIsFull
    setflag FLAG_GOT_BASEMENT_KEY_FROM_WATTSON
    msgbox MauvilleCity_Text_WattsonWontBeChallenge, MSGBOX_DEFAULT
    release
    end

MauvilleCity_EventScript_BegunNewMauville::
    msgbox MauvilleCity_Text_WattsonWontBeChallenge, MSGBOX_DEFAULT
    release
    end

MauvilleCity_EventScript_CompletedNewMauville::
    msgbox MauvilleCity_Text_WattsonThanksTakeTM, MSGBOX_DEFAULT
    giveitem ITEM_TM_THUNDERBOLT
    goto_if_eq VAR_RESULT, FALSE, Common_EventScript_ShowBagIsFull
    setflag FLAG_GOT_TM_THUNDERBOLT_FROM_WATTSON
    msgbox MauvilleCity_Text_WattsonYoungTakeCharge, MSGBOX_DEFAULT
    release
    end

MauvilleCity_EventScript_ReceivedThunderbolt::
    msgbox MauvilleCity_Text_WattsonYoungTakeCharge, MSGBOX_DEFAULT
    release
    end

`

text MauvilleCity_Text_WallyWantToChallengeGym {
   format("Aww, Uncle, please?\p"
    "I want to challenge this Gym and see how much better I've become.\p"
    "Please? May I, please?")
}

text MauvilleCity_Text_UncleYourePushingIt {
   format("Now hold on, Wally.\p"
    "Since you started living with Pokémon, you have grown quite a lot stronger.\p"
    "But don't you think you're pushing it to suddenly challenge a Gym?")
}

text MauvilleCity_Text_WallyWeCanBeatAnyone {
   format("I'm not pushing it.\p"
    "If I combine forces with Ralts,\n"
    "we can beat anyone!")
}

text MauvilleCity_Text_WallyWillYouBattleMe {
   format("Oh! Hi, {PLAYER}!\p"
    "I've gotten a lot stronger since we met.\p"
    "{PLAYER}, I want you and my Uncle to understand that.\p"
    "{PLAYER}, please, will you have a battle with me?")
}

text MauvilleCity_Text_WallyMyUncleWontKnowImStrong {
   format("Oh… If you won't battle me,\n"
    "{PLAYER}, my Uncle won't know that I've\l"
    "become really strong.")
}

text MauvilleCity_Text_WallyPleaseBattleMe {
   format("{PLAYER}, please!\n"
    "Battle with me, please.")
}

text MauvilleCity_Text_WallyHereICome {
   format("{PLAYER}, thank you.\p"
    "Okay… Here I come!")
}

text MauvilleCity_Text_WallyDefeat {
   format("… … … … … … …\p"
    "… … … … … … … …\p"
    "I lost…")
}

text MauvilleCity_Text_WallyIllGoBackToVerdanturf {
   format("WALLY: Uncle…\n"
    "I'll go back to Verdanturf…")
}

text MauvilleCity_Text_ThankYouNotEnoughToBattle {
   format("{PLAYER}, thank you.\n"
    "Being a Trainer is tough, isn't it?\p"
    "It's not enough just to have Pokémon\n"
    "and make them battle. That isn't what\l"
    "being a real Trainer is about.")
}

text MauvilleCity_Text_UncleNoNeedToBeDown {
   format("UNCLE: Wally, there's no need to be so\n"
    "down on yourself.\p"
    "Why, what's keeping you from becoming\n"
    "stronger and stronger?\p"
    "Come on, let's go home.\n"
    "Everyone's waiting for you.")
}

text MauvilleCity_Text_UncleVisitUsSometime {
   format("{PLAYER}{KUN}, it just dawned on me that you must be the Trainer who kept an eye out for Wally when he caught his Pokémon.\p"
    "Why don't you visit us in Verdanturf sometime?\l"
    "I'm sure Wally would enjoy it.")
}

script MauvilleCity_EventScript_RegisterWallyCall {
    lockall
    pokenavcall("… … … … … …\n"
        "… … … … … Beep!\p")
    speakername(CommonName_Text_Wally)
    pokenavcall("Oh, hello, {PLAYER}!\p"
        "I just wanted to tell you that\n"
        "my uncle bought me a Pokénav!\p"
        "Now I can get in touch with you\n"
        "anytime, {PLAYER}!\p"
        "… … … … … …\n"
        "… … … … … Click!")
    waitmessage
    delay(30)
    playfanfare(MUS_REGISTER_MATCH_CALL)
    speakername(0)
    msgbox(format("Registered Wally in the Pokénav."), MSGBOX_DEFAULT)
    waitfanfare
    closemessage
    delay(30)
    setflag(FLAG_ENABLE_WALLY_MATCH_CALL)
    clearflag(FLAG_ENABLE_FIRST_WALLY_POKENAV_CALL)
    releaseall
}

text MauvilleCity_Text_WattsonNeedFavorTakeKey {
   format("Oh, {PLAYER}{KUN}!\n"
    "You look like you have a lot of zip!\l"
    "That's a good thing, wahahahaha!\p"
    "Fine! I've decided!\n"
    "I need a favor, {PLAYER}{KUN}!\p"
    "Mauville City has an underground sector called New Mauville.\p"
    "{PLAYER}{KUN}, I'd like you to go there and switch off the Generator.\p"
    "The Generator has been running a bit haywire. It's getting unsafe.\p"
    "Here, this is the Key to get into New Mauville.")
}

text MauvilleCity_Text_WattsonWontBeChallenge {
   format("Don't you worry about it.\n"
    "It won't be a challenge to you.\p"
    "The entrance to New Mauville is just a short Surf away from Route 110.\p"
    "That's it, then, you have my trust!\n"
    "Wahahahaha!")
}

text MauvilleCity_Text_WattsonThanksTakeTM {
   format("Wahahahaha!\p"
    "I knew it, {PLAYER}{KUN}! I knew I'd made the right choice asking you!\p"
    "This is my thanks--a TM containing Thunderbolt!\p"
    "Go on, you've earned it!")
}

text MauvilleCity_Text_WattsonYoungTakeCharge {
   format("Wahahahaha!\p"
    "It pleases me to no end to see\n"
    "the young step up and take charge!")
}

script MauvilleCity_EventScript_Boy {
    msgbox(format("You know, it's cool to have Pokémon\n"
        "battles and stuff…\p"
        "But if your Pokémon gets hurt,\n"
        "you have to nurse it back to health."), MSGBOX_NPC)
}

script MauvilleCity_EventScript_Maniac {
    msgbox(format("The roads of this town stretch north and south, and east and west.\p"
    "Because of that, we get all sorts of people coming through."), MSGBOX_NPC)
}

script MauvilleCity_EventScript_Woman {
    msgbox(format("Have you been to Rydel's Cycles yet?\p"
        "Rydel, the owner, is a very generous man."), MSGBOX_NPC)
}

script MauvilleCity_EventScript_RichBoy {
    msgbox(format("Even if you're riding a Bike, wild Pokémon could jump you."), MSGBOX_NPC)
}

script MauvilleCity_EventScript_CitySign {
    msgbox(format("Mauville City\n"
        "“The bright and shiny city of fun!”"), MSGBOX_SIGN)
}

script MauvilleCity_EventScript_GymSign {
    msgbox(format("Mauville City Pokémon Gym\n"
        "Leader: Wattson\l"
        "“The cheerfully electrifying man!”"), MSGBOX_SIGN)
}

script MauvilleCity_EventScript_BikeShopSign {
    msgbox(format("“Ride in gravel and shake up your\n"
        "soul!”\l"
        "Rydel's Cycles"), MSGBOX_SIGN)
}

script MauvilleCity_EventScript_GameCornerSign {
    msgbox(format("“The play spot for all!”\n"
        "Mauville Game Corner"), MSGBOX_SIGN)
}

script MauvilleCity_EventScript_SchoolKidM {
    lock
    faceplayer
    goto_if_set(FLAG_TV_EXPLAINED, MauvilleCity_EventScript_TVExplained)
    msgbox(MauvilleCity_Text_ExplainTV, MSGBOX_DEFAULT)
    setflag(FLAG_TV_EXPLAINED)
    release
}

script MauvilleCity_EventScript_TVExplained {
    msgbox(format("Hi, have you been checking out TVs?"), MSGBOX_DEFAULT)
    release
}

text MauvilleCity_Text_ExplainTV {
   format("Hi, do you check out TV at all?\p"
    "They've added a bunch of cool new shows recently.\p"
    "TV is so cool. I've seen my friends on TV just out of the blue.\p"
    "And, sometimes, they even put you on TV without telling you.\p"
    "I wish I would be on TV sometime.\p"
    "That's why I think you should check out TVs whenever you can.")
}

script MauvilleCity_EventScript_WallysUncle {
    lock
    faceplayer
    speakername(CommonName_Text_WallysUncle)
    goto_if_set(FLAG_DECLINED_WALLY_BATTLE_MAUVILLE, MauvilleCity_EventScript_UncleAskPlayerToBattleWally)
    msgbox(format("It's because of Pokémon that this boy's got more pep, I suppose…\l"
        "But he's become a bit too peppy…"), MSGBOX_DEFAULT)
    closemessage
    applymovement(LOCALID_WALLYS_UNCLE, Common_Movement_FaceOriginalDirection)
    waitmovement(0)
    release
}

script MauvilleCity_EventScript_UncleAskPlayerToBattleWally {
    msgbox(format("{PLAYER}{KUN}, was it?\n"
    "On Wally's behalf, can I ask you to\l"
    "battle with him just this once?\p"
    "I don't think he's going to listen to any reason the way he is now."), MSGBOX_DEFAULT)
    closemessage
    applymovement(LOCALID_WALLYS_UNCLE, Common_Movement_FaceOriginalDirection)
    waitmovement(0)
    release
}

script MauvilleCity_EventScript_DefeatedWally {
    removeobject(LOCALID_WALLY)
    removeobject(LOCALID_WALLYS_UNCLE)
    clearflag(FLAG_HIDE_VERDANTURF_TOWN_WANDAS_HOUSE_WALLY)
    clearflag(FLAG_HIDE_VERDANTURF_TOWN_WANDAS_HOUSE_WALLYS_UNCLE)
    setflag(FLAG_DEFEATED_WALLY_MAUVILLE)
    setvar(VAR_WALLY_CALL_STEP_COUNTER, 0)
    setflag(FLAG_ENABLE_FIRST_WALLY_POKENAV_CALL)
    call_if_eq(VAR_FACING, DIR_NORTH, MauvilleCity_EventScript_ScottApproachPlayerNorth)
    call_if_eq(VAR_FACING, DIR_EAST, MauvilleCity_EventScript_ScottApproachPlayerEast)
    applymovement(OBJ_EVENT_ID_PLAYER, Common_Movement_WalkInPlaceFasterDown)
    waitmovement(0)
    speakername(CommonName_Text_Scott)
    msgbox(format("Hehe…\n"
        "I was watching that match!\p"
        "You're friends with that boy Wally, aren't you?\p"
        "But you didn't hold anything back and beat him impressively.\p"
        "Yeah! That's what a real Pokémon battle is all about!\p"
        "I idolize Trainers like that!\p"
        "… … … … … …\n"
        "I'll be cheering for you!"), MSGBOX_DEFAULT)
    closemessage
    addvar(VAR_SCOTT_STATE, 1)
    call_if_eq(VAR_FACING, DIR_NORTH, MauvilleCity_EventScript_ScottExitNorth)
    call_if_eq(VAR_FACING, DIR_EAST, MauvilleCity_EventScript_ScottExitEast)
    removeobject(LOCALID_SCOTT)
    releaseall
}

