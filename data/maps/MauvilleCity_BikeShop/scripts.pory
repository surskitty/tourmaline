mapscripts MauvilleCity_BikeShop_MapScripts {}

text MauvilleCity_BikeShop_Rydel { "Rydel" }

script MauvilleCity_BikeShop_EventScript_Rydel {
    lock
    faceplayer
    speakername(MauvilleCity_BikeShop_Rydel)
    goto_if_set(FLAG_RECEIVED_BIKE, MauvilleCity_BikeShop_EventScript_ComeBackToSwitchBikes)
    goto_if_set(FLAG_DECLINED_BIKE, MauvilleCity_BikeShop_EventScript_SkipGreeting)
    msgbox(MauvilleCity_BikeShop_Text_RydelGreeting, MSGBOX_DEFAULT)
    msgbox(MauvilleCity_BikeShop_Text_DidYouComeFromFarAway, MSGBOX_YESNO)
    goto_if_eq(VAR_RESULT, YES, MauvilleCity_BikeShop_EventScript_YesFar)
    goto_if_eq(VAR_RESULT, NO, MauvilleCity_BikeShop_EventScript_NotFar)
}

script MauvilleCity_BikeShop_EventScript_SkipGreeting {
    msgbox(MauvilleCity_BikeShop_Text_DidYouComeFromFarAway, MSGBOX_YESNO)
    goto_if_eq(VAR_RESULT, YES, MauvilleCity_BikeShop_EventScript_YesFar)
    goto_if_eq(VAR_RESULT, NO, MauvilleCity_BikeShop_EventScript_NotFar)
}

script MauvilleCity_BikeShop_EventScript_NotFar {
    setflag(FLAG_DECLINED_BIKE)
    msgbox(format("Is that right?\nThen, I guess you have no need for a bike."), MSGBOX_DEFAULT)
    release
}

script MauvilleCity_BikeShop_EventScript_YesFar {
    setflag(FLAG_RECEIVED_BIKE)
    goto(MauvilleCity_BikeShop_EventScript_GetBike)
}

script MauvilleCity_BikeShop_EventScript_GetBike {
    msgbox(format("Hm, hm… … … … …\n"
        "You're saying that you came all this way from Littleroot?\p"
        "My goodness!\n"
        "That's ridiculously far!\p"
        "If you had one of my Bikes, you could go anywhere easily while feeling the gentle caress of the wind!\p"
        "I'll tell you what!\n"
        "I'll give you a Bike!\p"), MSGBOX_DEFAULT)
    speakername(0)
    msgbox(format("{PLAYER} received a Bicycle."), MSGBOX_DEFAULT)
    giveitem(ITEM_ACRO_BIKE)
    goto_if_eq(VAR_RESULT, FALSE, Common_EventScript_ShowBagIsFull)
    goto(MauvilleCity_BikeShop_EventScript_ComeBackToSwitchBikes)
}

script MauvilleCity_BikeShop_EventScript_ComeBackToSwitchBikes {
    msgbox(format("Switch between the Acro and Mach modes by pressing the R Button, and travel all over Hoenn!"), MSGBOX_DEFAULT)
    release
}

raw `
MauvilleCity_BikeShop_EventScript_Assistant::
    msgbox MauvilleCity_BikeShop_Text_HandbooksAreInBack, MSGBOX_NPC
    end

MauvilleCity_BikeShop_EventScript_MachBikeHandbook::
    message MauvilleCity_BikeShop_Text_MachHandbookWhichPage
    waitmessage
    goto MauvilleCity_BikeShop_EventScript_ChooseMachHandbookPage
    end

MauvilleCity_BikeShop_EventScript_ChooseMachHandbookPage::
    multichoice 0, 0, MULTI_MACH_BIKE_INFO, FALSE
    switch VAR_RESULT
    case 0, MauvilleCity_BikeShop_EventScript_HowToRide
    case 1, MauvilleCity_BikeShop_EventScript_HowToTurn
    case 2, MauvilleCity_BikeShop_EventScript_SandySlopes
    case 3, MauvilleCity_BikeShop_EventScript_ExitMachHandbook
    case MULTI_B_PRESSED, MauvilleCity_BikeShop_EventScript_ExitMachHandbook
    end

MauvilleCity_BikeShop_EventScript_HowToRide::
    message MauvilleCity_BikeShop_Text_HowToRideMachBike
    waitmessage
    goto MauvilleCity_BikeShop_EventScript_ChooseMachHandbookPage
    end

MauvilleCity_BikeShop_EventScript_HowToTurn::
    message MauvilleCity_BikeShop_Text_HowToTurnMachBike
    waitmessage
    goto MauvilleCity_BikeShop_EventScript_ChooseMachHandbookPage
    end

MauvilleCity_BikeShop_EventScript_SandySlopes::
    message MauvilleCity_BikeShop_Text_SandySlopes
    waitmessage
    goto MauvilleCity_BikeShop_EventScript_ChooseMachHandbookPage
    end

MauvilleCity_BikeShop_EventScript_ExitMachHandbook::
    release
    end

MauvilleCity_BikeShop_EventScript_AcroBikeHandbook::
    message MauvilleCity_BikeShop_Text_AcroHandbookWhichPage
    waitmessage
    goto MauvilleCity_BikeShop_EventScript_ChooseAcroHandbookPage
    end

MauvilleCity_BikeShop_EventScript_ChooseAcroHandbookPage::
    multichoice 0, 0, MULTI_ACRO_BIKE_INFO, FALSE
    switch VAR_RESULT
    case 0, MauvilleCity_BikeShop_EventScript_Wheelies
    case 1, MauvilleCity_BikeShop_EventScript_BunnyHops
    case 2, MauvilleCity_BikeShop_EventScript_Jumps
    case 3, MauvilleCity_BikeShop_EventScript_ExitAcroHandbook
    case MULTI_B_PRESSED, MauvilleCity_BikeShop_EventScript_ExitAcroHandbook
    end

MauvilleCity_BikeShop_EventScript_Wheelies::
    message MauvilleCity_BikeShop_Text_Wheelies
    waitmessage
    goto MauvilleCity_BikeShop_EventScript_ChooseAcroHandbookPage
    end

MauvilleCity_BikeShop_EventScript_BunnyHops::
    message MauvilleCity_BikeShop_Text_BunnyHops
    waitmessage
    goto MauvilleCity_BikeShop_EventScript_ChooseAcroHandbookPage
    end

MauvilleCity_BikeShop_EventScript_Jumps::
    message MauvilleCity_BikeShop_Text_Jumps
    waitmessage
    goto MauvilleCity_BikeShop_EventScript_ChooseAcroHandbookPage
    end

MauvilleCity_BikeShop_EventScript_ExitAcroHandbook::
    release
    end
`

text MauvilleCity_BikeShop_Text_RydelGreeting {
    format("Well, well, what have we here?\n"
    "A most energetic customer!\p"
    "Me? You may call me Rydel.\n"
    "I'm the owner of this cycle shop.")
}

text MauvilleCity_BikeShop_Text_DidYouComeFromFarAway {
    format("Your Running Shoes…\n"
    "They're awfully filthy.\p"
    "Did you come from far away?")
}

text MauvilleCity_BikeShop_Text_OhYourBikeIsInPC {
    format("Oh? What happened to that Bike I gave you?\p"
    "Oh, I get it, you stored it using your PC.\p"
    "Well, take it out of PC storage,\n"
    "and I'll be happy to exchange it!\p"
    "May the wind always be at your back\n"
    "on your adventure!")
}

text MauvilleCity_BikeShop_Text_HandbooksAreInBack {
    format("I'm learning about Bikes while I work here.\p"
    "If you need advice on how to ride your Bike, there're a couple handbooks in the back.")
}

text MauvilleCity_BikeShop_Text_MachHandbookWhichPage {
    format("It's a handbook on the MACH BIKE.\p"
    "Which page do you want to read?")
}

text MauvilleCity_BikeShop_Text_HowToRideMachBike {
    format("A BIKE moves in the direction that\n"
    "the + Control Pad is pressed.\p"
    "It will speed up once it gets rolling.\p"
    "To stop, release the + Control Pad.\n"
    "The Bike will slow to a stop.\p"
    "Want to read a different page?")
}

text MauvilleCity_BikeShop_Text_HowToTurnMachBike {
    format("A Mach Bike is speedy, but it can't\n"
    "stop very quickly.\p"
    "It gets a little tricky to get around\n"
    "a corner.\p"
    "Release the + Control Pad a little\n"
    "before the corner and slow down.\p"
    "Want to read a different page?")
}

text MauvilleCity_BikeShop_Text_SandySlopes {
    format("There are small sandy slopes throughout the Hoenn region.\p"
    "The loose, crumbly sand makes it impossible to climb normally.\p"
    "But if you have a Mach Bike, you can zip up a sandy slope.\p"
    "Want to read a different page?")
}

text MauvilleCity_BikeShop_Text_AcroHandbookWhichPage {
    format("It's a handbook on the Acro Bike.\p"
    "Which page do you want to read?")
}

text MauvilleCity_BikeShop_Text_Wheelies {
    format("Press the B Button while riding,\n"
    "and the front wheel lifts up.\p"
    "You can zip around with the front\n"
    "wheel up using the + Control Pad.\p"
    "This technique is called a wheelie.\p"
    "Want to read a different page?")
}

text MauvilleCity_BikeShop_Text_BunnyHops {
    format("Keeping the B Button pressed,\n"
    "your BIKE can hop on the spot.\p"
    "This technique is called a bunny hop.\p"
    "You can ride while hopping, too.\p"
    "Want to read a different page?")
}

text MauvilleCity_BikeShop_Text_Jumps {
    format("Press the B Button and the + Control\n"
    "Pad at the same time to jump.\p"
    "Press the + Control Pad to the side\n"
    "to jump sideways.\p"
    "Press it backwards to make the Bike\n"
    "change directions while jumping.\p"
    "Want to read a different page?")
}

