mapscripts MeteorFalls_1F_1R_MapScripts {
    MAP_SCRIPT_ON_LOAD {
        special(SetRoofBirds)
        special(SetStationaryWilds)
        call_if_set(FLAG_SYS_GAME_CLEAR, MeteorFalls_1F_1R_EventScript_OpenStevensCave)
    }
}

raw `
.set LOCALID_MAGMA_GRUNT_1, 5
.set LOCALID_MAGMA_GRUNT_2, 6
.set LOCALID_ARCHIE, 7
.set LOCALID_AQUA_GRUNT_1, 8
.set LOCALID_AQUA_GRUNT_2, 9

MeteorFalls_1F_1R_EventScript_OpenStevensCave::
    setmetatile 4, 1, METATILE_MeteorFalls_CaveEntrance_Top, TRUE
    setmetatile 3, 2, METATILE_MeteorFalls_CaveEntrance_Left, TRUE
    setmetatile 4, 2, METATILE_MeteorFalls_CaveEntrance_Bottom, FALSE
    setmetatile 5, 2, METATILE_MeteorFalls_CaveEntrance_Right, TRUE
    return

MeteorFalls_1F_1R_EventScript_MagmaStealsMeteoriteScene::
    lockall
    playbgm MUS_ENCOUNTER_MAGMA, FALSE
    applymovement OBJ_EVENT_ID_PLAYER, Common_Movement_FaceDown
    waitmovement 0
    delay 30
    applymovement LOCALID_MAGMA_GRUNT_1, Common_Movement_WalkInPlaceDown
    waitmovement 0
    msgbox MeteorFalls_1F_1R_Text_WithThisMeteorite, MSGBOX_DEFAULT
    closemessage
    applymovement LOCALID_MAGMA_GRUNT_1, Common_Movement_WalkInPlaceFasterUp
    applymovement LOCALID_MAGMA_GRUNT_2, Common_Movement_WalkInPlaceFasterUp
    waitmovement 0
    playse SE_PIN
    applymovement LOCALID_MAGMA_GRUNT_1, Common_Movement_ExclamationMark
    waitmovement 0
    applymovement LOCALID_MAGMA_GRUNT_1, Common_Movement_Delay48
    waitmovement 0
    msgbox MeteorFalls_1F_1R_Text_DontExpectMercyFromMagma, MSGBOX_DEFAULT
    closemessage
    applymovement LOCALID_MAGMA_GRUNT_1, MeteorFalls_1F_1R_Movement_MagmaGruntApproachPlayer
    waitmovement 0
    msgbox MeteorFalls_1F_1R_Text_HoldItRightThereMagma, MSGBOX_DEFAULT
    closemessage
    applymovement OBJ_EVENT_ID_PLAYER, Common_Movement_WalkInPlaceFasterLeft
    applymovement LOCALID_MAGMA_GRUNT_1, Common_Movement_WalkInPlaceFasterLeft
    applymovement LOCALID_MAGMA_GRUNT_2, Common_Movement_WalkInPlaceFasterLeft
    waitmovement 0
    addobject LOCALID_ARCHIE
    addobject LOCALID_AQUA_GRUNT_1
    addobject LOCALID_AQUA_GRUNT_2
    playbgm MUS_ENCOUNTER_AQUA, FALSE
    applymovement LOCALID_ARCHIE, MeteorFalls_1F_1R_Movement_ArchieArrive
    applymovement LOCALID_AQUA_GRUNT_1, MeteorFalls_1F_1R_Movement_AquaGrunt1Arrive
    applymovement LOCALID_AQUA_GRUNT_2, MeteorFalls_1F_1R_Movement_AquaGrunt2Arrive
    waitmovement 0
    applymovement LOCALID_MAGMA_GRUNT_1, Common_Movement_WalkInPlaceFasterLeft
    applymovement LOCALID_MAGMA_GRUNT_2, Common_Movement_WalkInPlaceFasterLeft
    waitmovement 0
    msgbox MeteorFalls_1F_1R_Text_BeSeeingYouTeamAqua, MSGBOX_DEFAULT
    closemessage
    applymovement OBJ_EVENT_ID_PLAYER, MeteorFalls_1F_1R_Movement_PushPlayerOutOfWay
    applymovement LOCALID_MAGMA_GRUNT_1, MeteorFalls_1F_1R_Movement_MagmaGrunt1Exit
    applymovement LOCALID_MAGMA_GRUNT_2, MeteorFalls_1F_1R_Movement_MagmaGrunt2Exit
    waitmovement 0
    removeobject LOCALID_MAGMA_GRUNT_1
    removeobject LOCALID_MAGMA_GRUNT_2
    applymovement LOCALID_ARCHIE, MeteorFalls_1F_1R_Movement_ArchieApproachPlayer
    waitmovement 0
    speakername CommonName_Text_Archie
    msgbox MeteorFalls_1F_1R_Text_ArchieSeenYouBefore, MSGBOX_DEFAULT
    closemessage
    applymovement LOCALID_AQUA_GRUNT_1, MeteorFalls_1F_1R_Movement_AquaGrunt1ApproachArchie
    applymovement LOCALID_AQUA_GRUNT_2, MeteorFalls_1F_1R_Movement_AquaGrunt2ApproachArchie
    waitmovement 0
    msgbox MeteorFalls_1F_1R_Text_BossWeShouldChaseMagma, MSGBOX_DEFAULT
    applymovement LOCALID_ARCHIE, Common_Movement_WalkInPlaceFasterDown
    waitmovement 0
    speakername CommonName_Text_Archie
    msgbox MeteorFalls_1F_1R_Text_ArchieYesNoTellingWhatMagmaWillDo, MSGBOX_DEFAULT
    applymovement LOCALID_ARCHIE, Common_Movement_WalkInPlaceFasterLeft
    waitmovement 0
    speakername CommonName_Text_Archie
    msgbox MeteorFalls_1F_1R_Text_ArchieFarewell, MSGBOX_DEFAULT
    closemessage
    applymovement LOCALID_ARCHIE, MeteorFalls_1F_1R_Movement_ArchieExit
    applymovement LOCALID_AQUA_GRUNT_1, MeteorFalls_1F_1R_Movement_AquaGrunt1Exit
    applymovement LOCALID_AQUA_GRUNT_2, MeteorFalls_1F_1R_Movement_AquaGrunt2Exit
    waitmovement 0
    fadedefaultbgm
    removeobject LOCALID_ARCHIE
    removeobject LOCALID_AQUA_GRUNT_1
    removeobject LOCALID_AQUA_GRUNT_2
    setflag FLAG_HIDE_ROUTE_112_TEAM_MAGMA
    setflag FLAG_MET_ARCHIE_METEOR_FALLS
    setflag FLAG_HIDE_FALLARBOR_TOWN_BATTLE_TENT_SCOTT
    setvar VAR_METEOR_FALLS_STATE, 1
    releaseall
    end

`

text MeteorFalls_1F_1R_Text_WithThisMeteorite {
    format("Hehehe!\p"
    "With this Meteorite, that thing in Mt. Chimney will…")
}

text MeteorFalls_1F_1R_Text_DontExpectMercyFromMagma {
    format("Heh?\p"
    "I don't know who you are, but if you get in the way of Team Magma, don't expect any mercy!")
}

text MeteorFalls_1F_1R_Text_HoldItRightThereMagma {
    format("Hold it right there, Team Magma!\p"
    "You're badly mistaken if you think you can have your way with the world!")
}

text MeteorFalls_1F_1R_Text_BeSeeingYouTeamAqua {
    format("Hehehe!\n"
    "Even Team Aqua joins us!\p"
    "But it's too much trouble to deal with you all…\p"
    "Heh, it doesn't matter!\p"
    "We've got the Meteorite, so off to Mt. Chimney we go!\p"
    "Hehehe! Be seeing you, you Team Aqua dingbats!")
}

text MeteorFalls_1F_1R_Text_ArchieSeenYouBefore {
    format("Didn't I see you before?\n"
    "At Slateport's Museum?\p"
    "Ah, so your name is {PLAYER}.\p"
    "At the time, I thought you were one of Team Magma's goons.\p"
    "Humph…\n"
    "You're one odd kid.\p"
    "Team Magma is a dangerous group of total fanatics.\p"
    "They engage in destruction, claiming to expand the land mass.\p"
    "They are the rivals to us, the sea-loving Team Aqua!")
}

text MeteorFalls_1F_1R_Text_BossWeShouldChaseMagma {
    format("Boss, we should give chase to Team Magma…")
}

text MeteorFalls_1F_1R_Text_ArchieYesNoTellingWhatMagmaWillDo {
    format("Yes, yes, we must!\n"
    "We've got to hurry.\p"
    "There's no telling what Team Magma will do at Mt. Chimney!")
}

text MeteorFalls_1F_1R_Text_ArchieFarewell {
    format("{PLAYER}, you should keep an eye out for Team Magma, too.\p"
    "Farewell!")
}

script MeteorFalls_1F_1R_EventScript_ProfCozmo {
    lock
    faceplayer
    speakername(CommonName_Text_ProfCozmo)
    goto_if_set(FLAG_MET_PROF_COZMO, MeteorFalls_1F_1R_EventScript_MetCozmo)
    setflag(FLAG_MET_PROF_COZMO)
    msgbox(format("I… I'm Cozmo…\n"
        "I'm a Professor…\p"
        "Team Magma asked me to guide them to Meteor Falls…\p"
        "But they tricked me.\n"
        "They even took my Meteorite away…\p"
        "And then another similar bunch, Team Aqua showed up.\p"
        "After that…\n"
        "I don't understand any of it.\p"
        "But that Team Magma…\p"
        "What are they going to do with that\n"
        "Meteorite at Mt. Chimney?"), MSGBOX_DEFAULT)
    release
}

script MeteorFalls_1F_1R_EventScript_MetCozmo {
    msgbox(format("But that Team Magma…\p"
        "What are they going to do with that Meteorite at Mt. Chimney?"), MSGBOX_DEFAULT)
    release
}

movement MeteorFalls_1F_1R_Movement_MagmaGruntApproachPlayer {
    walk_right * 2
    walk_in_place_faster_up
}

movement MeteorFalls_1F_1R_Movement_MagmaGrunt1Exit {
    walk_fast_up * 2
    walk_fast_right * 7
}

movement MeteorFalls_1F_1R_Movement_MagmaGrunt2Exit {
    walk_fast_up * 3
    walk_fast_right * 7
}

movement MeteorFalls_1F_1R_Movement_ArchieArrive {
    walk_right * 6
}

movement MeteorFalls_1F_1R_Movement_AquaGrunt1Arrive {
    delay_16 * 2
    walk_right * 5
}

movement MeteorFalls_1F_1R_Movement_AquaGrunt2Arrive {
    delay_16 * 2
    walk_right * 5
}

movement MeteorFalls_1F_1R_Movement_ArchieExit {
    walk_right * 7
}

movement MeteorFalls_1F_1R_Movement_ArchieApproachPlayer {
    walk_right * 2
    walk_up * 2
    walk_in_place_faster_left
}

movement MeteorFalls_1F_1R_Movement_AquaGrunt1Exit {
    walk_up * 2
    walk_right * 7
}

movement MeteorFalls_1F_1R_Movement_AquaGrunt1ApproachArchie {
    walk_right * 3
    walk_in_place_faster_up
}

movement MeteorFalls_1F_1R_Movement_AquaGrunt2Exit {
    walk_up * 3
    walk_right * 7
}

movement MeteorFalls_1F_1R_Movement_AquaGrunt2ApproachArchie {
    walk_right * 3
    walk_in_place_faster_up
}

movement MeteorFalls_1F_1R_Movement_PushPlayerOutOfWay {
    walk_in_place_faster_down
    delay_4
    walk_in_place_faster_right
    lock_facing_direction
    walk_left
    unlock_facing_direction
    face_right
}
