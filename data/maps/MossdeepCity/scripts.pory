mapscripts MossdeepCity_MapScripts {
    MAP_SCRIPT_ON_TRANSITION: MossdeepCity_OnTransition
    MAP_SCRIPT_ON_LOAD {
        special(SetRoofBirds)
        special(SetStationaryWilds)
    }
}

raw `
.set LOCALID_GRUNT_1, 10
.set LOCALID_GRUNT_2, 11
.set LOCALID_GRUNT_3, 12
.set LOCALID_GRUNT_4, 13
.set LOCALID_MAXIE, 14
.set LOCALID_SCOTT, 16

MossdeepCity_OnTransition:
    clearflag FLAG_MOSSDEEP_GYM_SWITCH_1
    clearflag FLAG_MOSSDEEP_GYM_SWITCH_2
    clearflag FLAG_MOSSDEEP_GYM_SWITCH_3
    clearflag FLAG_MOSSDEEP_GYM_SWITCH_4
    call_if_set FLAG_SYS_WEATHER_CTRL, Common_EventScript_SetAbnormalWeather
    end

MossdeepCity_EventScript_PokefanF::
    lock
    faceplayer
    goto_if_set FLAG_RECEIVED_HM_DIVE, MossdeepCity_EventScript_PokefanFMagmaGone
    msgbox MossdeepCity_Text_SpaceCenterReceivedLetter, MSGBOX_DEFAULT
    release
    end

MossdeepCity_EventScript_PokefanFMagmaGone::
    msgbox MossdeepCity_Text_SpaceCenterLaunchingRockets, MSGBOX_DEFAULT
    release
    end

MossdeepCity_EventScript_Sailor::
    lock
    faceplayer
    goto_if_set FLAG_RECEIVED_HM_DIVE, MossdeepCity_EventScript_SailorMagmaGone
    msgbox MossdeepCity_Text_MossdeepTargetedByMagma, MSGBOX_DEFAULT
    release
    end

MossdeepCity_EventScript_SailorMagmaGone::
    msgbox MossdeepCity_Text_FeelReliefOnLand, MSGBOX_DEFAULT
    release
    end

MossdeepCity_EventScript_VisitedMossdeep::
    setflag FLAG_VISITED_MOSSDEEP_CITY
    setvar VAR_TEMP_1, 1
    end

MossdeepCity_EventScript_TeamMagmaEnterSpaceCenter::
    lockall
    applymovement LOCALID_MAXIE, MossdeepCity_Movement_MaxieGestureToSpaceCenter
    waitmovement 0
    applymovement LOCALID_GRUNT_1, MossdeepCity_Movement_GruntFaceSpaceCenter
    applymovement LOCALID_GRUNT_2, MossdeepCity_Movement_GruntFaceSpaceCenter
    applymovement LOCALID_GRUNT_3, MossdeepCity_Movement_GruntFaceSpaceCenter
    applymovement LOCALID_GRUNT_4, MossdeepCity_Movement_GruntFaceSpaceCenter
    waitmovement 0
    applymovement LOCALID_MAXIE, MossdeepCity_Movement_MaxieEnterSpaceCenter
    applymovement LOCALID_GRUNT_1, MossdeepCity_Movement_Grunt1EnterSpaceCenter
    applymovement LOCALID_GRUNT_2, MossdeepCity_Movement_Grunt2EnterSpaceCenter
    applymovement LOCALID_GRUNT_3, MossdeepCity_Movement_Grunt3EnterSpaceCenter
    applymovement LOCALID_GRUNT_4, MossdeepCity_Movement_Grunt4EnterSpaceCenter
    waitmovement 0
    removeobject LOCALID_MAXIE
    removeobject LOCALID_GRUNT_1
    removeobject LOCALID_GRUNT_2
    removeobject LOCALID_GRUNT_3
    removeobject LOCALID_GRUNT_4
    delay 30
    setvar VAR_MOSSDEEP_CITY_STATE, 2
    setflag FLAG_HIDE_MOSSDEEP_CITY_TEAM_MAGMA
    releaseall
    end

MossdeepCity_Movement_MaxieGestureToSpaceCenter:
    delay_16
    face_right
    delay_16
    delay_16
    delay_16
    delay_16
    face_left
    delay_16
    delay_16
    step_end

MossdeepCity_Movement_GruntFaceSpaceCenter:
    face_right
    delay_16
    delay_16
    delay_16
    step_end

MossdeepCity_Movement_MaxieEnterSpaceCenter:
    walk_down
    walk_right
    walk_right
    walk_down
    walk_down
    walk_down
    walk_right
    walk_right
    walk_right
    walk_right
    walk_right
    walk_right
    walk_right
    step_end

MossdeepCity_Movement_Grunt1EnterSpaceCenter:
    delay_16
    delay_8
    walk_down
    walk_down
    walk_down
    walk_right
    walk_right
    walk_right
    walk_down
    walk_down
    walk_down
    walk_right
    walk_right
    walk_right
    walk_right
    walk_right
    walk_right
    walk_right
    step_end

MossdeepCity_Movement_Grunt2EnterSpaceCenter:
    delay_16
    delay_8
    walk_down
    walk_down
    walk_right
    walk_right
    walk_right
    walk_down
    walk_down
    walk_down
    walk_right
    walk_right
    walk_right
    walk_right
    walk_right
    walk_right
    walk_right
    step_end

MossdeepCity_Movement_Grunt3EnterSpaceCenter:
    delay_16
    delay_8
    walk_down
    walk_right
    walk_right
    walk_right
    walk_down
    walk_down
    walk_down
    walk_right
    walk_right
    walk_right
    walk_right
    walk_right
    walk_right
    walk_right
    step_end

MossdeepCity_Movement_Grunt4EnterSpaceCenter:
    delay_16
    delay_8
    walk_right
    walk_right
    walk_right
    walk_down
    walk_down
    walk_down
    walk_right
    walk_right
    walk_right
    walk_right
    walk_right
    walk_right
    walk_right
    step_end

MossdeepCity_EventScript_Man::
    lock
    faceplayer
    msgbox MossdeepCity_Text_SurfExhilarating, MSGBOX_DEFAULT
    applymovement VAR_LAST_TALKED, Common_Movement_FaceOriginalDirection
    waitmovement 0
    release
    end

MossdeepCity_EventScript_KingsRockBoy::
    lock
    faceplayer
    goto_if_set FLAG_RECEIVED_KINGS_ROCK, MossdeepCity_EventScript_ReceivedKingsRock
    msgbox MossdeepCity_Text_WantKingsRockStevenGaveMe, MSGBOX_YESNO
    goto_if_eq VAR_RESULT, NO, MossdeepCity_EventScript_DeclineKingsRock
    msgbox MossdeepCity_Text_YouCanKeepIt, MSGBOX_DEFAULT
    giveitem ITEM_KINGS_ROCK
    goto_if_eq VAR_RESULT, FALSE, Common_EventScript_ShowBagIsFull
    setflag FLAG_RECEIVED_KINGS_ROCK
    release
    end

MossdeepCity_EventScript_ReceivedKingsRock::
    msgbox MossdeepCity_Text_StevensHouseOverThere, MSGBOX_DEFAULT
    release
    end

MossdeepCity_EventScript_DeclineKingsRock::
    msgbox MossdeepCity_Text_WhatToDoWithWeirdRock, MSGBOX_DEFAULT
    release
    end

MossdeepCity_EventScript_BlackBelt::
    msgbox MossdeepCity_Text_SootopolisNewGymLeader, MSGBOX_NPC
    end

MossdeepCity_EventScript_Scott::
    lock
    faceplayer
    msgbox MossdeepCity_Text_ScottSomethingWrongWithTown, MSGBOX_DEFAULT
    closemessage
    call_if_eq VAR_FACING, DIR_NORTH, MossdeepCity_EventScript_ScottExitNorth
    call_if_eq VAR_FACING, DIR_EAST, MossdeepCity_EventScript_ScottExitEast
    addvar VAR_SCOTT_STATE, 1
    removeobject LOCALID_SCOTT
    release
    end

MossdeepCity_EventScript_ScottExitNorth::
    applymovement OBJ_EVENT_ID_PLAYER, MossdeepCity_Movement_PlayerWatchScottExit
    applymovement LOCALID_SCOTT, MossdeepCity_Movement_ScottExitNorth
    waitmovement 0
    return

MossdeepCity_EventScript_ScottExitEast::
    applymovement OBJ_EVENT_ID_PLAYER, MossdeepCity_Movement_PlayerWatchScottExit
    applymovement LOCALID_SCOTT, MossdeepCity_Movement_ScottExitEast
    waitmovement 0
    return

MossdeepCity_Movement_PlayerWatchScottExit:
    delay_16
    delay_16
    walk_in_place_faster_left
    step_end

MossdeepCity_Movement_ScottExitNorth:
    walk_left
    walk_left
    walk_left
    walk_left
    walk_left
    walk_left
    walk_left
    walk_left
    walk_left
    step_end

MossdeepCity_Movement_ScottExitEast:
    walk_down
    walk_left
    walk_left
    walk_left
    walk_left
    walk_left
    walk_left
    walk_left
    walk_left
    walk_left
    step_end
`

text MossdeepCity_Text_WantKingsRockStevenGaveMe {
    format("I got this from Steven, but I don't\n"
    "know what it's good for.\p"
    "I think it's called King's Rock.\n"
    "Do you want it?")
}

text MossdeepCity_Text_YouCanKeepIt {
    format("Why would you want it?\n"
    "You're weird.\p"
    "You can keep it, but keep it a secret from Steven.")
}

text MossdeepCity_Text_StevensHouseOverThere {
    format("Steven's house is right over there!")
}

text MossdeepCity_Text_WhatToDoWithWeirdRock {
    format("Yeah, you think so, too! What are you supposed to do with some weird rock?")
}

text MossdeepCity_Text_WailmerWatching {
    format("Around Mossdeep, you can see wild Wailmer.\p"
    "It's called, uh…\n"
    "What was it now…\p"
    "Wai, Wai, Wai…\p"
    "Wailmer watching!")
}

text MossdeepCity_Text_SpaceCenterReceivedLetter {
    format("The island's Space Center has been\n"
    "launching huge rockets.\p"
    "There's been some kind of an uproar\n"
    "over a letter they received recently.")
}

text MossdeepCity_Text_SpaceCenterLaunchingRockets {
    format("The island's Space Center has been\n"
    "launching huge rockets.")
}

text MossdeepCity_Text_MossdeepTargetedByMagma {
    format("I heard from a Sailor buddy that Team Aqua set up shop in Lilycove.\p"
    "I also heard that someone came along and wiped the floor with them!\p"
    "But Mossdeep here's been targeted by that Team Magma.\p"
    "If you want to know what they're up to, go visit the Space Center.")
}

text MossdeepCity_Text_FeelReliefOnLand {
    format("I'm a SAILOR, so the sea's obviously\n"
    "more important to me.\p"
    "But you know? When I get back on land\n"
    "after a long voyage, I do feel relief!")
}

text MossdeepCity_Text_LifeNeedsSeaToLive {
    format("All life needs the sea to live, even though it makes its home on the land.\p"
    "Life, having run its course, becomes soil and returns to the land.\p"
    "And the sea is always connected to the land.\p"
    "Yes, like the very shoreline here.")
}

text MossdeepCity_Text_SurfExhilarating {
    format("A voyage on a ship is fine.\p"
    "But crossing the sea with Pokémon using Surf…\p"
    "Now that's an exhilarating trip!\n"
    "Wouldn't you agree, youngster?")
}

text MossdeepCity_Text_ScottSomethingWrongWithTown {
    format("SCOTT: {PLAYER}{KUN}, feeling good?\n"
    "I'm doing great!\p"
    "I'd heard Mossdeep's Gym Leaders are pretty strong, so I decided to come take a look-see for myself.\p"
    "But there's something wrong about this town.\p"
    "People are going on about a warning letter and the Space Center…\p"
    "I don't think it concerns me in any way, though.")
}

text MossdeepCity_Text_SootopolisNewGymLeader {
    format("Ahh… It feels great letting the waves wash over my feet…\p"
    "Speaking of the waves, you know that island city Sootopolis?\p"
    "I think the Gym there had a new Leader come in.\p"
    "People tell me the new Leader once mentored Wallace.")
}

script MossdeepCity_EventScript_NinjaBoy {
    msgbox(MossdeepCity_Text_WailmerWatching, MSGBOX_NPC)
}

script MossdeepCity_EventScript_ExpertM {
    msgbox(MossdeepCity_Text_LifeNeedsSeaToLive, MSGBOX_NPC)
}

script MossdeepCity_EventScript_Girl {
    msgbox(format("Wouldn't it be nice?\p"
        "If the whole world was covered in plants and flowers like this island?"), MSGBOX_NPC)
}

script MossdeepCity_EventScript_Woman {
    msgbox(format("This rock has a special meaning to the people at the Space Center.\p"
        "They put it here as their wish for their rockets to fly safely.\p"
        "When you make a wish, what do you use?\n"
        "Do you wish upon a star?\p"
        "I use a wish tag to make it happen.\n"
        "That's what I do."), MSGBOX_NPC)
}

script MossdeepCity_EventScript_WhiteRock {
    msgbox(format("It's a white rock."), MSGBOX_SIGN)
}

script MossdeepCity_EventScript_GymSign {
    msgbox(format("Mossdeep City Pokémon Gym\n"
        "Leaders: Liza & Tate\p"
        "“The mystic combination!”"), MSGBOX_SIGN)
}

script MossdeepCity_EventScript_CitySign {
    msgbox(format("Mossdeep City\n"
        "“Our slogan: Cherish Pokémon!”"), MSGBOX_SIGN)
}

script MossdeepCity_EventScript_SpaceCenterSign {
    msgbox(format("Mossdeep Space Center\n"
        "“The closest place to space.”"), MSGBOX_SIGN)
}

script MossdeepCity_EventScript_Hitmonchan {
    lock
    faceplayer
    waitse
    setseenmon(SPECIES_HITMONCHAN)
    playmoncry(SPECIES_HITMONCHAN, CRY_MODE_ENCOUNTER)
    msgbox("HITMONCHAN: Hiyaaah!", MSGBOX_DEFAULT)
    waitmoncry
    release
}
