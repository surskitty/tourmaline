mapscripts MtChimney_MapScripts {
    MAP_SCRIPT_ON_RESUME { setstepcallback(STEP_CB_ASH) }
    MAP_SCRIPT_ON_TRANSITION {
        setvar(VAR_JAGGED_PASS_ASH_WEATHER, 1)
    }
}

raw `
.set LOCALID_ARCHIE, 1
.set LOCALID_MAXIE, 2
.set LOCALID_TABITHA, 3
.set LOCALID_MAGMA_GRUNT_2, 22
.set LOCALID_MAGMA_GRUNT_1, 29

MtChimney_EventScript_Archie::
    lock
    faceplayer
    speakername CommonName_Text_Archie
    call_if_unset FLAG_EVIL_LEADER_PLEASE_STOP, MtChimney_EventScript_ArchieGoStopTeamMagma
    call_if_set FLAG_EVIL_LEADER_PLEASE_STOP, MtChimney_EventScript_ArchieBusyFighting
    closemessage
    applymovement LOCALID_ARCHIE, Common_Movement_FaceOriginalDirection
    waitmovement 0
    setflag FLAG_EVIL_LEADER_PLEASE_STOP
    release
    end

MtChimney_EventScript_ArchieGoStopTeamMagma::
    msgbox MtChimney_Text_ArchieGoStopTeamMagma, MSGBOX_DEFAULT
    return

MtChimney_EventScript_ArchieBusyFighting::
    msgbox MtChimney_Text_ArchieIHaveMyHandsFull, MSGBOX_DEFAULT
    return

MtChimney_EventScript_Maxie::
    lockall
    playbgm MUS_ENCOUNTER_MAGMA, FALSE
    speakername CommonName_Text_Maxie
    msgbox MtChimney_Text_MeteoriteWillActivateVolcano, MSGBOX_DEFAULT
    applymovement LOCALID_MAXIE, Common_Movement_FacePlayer
    waitmovement 0
    playse SE_PIN
    applymovement LOCALID_MAXIE, Common_Movement_ExclamationMark
    waitmovement 0
    applymovement LOCALID_MAXIE, Common_Movement_Delay48
    waitmovement 0
    msgbox MtChimney_Text_MaxieIntro, MSGBOX_DEFAULT
    trainerbattle_no_intro TRAINER_MAXIE_MT_CHIMNEY, MtChimney_Text_MaxieDefeat
    msgbox MtChimney_Text_MaxieYouHaventSeenLastOfMagma, MSGBOX_DEFAULT
    closemessage
    delay 30
    fadescreen FADE_TO_BLACK
    removeobject LOCALID_MAXIE
    removeobject LOCALID_MAGMA_GRUNT_1
    removeobject LOCALID_TABITHA
    removeobject LOCALID_MAGMA_GRUNT_2
    setflag FLAG_HIDE_MT_CHIMNEY_TEAM_MAGMA
    fadescreen FADE_FROM_BLACK
    setobjectxyperm LOCALID_ARCHIE, 10, 12
    addobject LOCALID_ARCHIE
    call_if_eq VAR_FACING, DIR_EAST, MtChimney_EventScript_ArchieApproachPlayerEast
    call_if_eq VAR_FACING, DIR_NORTH, MtChimney_EventScript_ArchieApproachPlayerNorth
    applymovement OBJ_EVENT_ID_PLAYER, Common_Movement_WalkInPlaceFasterLeft
    waitmovement 0
    speakername CommonName_Text_Archie
    msgbox MtChimney_Text_ArchieThankYou, MSGBOX_DEFAULT
    closemessage
    call_if_eq VAR_FACING, DIR_EAST, MtChimney_EventScript_ArchieExitEast
    call_if_eq VAR_FACING, DIR_NORTH, MtChimney_EventScript_ArchieExitNorth
    removeobject LOCALID_ARCHIE
    setflag FLAG_HIDE_MT_CHIMNEY_TEAM_AQUA
    setflag FLAG_DEFEATED_EVIL_TEAM_MT_CHIMNEY
    clearflag FLAG_HIDE_FALLARBOR_HOUSE_PROF_COZMO
    setflag FLAG_HIDE_METEOR_FALLS_1F_1R_COZMO
    clearflag FLAG_HIDE_MT_CHIMNEY_LAVA_COOKIE_LADY
    releaseall
    end

MtChimney_EventScript_ArchieApproachPlayerEast::
    applymovement LOCALID_ARCHIE, MtChimney_Movement_ArchieApproachPlayerEast
    waitmovement 0
    return

MtChimney_EventScript_ArchieApproachPlayerNorth::
    applymovement LOCALID_ARCHIE, MtChimney_Movement_ArchieApproachPlayerNorth
    waitmovement 0
    return

MtChimney_EventScript_ArchieExitEast::
    applymovement LOCALID_ARCHIE, MtChimney_Movement_ArchieExitEast
    waitmovement 0
    return

MtChimney_EventScript_ArchieExitNorth::
    applymovement LOCALID_ARCHIE, MtChimney_Movement_ArchieExitNorth
    waitmovement 0
    return

MtChimney_EventScript_LavaCookieLady::
    lock
    faceplayer
    showmoneybox 0, 0
    msgbox MtChimney_Text_LavaCookiesJust200, MSGBOX_YESNO
    goto_if_eq VAR_RESULT, NO, MtChimney_EventScript_DeclineLavaCookie
    checkmoney 200
    goto_if_eq VAR_RESULT, FALSE, MtChimney_EventScript_NotEnoughMoney
    msgbox MtChimney_Text_ThankYouDear, MSGBOX_DEFAULT
    checkitemspace ITEM_LAVA_COOKIE
    call_if_eq VAR_RESULT, TRUE, MtChimney_EventScript_RemoveMoney
    hidemoneybox
    giveitem ITEM_LAVA_COOKIE
    goto_if_eq VAR_RESULT, FALSE, MtChimney_EventScript_BagIsFull
    release
    end

MtChimney_EventScript_BagIsFull::
    msgbox gText_TooBadBagIsFull, MSGBOX_DEFAULT
    release
    end


MtChimney_EventScript_RemoveMoney::
    removemoney 200
    updatemoneybox
    return

MtChimney_EventScript_DeclineLavaCookie::
    msgbox MtChimney_Text_OhFineThen, MSGBOX_DEFAULT
    hidemoneybox
    release
    end

MtChimney_EventScript_NotEnoughMoney::
    msgbox MtChimney_Text_YouveNotGotTheMoney, MSGBOX_DEFAULT
    hidemoneybox
    release
    end

MtChimney_Movement_ArchieApproachPlayerEast:
    walk_up
    walk_up
    walk_up
    walk_up
    walk_up
    walk_up
    walk_right
    step_end

MtChimney_Movement_ArchieExitEast:
    walk_left
    walk_down
    walk_down
    walk_down
    walk_down
    walk_down
    walk_down
    walk_down
    step_end

MtChimney_Movement_ArchieApproachPlayerNorth:
    walk_up
    walk_up
    walk_up
    walk_up
    walk_up
    walk_right
    walk_right
    step_end

MtChimney_Movement_ArchieExitNorth:
    walk_left
    walk_left
    walk_down
    walk_down
    walk_down
    walk_down
    walk_down
    walk_down
    walk_down
    step_end

MtChimney_EventScript_BusyAquaGrunt1::
    msgbox MtChimney_Text_MagmaOutnumbersUs, MSGBOX_SIGN
    end

MtChimney_EventScript_BusyAquaGrunt2::
    msgbox MtChimney_Text_LessHabitatForWaterPokemon, MSGBOX_SIGN
    end

MtChimney_EventScript_BusyAquaGrunt3::
    msgbox MtChimney_Text_MagmasNameSimilar, MSGBOX_SIGN
    end

MtChimney_EventScript_Tabitha::
    trainerbattle_single TRAINER_TABITHA_MT_CHIMNEY, MtChimney_Text_TabithaIntro, MtChimney_Text_TabithaDefeat
    msgbox MtChimney_Text_TabithaPostBattle, MSGBOX_AUTOCLOSE
    end

MtChimney_EventScript_Grunt2::
    trainerbattle_single TRAINER_GRUNT_MT_CHIMNEY_2, MtChimney_Text_Grunt2Intro, MtChimney_Text_Grunt2Defeat
    msgbox MtChimney_Text_Grunt2PostBattle, MSGBOX_AUTOCLOSE
    end

MtChimney_EventScript_BusyMagmaGrunt1::
    msgbox MtChimney_Text_TeamAquaAlwaysMessingWithPlans, MSGBOX_SIGN
    end

MtChimney_EventScript_BusyMagmaGrunt2::
    msgbox MtChimney_Text_MeteoritesPackAmazingPower, MSGBOX_SIGN
    end

MtChimney_EventScript_BusyMagmaGrunt3::
    msgbox MtChimney_Text_YouBetterNotMessWithUs, MSGBOX_SIGN
    end

MtChimney_EventScript_BusyMagmaGrunt4::
    msgbox MtChimney_Text_AquasNameSimilar, MSGBOX_SIGN
    end

MtChimney_EventScript_BusyMagmaGrunt5::
    msgbox MtChimney_Text_DouseThemInFire, MSGBOX_SIGN
    end

MtChimney_EventScript_BusyMagmaGrunt6::
    msgbox MtChimney_Text_KeepMakingMoreLand, MSGBOX_SIGN
    end

MtChimney_EventScript_MagmaPoochyena::
    msgbox MtChimney_Text_Bufoh, MSGBOX_SIGN
    end

MtChimney_EventScript_AquaPoochyena::
    msgbox MtChimney_Text_Bushaa, MSGBOX_SIGN
    end

MtChimney_EventScript_MeteoriteMachine::
    lockall
    goto_if_unset FLAG_DEFEATED_EVIL_TEAM_MT_CHIMNEY, MtChimney_EventScript_MachineOn
    goto_if_set FLAG_RECEIVED_METEORITE, MtChimney_EventScript_MachineOff
    msgbox MtChimney_Text_RemoveTheMeteorite, MSGBOX_YESNO
    goto_if_eq VAR_RESULT, NO, MtChimney_EventScript_LeaveMeteoriteAlone
    msgbox MtChimney_Text_PlayerRemovedMeteorite, MSGBOX_DEFAULT
    giveitem ITEM_METEORITE
    goto_if_eq VAR_RESULT, FALSE, Common_EventScript_ShowBagIsFull
    setflag FLAG_RECEIVED_METEORITE
    releaseall
    end

MtChimney_EventScript_LeaveMeteoriteAlone::
    msgbox MtChimney_Text_PlayerLeftMeteorite, MSGBOX_DEFAULT
    releaseall
    end

MtChimney_EventScript_MachineOff::
    msgbox MtChimney_Text_MachineMakesNoResponse, MSGBOX_DEFAULT
    releaseall
    end

MtChimney_EventScript_MachineOn::
    msgbox MtChimney_Text_MetoriteFittedOnMachine, MSGBOX_DEFAULT
    releaseall
    end

MtChimney_EventScript_RouteSign::
    msgbox MtChimney_Text_RouteSign, MSGBOX_SIGN
    end

MtChimney_EventScript_Shelby::
    trainerbattle_single TRAINER_SHELBY_1, MtChimney_Text_ShelbyIntro, MtChimney_Text_ShelbyDefeat, MtChimney_EventScript_DefeatedShelby
    specialvar VAR_RESULT, ShouldTryRematchBattle
    goto_if_eq VAR_RESULT, TRUE, MtChimney_EventScript_RematchShelby
    msgbox MtChimney_Text_ShelbyPostBattle, MSGBOX_DEFAULT
    release
    end

MtChimney_EventScript_DefeatedShelby::
    special PlayerFaceTrainerAfterBattle
    waitmovement 0
    msgbox MtChimney_Text_ShelbyRegister, MSGBOX_DEFAULT
    register_matchcall TRAINER_SHELBY_1
    release
    end

MtChimney_EventScript_RematchShelby::
    trainerbattle_rematch TRAINER_SHELBY_1, MtChimney_Text_ShelbyRematchIntro, MtChimney_Text_ShelbyRematchDefeat
    msgbox MtChimney_Text_ShelbyPostRematch, MSGBOX_AUTOCLOSE
    end

MtChimney_EventScript_Melissa::
    trainerbattle_single TRAINER_MELISSA, MtChimney_Text_MelissaIntro, MtChimney_Text_MelissaDefeat
    msgbox MtChimney_Text_MelissaPostBattle, MSGBOX_AUTOCLOSE
    end

MtChimney_EventScript_Sheila::
    trainerbattle_single TRAINER_SHEILA, MtChimney_Text_SheilaIntro, MtChimney_Text_SheilaDefeat
    msgbox MtChimney_Text_SheilaPostBattle, MSGBOX_AUTOCLOSE
    end

MtChimney_EventScript_Shirley::
    trainerbattle_single TRAINER_SHIRLEY, MtChimney_Text_ShirleyIntro, MtChimney_Text_ShirleyDefeat
    msgbox MtChimney_Text_ShirleyPostBattle, MSGBOX_AUTOCLOSE
    end

MtChimney_EventScript_Grunt1::
    trainerbattle_single TRAINER_GRUNT_MT_CHIMNEY_1, MtChimney_Text_Grunt1Intro, MtChimney_Text_Grunt1Defeat
    msgbox MtChimney_Text_Grunt1PostBattle, MSGBOX_AUTOCLOSE
    end

MtChimney_EventScript_Sawyer::
    trainerbattle_single TRAINER_SAWYER_1, MtChimney_Text_SawyerIntro, MtChimney_Text_SawyerDefeat, MtChimney_EventScript_SawyerDefeated
    specialvar VAR_RESULT, ShouldTryRematchBattle
    goto_if_eq VAR_RESULT, TRUE, MtChimney_EventScript_SawyerRematch
    msgbox MtChimney_Text_SawyerPostBattle, MSGBOX_DEFAULT
    release
    end

MtChimney_EventScript_SawyerDefeated::
    special PlayerFaceTrainerAfterBattle
    waitmovement 0
    msgbox MtChimney_Text_SawyerRegister, MSGBOX_DEFAULT
    register_matchcall TRAINER_SAWYER_1
    release
    end

MtChimney_EventScript_SawyerRematch::
    trainerbattle_rematch TRAINER_SAWYER_1, MtChimney_Text_SawyerRematchIntro, MtChimney_Text_SawyerRematchDefeat
    msgbox MtChimney_Text_SawyerPostRematch, MSGBOX_AUTOCLOSE
    end
`

text MtChimney_Text_MeteoriteWillActivateVolcano {
    format("The power contained in the Meteorite…\p"
    "By amplifying its power with this machine, Mt. Chimney's volcanic activity will instantly intensify…\p"
    "Its energy will grow deep inside the crater and…\l"
    "Fufufu…")
}

text MtChimney_Text_MaxieIntro {
    format("Hm?\n"
    "Who are you?\p"
    "… … … … … …\n"
    "… … … … … …\p"
    "I'd heard Archie bemoaning a child who's been meddling in Team Aqua's affairs.\p"
    "It must be you he meant.\p"
    "Humph! So you're thinking of interfering with us, Team Magma?\p"
    "Now you listen.\n"
    "Long ago, living things used the\l"
    "land to live and grow.\p"
    "That is why land is all important!\n"
    "It is the cradle of all!\p"
    "That is why Team Magma is dedicated to the expansion of the land mass.\p"
    "It is for further advancement of\n"
    "humankind and Pokémon!\p"
    "And for that, we need the power of\n"
    "what sleeps within this mountain…\p"
    "Oh! There was no need for you to\n"
    "learn that much.\p"
    "But, no matter!\n"
    "I'll teach you the consequences of\l"
    "meddling in our grand design!")
}

text MtChimney_Text_MaxieDefeat {
    format("What?!\p"
    "I, Maxie, was caught off guard?!")
}

text MtChimney_Text_MaxieYouHaventSeenLastOfMagma {
    format("Enough.\n"
    "I will back off this time.\p"
    "But don't think that this is the last you've seen of Team Magma.\p"
    "Fufufu…\p"
    "Even without the Meteorite, if we obtain that Orb… Fufufu…")
}

text MtChimney_Text_TabithaIntro {
    format("Hehehe!\p"
    "So you've come all the way here!\p"
    "But you're too late!\n"
    "I've already delivered the Meteorite from Meteor Falls to the Boss!")
}

text MtChimney_Text_TabithaDefeat {
    format("Hehehe…\p"
    "Even though I've lost, if our leader awakens that thing…")
}

text MtChimney_Text_TabithaPostBattle {
    format("Boss, hurry!\n"
    "Give it the Meteorite's energy!\p"
    "Hehehe…")
}

text MtChimney_Text_Grunt2Intro {
    format("We of Team Magma are working hard for everyone's sake.\p"
    "Like, if that thing's power made more land, there'd be more places to live.\p"
    "Everyone'd be happy!")
}

text MtChimney_Text_Grunt2Defeat {
    format("Hunh?\n"
    "What do you mean I lost?")
}

text MtChimney_Text_Grunt2PostBattle {
    format("Our Boss says, “It will make everyone happy.”\p"
    "But why does everyone keep getting in our way?")
}

text MtChimney_Text_Grunt1Intro {
    format("If there were more land, I'd be able\n"
    "to get a big house of my own!\p"
    "I'm going to build it on hardened lava!")
}

text MtChimney_Text_Grunt1Defeat {
    format("My dream of a big house…")
}

text MtChimney_Text_Grunt1PostBattle {
    format("A kid like you, you ought to be splashing about in the waves!")
}

text MtChimney_Text_TeamAquaAlwaysMessingWithPlans {
    format("That annoying Team Aqua…\n"
    "They always mess with our plans!")
}

text MtChimney_Text_MeteoritesPackAmazingPower {
    format("Meteorites pack amazing power!")
}

text MtChimney_Text_YouBetterNotMessWithUs {
    format("You'd better not mess with us!\p"
    "We're trying to awaken that thing\n"
    "for the benefit of everyone!")
}

text MtChimney_Text_AquasNameSimilar {
    format("We're Team Magma!\p"
    "They're Team Aqua!\p"
    "It totally annoys me that they'd\n"
    "use a name like ours!")
}

text MtChimney_Text_DouseThemInFire {
    format("Yeah!\n"
    "Douse them in fire!")
}

text MtChimney_Text_KeepMakingMoreLand {
    format("We're going to keep making more land!")
}

text MtChimney_Text_Bufoh {
    format("Bufoh!")
}

text MtChimney_Text_ArchieGoStopTeamMagma {
    format("Grr, {PLAYER}!\n"
    "I should've guessed you'd show up!\p"
    "See for yourself what the fanatics\n"
    "of Team Magma are up to!\p"
    "They're trying to inject the stolen\n"
    "Meteorite's power into the volcano!\p"
    "Doing something like that will cause\n"
    "the volcano's eruption!")
}

text MtChimney_Text_ArchieIHaveMyHandsFull {
    format("Grrr…\p"
    "I want to stop that Maxie,\n"
    "but I can't!\p"
    "Not when I have my hands full battling\n"
    "three opponents at once!")
}

text MtChimney_Text_ArchieThankYou {
    format("{PLAYER}!\n"
    "Thank you!\p"
    "With your help, we thwarted Team Magma's destructive plan!\p"
    "But… You…\n"
    "Whose side are you on?\p"
    "Ah, it doesn't matter.\p"
    "We will remain vigilant and keep up our pursuit of Team Magma.\p"
    "{PLAYER}, we shall meet again!")
}

text MtChimney_Text_MagmaOutnumbersUs {
    format("Darn… Team Magma outnumbers us!\n"
    "We can't keep up with them!")
}

text MtChimney_Text_LessHabitatForWaterPokemon {
    format("If they expand the land, there'll be\n"
    "less habitats for Water Pokémon!")
}

text MtChimney_Text_MagmasNameSimilar {
    format("We're Team Aqua!\p"
    "They're Team Magma!\p"
    "It burns me up that they'd use such\n"
    "a confusing name!")
}

text MtChimney_Text_Bushaa {
    format("Bushaa!")
}

text MtChimney_Text_LavaCookiesJust200 {
    format("Lava Cookies are Mt. Chimney's local specialty.\p"
    "Try one. It's just ¥200.")
}

text MtChimney_Text_ThankYouDear {
    format("Thank you, dear!")
}

text MtChimney_Text_YouveNotGotTheMoney {
    format("Oh, dear. You can't buy a thing if\n"
    "you've not got the money.")
}

text MtChimney_Text_OhFineThen {
    format("Oh, fine then.")
}

text MtChimney_Text_MetoriteFittedOnMachine {
    format("A Meteorite is fitted on a mysterious\n"
    "machine…\p"
    "The machine seems to be storing\n"
    "energy in the Meteorite.")
}

text MtChimney_Text_RemoveTheMeteorite {
    format("A Meteorite is fitted on a mysterious machine…\p"
    "Do you want to remove the Meteorite?")
}

text MtChimney_Text_PlayerRemovedMeteorite {
    format("{PLAYER} removed the Meteorite from\n"
    "the mysterious machine.")
}

text MtChimney_Text_PlayerLeftMeteorite {
    format("{PLAYER} left the Meteorite where\n"
    "it was.")
}

text MtChimney_Text_MachineMakesNoResponse {
    format("This mysterious machine…\n"
    "It makes no response whatsoever.")
}

text MtChimney_Text_RouteSign {
    format("{DOWN_ARROW} Jagged Pass\n"
    "Lavaridge Town ahead")
}

text MtChimney_Text_ShelbyIntro {
    format("I've been to the hot springs and refreshed my tired bones.\l"
    "Right now I'm feeling strong!")
}

text MtChimney_Text_ShelbyDefeat {
    format("Oh, my goodness.\n"
    "Now, aren't you something!")
}

text MtChimney_Text_ShelbyPostBattle {
    format("Well, well, I've lost. I can't call\n"
    "myself an Expert now, can I?")
}

text MtChimney_Text_ShelbyRegister {
    format("Thank you, child. It was fun, as if\n"
    "I were battling my own grandchild.\p"
    "Please, come see me again for a rematch.")
}

text MtChimney_Text_ShelbyRematchIntro {
    format("If you can mesh your heart with those of your Pokémon, why, you should be able to achieve great things.")
}

text MtChimney_Text_ShelbyRematchDefeat {
    format("Oh, my goodness.\n"
    "Now, aren't you something!")
}

text MtChimney_Text_ShelbyPostRematch {
    format("Perhaps your heart has become one with the hearts of your Pokémon.")
}

text MtChimney_Text_MelissaIntro {
    format("I've got the fire in me, baby.\n"
    "I can't stand it! I have to battle!")
}

text MtChimney_Text_MelissaDefeat {
    format("Ooh, that was a scorching-hot match!")
}

text MtChimney_Text_MelissaPostBattle {
    format("The heat of Mt. Chimney warms me up, baby!")
}

text MtChimney_Text_SheilaIntro {
    format("I've finally made it to Mt. Chimney.\n"
    "I want to make my Pokémon battle!")
}

text MtChimney_Text_SheilaDefeat {
    format("The way you battle…\n"
    "It's like a Mt. Chimney eruption!")
}

text MtChimney_Text_SheilaPostBattle {
    format("Like I said, I've finally made it to Mt. Chimney. It would be a shame if I only do a little sightseeing…\p"
    "I want to get in some battles and buy Cookies as souvenirs.")
}

text MtChimney_Text_ShirleyIntro {
    format("Since I bathed in the hot springs,\n"
    "I've been feeling great!\l"
    "I'm sure I'm going to win!")
}

text MtChimney_Text_ShirleyDefeat {
    format("Yowch!\n"
    "I'm getting a chill out of the water.")
}

text MtChimney_Text_ShirleyPostBattle {
    format("I'll have to take another dip in the hot springs. Want to join me?\p"
    "Just joking!")
}

text MtChimney_Text_SawyerIntro {
    format("This is one fine mountain! Plenty of hot people around for company!")
}

text MtChimney_Text_SawyerDefeat {
    format("Oh, you're a real firebrand, too!")
}

text MtChimney_Text_SawyerPostBattle {
    format("I think I need a dip in Lavaridge Hot Spring with the locals!")
}

text MtChimney_Text_SawyerRegister {
    format("I like little fireballs like you.\n"
    "Let me register you in my Pokénav.")
}

text MtChimney_Text_SawyerRematchIntro {
    format("I'm happily surrounded by hot people around these parts. I won't lose!")
}

text MtChimney_Text_SawyerRematchDefeat {
    format("Gosh, you're still the same firebrand as before!")
}

text MtChimney_Text_SawyerPostRematch {
    format("Actually, it really is hot here.\n"
    "I'm overdressed for these parts.")
}

