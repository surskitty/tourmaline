mapscripts MtPyre_Summit_MapScripts {
    MAP_SCRIPT_ON_TRANSITION {
        call_if_eq(VAR_MT_PYRE_STATE, 2, MtPyre_Summit_EventScript_SetArchieMaxiePositions)
    }
    MAP_SCRIPT_ON_LOAD {
        special(SetRoofBirds)
        special(SetStationaryWilds)
    }
}

raw `
.set LOCALID_ARCHIE, 2
.set LOCALID_OLD_LADY, 3
.set LOCALID_GRUNT_1, 4
.set LOCALID_GRUNT_2, 5
.set LOCALID_GRUNT_3, 6
.set LOCALID_GRUNT_4, 7
.set LOCALID_MAXIE, 8

MtPyre_Summit_EventScript_SetArchieMaxiePositions::
    setobjectxyperm LOCALID_MAXIE, 23, 6
    setobjectxyperm LOCALID_ARCHIE, 22, 6
    end

MtPyre_Summit_EventScript_TeamAquaTrigger0::
    lockall
    setvar VAR_0x8008, 0
    goto MtPyre_Summit_EventScript_TeamAquaExits
    end

MtPyre_Summit_EventScript_TeamAquaTrigger1::
    lockall
    setvar VAR_0x8008, 1
    goto MtPyre_Summit_EventScript_TeamAquaExits
    end

MtPyre_Summit_EventScript_TeamAquaTrigger2::
    lockall
    setvar VAR_0x8008, 2
    goto MtPyre_Summit_EventScript_TeamAquaExits
    end

MtPyre_Summit_EventScript_TeamAquaExits::
    playbgm MUS_ENCOUNTER_AQUA, FALSE
    applymovement OBJ_EVENT_ID_PLAYER, Common_Movement_FaceUp
    waitmovement 0
    applymovement LOCALID_ARCHIE, Common_Movement_WalkInPlaceFasterDown
    waitmovement 0
    delay 50
    call_if_eq VAR_0x8008, 0, MtPyre_Summit_EventScript_ArchieFacePlayer0
    call_if_eq VAR_0x8008, 1, MtPyre_Summit_EventScript_ArchieFacePlayer1
    call_if_eq VAR_0x8008, 2, MtPyre_Summit_EventScript_ArchieFacePlayer2
    speakername CommonName_Text_Archie
    msgbox MtPyre_Summit_Text_ArchieWeGotTheOrbLetsGo, MSGBOX_DEFAULT
    closemessage
    fadescreen FADE_TO_BLACK
    removeobject LOCALID_ARCHIE
    removeobject LOCALID_GRUNT_1
    removeobject LOCALID_GRUNT_2
    removeobject LOCALID_GRUNT_3
    removeobject LOCALID_GRUNT_4
    setflag FLAG_HIDE_MT_PYRE_SUMMIT_ARCHIE
    setflag FLAG_HIDE_MT_PYRE_SUMMIT_TEAM_AQUA
    fadedefaultbgm
    fadescreen FADE_FROM_BLACK
    delay 20
    setvar VAR_MT_PYRE_STATE, 1
    call_if_eq VAR_0x8008, 0, MtPyre_Summit_EventScript_OldLadyApproachPlayer0
    call_if_eq VAR_0x8008, 1, MtPyre_Summit_EventScript_OldLadyApproachPlayer1
    call_if_eq VAR_0x8008, 2, MtPyre_Summit_EventScript_OldLadyApproachPlayer2
    speakername 0
    msgbox MtPyre_Summit_Text_BothOrbsTakenMagmaLeftThis, MSGBOX_DEFAULT
    giveitem ITEM_MAGMA_EMBLEM
    setflag FLAG_RECEIVED_RED_OR_BLUE_ORB
    setflag FLAG_HIDE_JAGGED_PASS_MAGMA_GUARD
    releaseall
    end

MtPyre_Summit_EventScript_ArchieFacePlayer0::
    applymovement LOCALID_ARCHIE, MtPyre_Summit_Movement_ArchieFacePlayer0
    waitmovement 0
    return

@ Archie is already facing player
MtPyre_Summit_EventScript_ArchieFacePlayer1::
    return

MtPyre_Summit_EventScript_ArchieFacePlayer2::
    applymovement LOCALID_ARCHIE, MtPyre_Summit_Movement_ArchieFacePlayer2
    waitmovement 0
    return

MtPyre_Summit_EventScript_OldLadyApproachPlayer0::
    applymovement LOCALID_OLD_LADY, MtPyre_Summit_Movement_OldLadyApproachPlayer0
    waitmovement 0
    applymovement OBJ_EVENT_ID_PLAYER, Common_Movement_WalkInPlaceFasterRight
    waitmovement 0
    return

MtPyre_Summit_EventScript_OldLadyApproachPlayer1::
    applymovement LOCALID_OLD_LADY, MtPyre_Summit_Movement_OldLadyApproachPlayer1
    waitmovement 0
    return

MtPyre_Summit_EventScript_OldLadyApproachPlayer2::
    applymovement LOCALID_OLD_LADY, MtPyre_Summit_Movement_OldLadyApproachPlayer2
    waitmovement 0
    applymovement OBJ_EVENT_ID_PLAYER, Common_Movement_WalkInPlaceFasterLeft
    waitmovement 0
    return

MtPyre_Summit_Movement_OldLadyApproachPlayer0:
    walk_down
    walk_down
    walk_in_place_faster_left
    step_end

MtPyre_Summit_Movement_OldLadyApproachPlayer1:
    walk_down
    step_end

MtPyre_Summit_Movement_OldLadyApproachPlayer2:
    walk_down
    walk_down
    walk_in_place_faster_right
    step_end

MtPyre_Summit_Movement_ArchieFacePlayer0:
    walk_left
    walk_in_place_faster_down
    step_end

MtPyre_Summit_Movement_ArchieFacePlayer2:
    walk_right
    walk_in_place_faster_down
    step_end

MtPyre_Summit_EventScript_OldMan::
    lock
    faceplayer
    goto_if_set FLAG_SOOTOPOLIS_ARCHIE_MAXIE_LEAVE, MtPyre_Summit_EventScript_OldManAfterRayquaza
    msgbox MtPyre_Summit_Text_WillYouHearOutMyTale, MSGBOX_YESNO
    call_if_eq VAR_RESULT, YES, MtPyre_Summit_EventScript_OldManTale
    call_if_eq VAR_RESULT, NO, MtPyre_Summit_EventScript_DeclineOldManTale
    release
    end

MtPyre_Summit_EventScript_OldManAfterRayquaza::
    msgbox MtPyre_Summit_Text_HearTheNewLegendOfHoenn, MSGBOX_YESNO
    call_if_eq VAR_RESULT, YES, MtPyre_Summit_EventScript_OldManNewTale
    call_if_eq VAR_RESULT, NO, MtPyre_Summit_EventScript_DeclineOldManTale
    release
    end

MtPyre_Summit_EventScript_OldManTale::
    msgbox MtPyre_Summit_Text_GroudonKyogreTale, MSGBOX_DEFAULT
    return

MtPyre_Summit_EventScript_DeclineOldManTale::
    msgbox MtPyre_Summit_Text_WellThatTooIsFine, MSGBOX_DEFAULT
    return

MtPyre_Summit_EventScript_OldManNewTale::
    msgbox MtPyre_Summit_Text_HoennTrioTale, MSGBOX_DEFAULT
    return

MtPyre_Summit_EventScript_OldLady::
    lock
    faceplayer
    goto_if_set FLAG_RETURNED_RED_OR_BLUE_ORB, MtPyre_Summit_EventScript_OldLadyAfterOrbsReturned
    call_if_ge VAR_MT_PYRE_STATE, 3, MtPyre_Summit_EventScript_OldLadyOrbsReturned
    goto_if_set FLAG_KYOGRE_ESCAPED_SEAFLOOR_CAVERN, MtPyre_Summit_EventScript_OldLadyLegendariesAwake
    msgbox MtPyre_Summit_Text_OrbsHaveBeenTaken, MSGBOX_DEFAULT
    release
    end

MtPyre_Summit_EventScript_OldLadyOrbsReturned::
    msgbox MtPyre_Summit_Text_ThoseTwoMenReturnedOrbs, MSGBOX_DEFAULT
    setflag FLAG_RETURNED_RED_OR_BLUE_ORB
    release
    end

MtPyre_Summit_EventScript_OldLadyLegendariesAwake::
    msgbox MtPyre_Summit_Text_GroudonKyogreAwakened, MSGBOX_DEFAULT
    release
    end

MtPyre_Summit_EventScript_OldLadyAfterOrbsReturned::
    msgbox MtPyre_Summit_Text_SuperAncientPokemonTaughtUs, MSGBOX_DEFAULT
    release
    end

MtPyre_Summit_EventScript_ArchieMaxieTrigger0::
    lockall
    setvar VAR_0x8008, 0
    goto MtPyre_Summit_EventScript_ArchieMaxieReturnOrbs
    end

MtPyre_Summit_EventScript_ArchieMaxieTrigger1::
    lockall
    setvar VAR_0x8008, 1
    goto MtPyre_Summit_EventScript_ArchieMaxieReturnOrbs
    end

MtPyre_Summit_EventScript_ArchieMaxieTrigger2::
    lockall
    setvar VAR_0x8008, 2
    goto MtPyre_Summit_EventScript_ArchieMaxieReturnOrbs
    end

MtPyre_Summit_EventScript_ArchieMaxieReturnOrbs::
    applymovement OBJ_EVENT_ID_PLAYER, Common_Movement_WalkInPlaceFasterUp
    waitmovement 0
    delay 60
    call_if_eq VAR_0x8008, 0, MtPyre_Summit_EventScript_ArchieMaxieBeginExit0
    call_if_eq VAR_0x8008, 1, MtPyre_Summit_EventScript_ArchieMaxieBeginExit1
    call_if_eq VAR_0x8008, 2, MtPyre_Summit_EventScript_ArchieMaxieBeginExit2
    playse SE_PIN
    applymovement LOCALID_MAXIE, Common_Movement_ExclamationMark
    waitmovement 0
    applymovement LOCALID_MAXIE, Common_Movement_Delay48
    waitmovement 0
    delay 30
    call_if_eq VAR_0x8008, 0, MtPyre_Summit_EventScript_MaxieApproachPlayer0
    call_if_eq VAR_0x8008, 1, MtPyre_Summit_EventScript_MaxieApproachPlayer1
    call_if_eq VAR_0x8008, 2, MtPyre_Summit_EventScript_MaxieApproachPlayer2
    speakername CommonName_Text_Maxie
    msgbox MtPyre_Summit_Text_MaxieSilence, MSGBOX_DEFAULT
    closemessage
    call_if_eq VAR_0x8008, 0, MtPyre_Summit_EventScript_MaxieApproachArchie0
    call_if_eq VAR_0x8008, 1, MtPyre_Summit_EventScript_MaxieApproachArchie1
    call_if_eq VAR_0x8008, 2, MtPyre_Summit_EventScript_MaxieApproachArchie2
    delay 30
    applymovement LOCALID_ARCHIE, MtPyre_Summit_Movement_ArchieExit
    applymovement LOCALID_MAXIE, MtPyre_Summit_Movement_MaxieExit
    waitmovement 0
    removeobject LOCALID_ARCHIE
    removeobject LOCALID_MAXIE
    setvar VAR_MT_PYRE_STATE, 3
    releaseall
    end

MtPyre_Summit_EventScript_ArchieMaxieBeginExit0::
    applymovement OBJ_EVENT_ID_PLAYER, MtPyre_Summit_Movement_PlayerWatchArchieMaxieExit0
    applymovement LOCALID_ARCHIE, MtPyre_Summit_Movement_ArchieExit
    applymovement LOCALID_MAXIE, MtPyre_Summit_Movement_MaxieExit
    waitmovement 0
    return

MtPyre_Summit_EventScript_ArchieMaxieBeginExit1::
    applymovement OBJ_EVENT_ID_PLAYER, MtPyre_Summit_Movement_PlayerWatchArchieMaxieExit1
    applymovement LOCALID_ARCHIE, MtPyre_Summit_Movement_ArchieExit
    applymovement LOCALID_MAXIE, MtPyre_Summit_Movement_MaxieExit
    waitmovement 0
    return

MtPyre_Summit_EventScript_ArchieMaxieBeginExit2::
    applymovement OBJ_EVENT_ID_PLAYER, MtPyre_Summit_Movement_PlayerWatchArchieMaxieExit2
    applymovement LOCALID_ARCHIE, MtPyre_Summit_Movement_ArchieExit
    applymovement LOCALID_MAXIE, MtPyre_Summit_Movement_MaxieExit
    waitmovement 0
    return

MtPyre_Summit_EventScript_MaxieApproachPlayer0::
    applymovement OBJ_EVENT_ID_PLAYER, MtPyre_Summit_Movement_PlayerFaceMaxie0
    applymovement LOCALID_ARCHIE, MtPyre_Summit_Movement_ArchieWatchMaxie
    applymovement LOCALID_MAXIE, MtPyre_Summit_Movement_MaxieApproachPlayer0
    waitmovement 0
    return

MtPyre_Summit_EventScript_MaxieApproachPlayer1::
    applymovement OBJ_EVENT_ID_PLAYER, MtPyre_Summit_Movement_PlayerFaceMaxie
    applymovement LOCALID_ARCHIE, MtPyre_Summit_Movement_ArchieWatchMaxie
    applymovement LOCALID_MAXIE, MtPyre_Summit_Movement_MaxieApproachPlayer1
    waitmovement 0
    return

MtPyre_Summit_EventScript_MaxieApproachPlayer2::
    applymovement OBJ_EVENT_ID_PLAYER, MtPyre_Summit_Movement_PlayerFaceMaxie
    applymovement LOCALID_ARCHIE, MtPyre_Summit_Movement_ArchieWatchMaxie
    applymovement LOCALID_MAXIE, MtPyre_Summit_Movement_MaxieApproachPlayer2
    waitmovement 0
    return

MtPyre_Summit_EventScript_MaxieApproachArchie0::
    applymovement OBJ_EVENT_ID_PLAYER, MtPyre_Summit_Movement_PlayerWatchMaxie
    applymovement LOCALID_MAXIE, MtPyre_Summit_Movement_MaxieApproachArchie0
    waitmovement 0
    applymovement LOCALID_ARCHIE, Common_Movement_WalkInPlaceFasterRight
    waitmovement 0
    return

MtPyre_Summit_EventScript_MaxieApproachArchie1::
    applymovement OBJ_EVENT_ID_PLAYER, MtPyre_Summit_Movement_PlayerWatchMaxie
    applymovement LOCALID_MAXIE, MtPyre_Summit_Movement_MaxieApproachArchie1
    waitmovement 0
    applymovement LOCALID_ARCHIE, Common_Movement_WalkInPlaceFasterRight
    waitmovement 0
    return

MtPyre_Summit_EventScript_MaxieApproachArchie2::
    applymovement OBJ_EVENT_ID_PLAYER, MtPyre_Summit_Movement_PlayerWatchMaxie
    applymovement LOCALID_MAXIE, MtPyre_Summit_Movement_MaxieApproachArchie2
    waitmovement 0
    applymovement LOCALID_ARCHIE, Common_Movement_WalkInPlaceFasterRight
    waitmovement 0
    return

MtPyre_Summit_Movement_PlayerFaceMaxie0:
    delay_16
    delay_16
    walk_in_place_faster_right
    step_end

MtPyre_Summit_Movement_PlayerFaceMaxie:
    delay_16
    delay_16
    walk_in_place_faster_left
    step_end

MtPyre_Summit_Movement_ArchieExit:
    walk_down
    walk_down
    walk_down
    walk_down
    walk_down
    walk_down
    step_end

MtPyre_Summit_Movement_MaxieExit:
    walk_down
    walk_down
    walk_down
    walk_down
    walk_down
    walk_down
    step_end

MtPyre_Summit_Movement_PlayerWatchArchieMaxieExit0:
    delay_16
    delay_8
    walk_left
    walk_in_place_faster_right
    delay_16
    walk_in_place_faster_down
    step_end

MtPyre_Summit_Movement_PlayerWatchArchieMaxieExit1:
    delay_16
    delay_8
    walk_right
    walk_in_place_faster_left
    delay_16
    walk_in_place_faster_down
    step_end

MtPyre_Summit_Movement_PlayerWatchArchieMaxieExit2:
    delay_16
    delay_8
    walk_in_place_faster_left
    delay_16
    walk_in_place_faster_down
    step_end

MtPyre_Summit_Movement_MaxieApproachPlayer0:
    walk_up
    walk_up
    walk_up
    walk_left
    step_end

MtPyre_Summit_Movement_MaxieApproachPlayer1:
    walk_up
    walk_up
    walk_up
    walk_in_place_faster_right
    step_end

MtPyre_Summit_Movement_MaxieApproachPlayer2:
    walk_up
    walk_up
    walk_up
    walk_in_place_faster_right
    step_end

MtPyre_Summit_Movement_ArchieWatchMaxie:
    delay_16
    walk_in_place_faster_up
    step_end

MtPyre_Summit_Movement_PlayerWatchMaxie:
    delay_16
    walk_in_place_faster_down
    step_end

MtPyre_Summit_Movement_MaxieApproachArchie0:
    walk_down
    walk_right
    walk_down
    walk_down
    walk_in_place_faster_left
    step_end

MtPyre_Summit_Movement_MaxieApproachArchie1:
    walk_down
    walk_down
    walk_down
    walk_in_place_faster_left
    step_end

MtPyre_Summit_Movement_MaxieApproachArchie2:
    walk_down
    walk_down
    walk_down
    walk_in_place_faster_left
    step_end

MtPyre_Summit_EventScript_Grunt1::
    trainerbattle_single TRAINER_GRUNT_MT_PYRE_1, MtPyre_Summit_Text_Grunt1Intro, MtPyre_Summit_Text_Grunt1Defeat
    msgbox MtPyre_Summit_Text_Grunt1PostBattle, MSGBOX_AUTOCLOSE
    end

MtPyre_Summit_EventScript_Grunt2::
    trainerbattle_single TRAINER_GRUNT_MT_PYRE_2, MtPyre_Summit_Text_Grunt2Intro, MtPyre_Summit_Text_Grunt2Defeat
    msgbox MtPyre_Summit_Text_Grunt2PostBattle, MSGBOX_AUTOCLOSE
    end

MtPyre_Summit_EventScript_Grunt3::
    trainerbattle_single TRAINER_GRUNT_MT_PYRE_3, MtPyre_Summit_Text_Grunt3Intro, MtPyre_Summit_Text_Grunt3Defeat
    msgbox MtPyre_Summit_Text_Grunt3PostBattle, MSGBOX_AUTOCLOSE
    end

MtPyre_Summit_EventScript_Grunt4::
    trainerbattle_single TRAINER_GRUNT_MT_PYRE_4, MtPyre_Summit_Text_Grunt4Intro, MtPyre_Summit_Text_Grunt4Defeat
    msgbox MtPyre_Summit_Text_Grunt4PostBattle, MSGBOX_AUTOCLOSE
    end
`

text MtPyre_Summit_Text_Grunt1Intro {
    format("No! Those Team Magma goons got here ahead of us!\p"
    "We can't fall behind!")
}

text MtPyre_Summit_Text_Grunt1Defeat {
    format("I thought you were one of the Magmas who happened to come back…")
}

text MtPyre_Summit_Text_Grunt1PostBattle {
    format("Yeah, so you are strong…\n"
    "But there're a lot of us!")
}

text MtPyre_Summit_Text_Grunt2Intro {
    format("Hah!\n"
    "Too bad for you!\p"
    "If you came earlier, you only would've\n"
    "had to battle the wimpy Team Magma.\p"
    "But since you arrived now, you have\n"
    "to take on us toughies!")
}

text MtPyre_Summit_Text_Grunt2Defeat {
    format("Urgh… I should've let you take on Team Magma first…")
}

text MtPyre_Summit_Text_Grunt2PostBattle {
    format("You don't know anything!\n"
    "So why are you messing with us?")
}

text MtPyre_Summit_Text_Grunt3Intro {
    format("You…\n"
    "We saw you at Mt. Chimney.\p"
    "You don't belong to either Team,\n"
    "so why would you be here?")
}

text MtPyre_Summit_Text_Grunt3Defeat {
    format("If you're going to mess with anyone,\n"
    "let it be Team Magma…")
}

text MtPyre_Summit_Text_Grunt3PostBattle {
    format("Heh, it doesn't matter!\n"
    "We bought ourselves some time!\p"
    "The Boss should have snatched what\n"
    "he was after!")
}

text MtPyre_Summit_Text_Grunt4Intro {
    format("Oh, I know!\n"
    "You tried to join Team Magma,\l"
    "but they wouldn't have you!\p"
    "Well, don't think that we'll let you\n"
    "join our Team Aqua!")
}

text MtPyre_Summit_Text_Grunt4Defeat {
    format("If you want to join Team Aqua that\n"
    "badly, we can consider it…")
}

text MtPyre_Summit_Text_Grunt4PostBattle {
    format("We have a great combination going\n"
    "with us members and our leader.")
}

text MtPyre_Summit_Text_ArchieWeGotTheOrbLetsGo {
    format("Team Magma's Maxie got ahead of us, but we also got what we wanted.\p"
    "The Red Orb preserved at Mt. Pyre…\n"
    "I, Archie, now have it in my possession!\p"
    "Fufufu… Now we can bring our ultimate\n"
    "objective to fruition!\p"
    "Okay, Team!\n"
    "We're pulling out!")
}

text MtPyre_Summit_Text_BothOrbsTakenMagmaLeftThis {
    format("Oh, no…\n"
    "This cannot happen…\p"
    "Not only the Blue Orb, but even\n"
    "the Red Orb has been taken…\p"
    "The Blue Orb and Red Orb must never\n"
    "be separated. They belong together.\p"
    "What are those men trying to do with\n"
    "the two Orbs?\p"
    "… … … … … …\n"
    "… … … … … …\p"
    "Oh, yes. Was it Team Magma who came\n"
    "and took the Blue Orb first?\p"
    "Well, in their haste, they left this behind.\p"
    "I couldn't imagine what exactly it could be.\p"
    "I would like you to have it.\n"
    "Perhaps it will be useful in some way.")
}

text MtPyre_Summit_Text_OrbsHaveBeenTaken {
    format("The Blue Orb and Red Orb taken by those sinister men…\p"
    "They must never be apart…\n"
    "I fear something terrible will happen…")
}

text MtPyre_Summit_Text_GroudonKyogreAwakened {
    format("Oh, my goodness…\p"
    "You say that both Groudon and Kyogre have been awakened?\p"
    "It is said that the Blue Orb and the Red Orb quelled the rage of the two Pokémon…\p"
    "It is also said that the true owner\n"
    "of the Orbs still exists.\p"
    "However, no one knows who, or what\n"
    "exactly, the Orbs belong to.\p"
    "It could be a human, or perhaps\n"
    "a Pokémon, but no one knows.")
}

text MtPyre_Summit_Text_ThoseTwoMenReturnedOrbs {
    format("The two men who took the Orbs came back to return them on their own.\p"
    "Those men…\n"
    "Perhaps they are not so evil after all…")
}

text MtPyre_Summit_Text_SuperAncientPokemonTaughtUs {
    format("The embodiments of the land, sea, and the sky…\p"
    "That is said to be the identities of\n"
    "the super-ancient Pokémon…\p"
    "In the same way that we humans and\n"
    "Pokémon have life, our world, too,\l"
    "is alive…\p"
    "The super-ancient Pokémon have\n"
    "taught us that…")
}

text MtPyre_Summit_Text_WillYouHearOutMyTale {
    format("Mt. Pyre is where we calm the spirits\n"
    "of Pokémon that have passed on…\p"
    "This is a high place where one can\n"
    "see all of Hoenn…\p"
    "The disembodied spirits of Pokémon\n"
    "must find solace here…\p"
    "Is it coincidence that you are here…\p"
    "Will you hear out my tale? A tale long\n"
    "told in the Hoenn region?")
}

text MtPyre_Summit_Text_GroudonKyogreTale {
    format("It happened long, long ago…\p"
    "The world was wracked by a ferocious\n"
    "clash between the Pokémon of the land\l"
    "and the Pokémon of the sea…\p"
    "The Pokémon of the land raised\n"
    "mountains and created continents…\p"
    "The Pokémon of the sea raised huge\n"
    "waves and carved out oceans…\p"
    "Their fierce duel raged without end.\p"
    "The world was engulfed in massive\n"
    "infernos and vicious storms.\p"
    "The Blue Orb and the Red Orb brought\n"
    "an end to the calamity.\p"
    "Light poured from the two Orbs and\n"
    "becalmed the enraged adversaries.\p"
    "The pair, made docile, dove deep into\n"
    "the sea where eventually they\l"
    "disappeared…")
}

text MtPyre_Summit_Text_WellThatTooIsFine {
    format("I see…\n"
    "Well, that, too, is fine…")
}

text MtPyre_Summit_Text_MaxieSilence {
    format("{PLAYER}…\p"
    "… … … … … …\n"
    "… … … … … …")
}

text MtPyre_Summit_Text_HearTheNewLegendOfHoenn {
    format("It is my role to pass on the legends of Hoenn to future generations.\p"
    "And the crisis that just ended in Sootopolis rewrote a legend…\p"
    "Have you the time to hear the new legend of Hoenn?")
}

text MtPyre_Summit_Text_HoennTrioTale {
    format("It happened long, long ago…\p"
    "The world was wracked by a ferocious\n"
    "clash between the Pokémon of the land\l"
    "and the Pokémon of the sea…\p"
    "The Pokémon of the land raised\n"
    "mountains and created continents…\p"
    "The Pokémon of the sea raised huge\n"
    "waves and carved out oceans…\p"
    "Their fierce duel raged without end.\p"
    "The world was engulfed in massive\n"
    "fires and vicious storms.\p"
    "It was then that the Pokémon of\n"
    "the sky descended from a storm.\p"
    "The Pokémon, shining a vivid green,\n"
    "becalmed the two enraged Pokémon.\p"
    "Then, its duty done, the green Pokémon flew off into the wild blue yonder…")
}

