mapscripts OldaleTown_Mart_MapScripts {}

script OldaleTown_Mart_EventScript_Clerk {
    lock
    faceplayer
    message(gText_HowMayIServeYou)
    waitmessage
    if (flag(FLAG_ADVENTURE_STARTED)) {
        pokemart(OldaleTown_Mart_Pokemart_Expanded)
    } else {
        pokemart(OldaleTown_Mart_Pokemart_Basic)
    }
    msgbox(gText_PleaseComeAgain, MSGBOX_DEFAULT)
    release
}

mart OldaleTown_Mart_Pokemart_Basic {
    ITEM_POTION
    ITEM_REPEL
    ITEM_LURE
    ITEM_ANTIDOTE
    ITEM_PARALYZE_HEAL
    ITEM_AWAKENING
}

mart OldaleTown_Mart_Pokemart_Expanded {
    ITEM_POKE_BALL
    ITEM_POTION
    ITEM_REPEL
    ITEM_LURE
    ITEM_ANTIDOTE
    ITEM_PARALYZE_HEAL
    ITEM_AWAKENING
}

script OldaleTown_Mart_EventScript_Woman {
    lock
    faceplayer
    if (flag(FLAG_ADVENTURE_STARTED)) {
        msgbox(OldaleTown_Mart_Text_ImGoingToBuyPokeBalls, MSGBOX_DEFAULT)
        release
    } else {
        msgbox(OldaleTown_Mart_Text_PokeBallsAreSoldOut, MSGBOX_DEFAULT)
        release
    }
}

script OldaleTown_Mart_EventScript_Boy {
    msgbox(format("You can use a Lure to attract wild Pokémon, or a Repel to keep them away.\p"
        "I heard what Pokémon you have in front matters, too."), MSGBOX_NPC)
}

text OldaleTown_Mart_Text_PokeBallsAreSoldOut {
    "The clerk says they're all sold out.\n"
    "I can't buy any Poké Balls."
}

text OldaleTown_Mart_Text_ImGoingToBuyPokeBalls {
    format("I'm going to buy a bunch of Poké Balls and catch a bunch of Pokémon!")
}
