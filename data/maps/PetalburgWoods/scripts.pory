mapscripts PetalburgWoods_MapScripts {
    MAP_SCRIPT_ON_LOAD {
        special(SetStationaryWilds)
        setvar(VAR_WILD_SYMBOL_LEVEL, 8)
    }
}

raw `
.set LOCALID_GRUNT, 3
.set LOCALID_DEVON_EMPLOYEE, 4

PetalburgWoods_EventScript_DevonResearcherLeft::
    lockall
    call PetalburgWoods_EventScript_DevonResearcherIntro
    applymovement LOCALID_DEVON_EMPLOYEE, PetalburgWoods_Movement_DevonResearcherApproachPlayerLeft
    waitmovement 0
    msgbox PetalburgWoods_Text_HaveYouSeenShroomish, MSGBOX_DEFAULT
    closemessage
    playbgm MUS_ENCOUNTER_AQUA, FALSE
    applymovement LOCALID_GRUNT, PetalburgWoods_Movement_AquaEntrance
    waitmovement 0
    msgbox PetalburgWoods_Text_IWasGoingToAmbushYou, MSGBOX_DEFAULT
    closemessage
    applymovement LOCALID_GRUNT, PetalburgWoods_Movement_AquaApproachResearcherLeft
    waitmovement 0
    applymovement LOCALID_DEVON_EMPLOYEE, Common_Movement_WalkInPlaceFasterUp
    waitmovement 0
    msgbox PetalburgWoods_Text_HandOverThosePapers, MSGBOX_DEFAULT
    closemessage
    applymovement LOCALID_DEVON_EMPLOYEE, PetalburgWoods_Movement_DevonResearcherFleeToPlayerLeft
    waitmovement 0
    msgbox PetalburgWoods_Text_YouHaveToHelpMe, MSGBOX_DEFAULT
    closemessage
    applymovement LOCALID_GRUNT, PetalburgWoods_Movement_AquaApproachPlayer
    waitmovement 0
    msgbox PetalburgWoods_Text_NoOneCrossesTeamAqua, MSGBOX_DEFAULT
    trainerbattle_no_intro TRAINER_GRUNT_PETALBURG_WOODS, PetalburgWoods_Text_YoureKiddingMe
    applymovement LOCALID_GRUNT, PetalburgWoods_Movement_AquaBackOff
    waitmovement 0
    call PetalburgWoods_EventScript_DevonResearcherPostBattle
    applymovement OBJ_EVENT_ID_PLAYER, PetalburgWoods_Movement_WatchResearcherLeave
    applymovement LOCALID_DEVON_EMPLOYEE, PetalburgWoods_Movement_DevonResearcherExitLeft
    waitmovement 0
    goto PetalburgWoods_EventScript_RemoveDevonResearcher
    end

PetalburgWoods_EventScript_DevonResearcherRight::
    lockall
    call PetalburgWoods_EventScript_DevonResearcherIntro
    applymovement LOCALID_DEVON_EMPLOYEE, PetalburgWoods_Movement_DevonResearcherApproachPlayerRight
    waitmovement 0
    applymovement OBJ_EVENT_ID_PLAYER, Common_Movement_WalkInPlaceFasterLeft
    waitmovement 0
    msgbox PetalburgWoods_Text_HaveYouSeenShroomish, MSGBOX_DEFAULT
    closemessage
    playbgm MUS_ENCOUNTER_AQUA, FALSE
    applymovement LOCALID_GRUNT, PetalburgWoods_Movement_AquaEntrance
    waitmovement 0
    msgbox PetalburgWoods_Text_IWasGoingToAmbushYou, MSGBOX_DEFAULT
    closemessage
    applymovement LOCALID_GRUNT, PetalburgWoods_Movement_AquaApproachResearcherRight
    waitmovement 0
    applymovement LOCALID_DEVON_EMPLOYEE, Common_Movement_WalkInPlaceFasterUp
    waitmovement 0
    msgbox PetalburgWoods_Text_HandOverThosePapers, MSGBOX_DEFAULT
    closemessage
    applymovement LOCALID_DEVON_EMPLOYEE, PetalburgWoods_Movement_DevonResearcherFleeToPlayerRight
    waitmovement 0
    msgbox PetalburgWoods_Text_YouHaveToHelpMe, MSGBOX_DEFAULT
    applymovement OBJ_EVENT_ID_PLAYER, Common_Movement_WalkInPlaceFasterUp
    waitmovement 0
    msgbox PetalburgWoods_Text_NoOneCrossesTeamAqua, MSGBOX_DEFAULT
    trainerbattle_no_intro TRAINER_GRUNT_PETALBURG_WOODS, PetalburgWoods_Text_YoureKiddingMe
    applymovement LOCALID_GRUNT, PetalburgWoods_Movement_AquaBackOff
    waitmovement 0
    call PetalburgWoods_EventScript_DevonResearcherPostBattle
    applymovement OBJ_EVENT_ID_PLAYER, PetalburgWoods_Movement_WatchResearcherLeave
    applymovement LOCALID_DEVON_EMPLOYEE, PetalburgWoods_Movement_DevonResearcherExitRight
    waitmovement 0
    goto PetalburgWoods_EventScript_RemoveDevonResearcher
    end

PetalburgWoods_EventScript_DevonResearcherIntro::
    applymovement LOCALID_DEVON_EMPLOYEE, PetalburgWoods_Movement_DevonResearcherLookAround
    waitmovement 0
    msgbox PetalburgWoods_Text_NotAOneToBeFound, MSGBOX_DEFAULT
    closemessage
    return

PetalburgWoods_EventScript_DevonResearcherPostBattle::
    msgbox PetalburgWoods_Text_YouveGotSomeNerve, MSGBOX_DEFAULT
    closemessage
    applymovement LOCALID_GRUNT, PetalburgWoods_Movement_AquaRunAway
    waitmovement 0
    removeobject LOCALID_GRUNT
    applymovement OBJ_EVENT_ID_PLAYER, Common_Movement_WalkInPlaceFasterDown
    waitmovement 0
    msgbox PetalburgWoods_Text_ThatWasAwfullyClose, MSGBOX_DEFAULT
    giveitem ITEM_ESCAPE_ROPE
    goto_if_eq VAR_RESULT, FALSE, PetalburgWoods_EventScript_BagFull
    goto PetalburgWoods_EventScript_DevonResearcherFinish
    end

PetalburgWoods_EventScript_BagFull::
    msgbox PetalburgWoods_Text_YoureLoadedWithItems, MSGBOX_DEFAULT
    goto PetalburgWoods_EventScript_DevonResearcherFinish
    end

PetalburgWoods_EventScript_DevonResearcherFinish::
    msgbox PetalburgWoods_Text_TeamAquaAfterSomethingInRustboro, MSGBOX_DEFAULT
    applymovement LOCALID_DEVON_EMPLOYEE, PetalburgWoods_Movement_DevonResearcherStartExit
    waitmovement 0
    msgbox PetalburgWoods_Text_ICantBeWastingTime, MSGBOX_DEFAULT
    closemessage
    return

PetalburgWoods_EventScript_RemoveDevonResearcher::
    removeobject LOCALID_DEVON_EMPLOYEE
    setvar VAR_PETALBURG_WOODS_STATE, 1
    releaseall
    end

PetalburgWoods_Movement_DevonResearcherLookAround:
    face_up
    delay_16
    delay_4
    face_right
    delay_16
    delay_8
    face_left
    delay_16
    delay_8
    face_down
    delay_16
    face_right
    delay_16
    delay_8
    face_up
    delay_16
    step_end

PetalburgWoods_Movement_DevonResearcherExitLeft:
    walk_fast_right
    walk_fast_up
    walk_fast_up
    walk_fast_up
    walk_fast_up
    walk_fast_up
    walk_fast_up
    walk_fast_up
    step_end

PetalburgWoods_Movement_DevonResearcherApproachPlayerLeft:
    delay_16
    face_player
    walk_down
    walk_down
    step_end

PetalburgWoods_Movement_DevonResearcherApproachPlayerRight:
    delay_16
    face_player
    walk_down
    walk_down
    walk_down
    walk_in_place_faster_right
    step_end

PetalburgWoods_Movement_DevonResearcherExitRight:
    walk_fast_left
    walk_fast_up
    walk_fast_up
    walk_fast_up
    walk_fast_up
    walk_fast_up
    walk_fast_up
    walk_fast_up
    step_end

PetalburgWoods_Movement_WatchResearcherLeave:
    delay_16
    delay_16
    walk_in_place_faster_up
    step_end

PetalburgWoods_Movement_DevonResearcherFleeToPlayerLeft:
    walk_fast_right
    walk_fast_down
    walk_fast_down
    walk_fast_left
    walk_in_place_faster_up
    step_end

PetalburgWoods_Movement_DevonResearcherFleeToPlayerRight:
    walk_fast_down
    walk_fast_right
    walk_in_place_faster_up
    step_end

PetalburgWoods_Movement_DevonResearcherStartExit:
    walk_in_place_faster_down
    delay_16
    delay_16
    delay_16
    delay_16
    delay_16
    face_up
    step_end

PetalburgWoods_Movement_AquaApproachResearcherLeft:
    walk_fast_down
    walk_fast_down
    step_end

PetalburgWoods_Movement_AquaBackOff:
    lock_facing_direction
    walk_up
    unlock_facing_direction
    step_end

PetalburgWoods_Movement_AquaRunAway:
    walk_fast_up
    walk_fast_up
    walk_fast_up
    walk_fast_up
    walk_fast_up
    delay_16
    delay_16
    step_end

PetalburgWoods_Movement_AquaApproachResearcherRight:
    walk_fast_down
    walk_fast_down
    walk_fast_down
    step_end

PetalburgWoods_Movement_AquaEntrance:
    walk_down
    walk_down
    delay_16
    delay_16
    step_end

PetalburgWoods_Movement_AquaApproachPlayer:
    walk_down
    step_end

PetalburgWoods_EventScript_Lyle::
    trainerbattle_single TRAINER_LYLE, PetalburgWoods_Text_GoBugPokemonTeam, PetalburgWoods_Text_ICouldntWin
    msgbox PetalburgWoods_Text_ImOutOfPokeBalls, MSGBOX_AUTOCLOSE
    end

PetalburgWoods_EventScript_James::
    trainerbattle_single TRAINER_JAMES_1, PetalburgWoods_Text_InstantlyPopularWithBugPokemon, PetalburgWoods_Text_CantBePopularIfILose, PetalburgWoods_EventScript_TryRegisterJames
    specialvar VAR_RESULT, ShouldTryRematchBattle
    goto_if_eq VAR_RESULT, TRUE, PetalburgWoods_EventScript_JamesRematch
    setvar VAR_0x8004, TRAINER_JAMES_1
    specialvar VAR_RESULT, IsTrainerRegistered
    goto_if_eq VAR_RESULT, FALSE, PetalburgWoods_EventScript_TryRegisterJames2
    msgbox PetalburgWoods_Text_PeopleRespectYou, MSGBOX_DEFAULT
    release
    end

PetalburgWoods_EventScript_TryRegisterJames::
    special PlayerFaceTrainerAfterBattle
    waitmovement 0
    goto_if_set FLAG_HAS_MATCH_CALL, PetalburgWoods_EventScript_RegisterJames
    release
    end

PetalburgWoods_EventScript_RegisterJames::
    msgbox PetalburgWoods_Text_IWantRematch1, MSGBOX_DEFAULT
    register_matchcall TRAINER_JAMES_1
    release
    end

PetalburgWoods_EventScript_TryRegisterJames2::
    goto_if_set FLAG_HAS_MATCH_CALL, PetalburgWoods_EventScript_RegisterJames2
    msgbox PetalburgWoods_Text_PeopleRespectYou, MSGBOX_DEFAULT
    release
    end

PetalburgWoods_EventScript_RegisterJames2::
    msgbox PetalburgWoods_Text_IWantRematch2, MSGBOX_DEFAULT
    register_matchcall TRAINER_JAMES_1
    release
    end

PetalburgWoods_EventScript_JamesRematch::
    trainerbattle_rematch TRAINER_JAMES_1, PetalburgWoods_Text_MyPokemonHaveGrown, PetalburgWoods_Text_CantBePopularIfLose
    msgbox PetalburgWoods_Text_IveBeenTrainingHard, MSGBOX_AUTOCLOSE
    end
`

text PetalburgWoods_Text_NotAOneToBeFound {
    format("Hmmm…\n"
    "Not a one to be found…")
}

text PetalburgWoods_Text_HaveYouSeenShroomish {
    format("Hello, have you seen any Pokémon called Shroomish around here?\p"
    "I really love that Pokémon.")
}

text PetalburgWoods_Text_IWasGoingToAmbushYou {
    format("I was going to ambush you, but you had to dawdle in Petalburg Woods forever, didn't you?\p"
    "I got sick of waiting, so here I am!")
}

text PetalburgWoods_Text_HandOverThosePapers {
    format("You! Devon Researcher!\p"
    "Hand over those papers!")
}

text PetalburgWoods_Text_YouHaveToHelpMe {
    format("Aiyeeeh!\p"
    "You're a Pokémon Trainer, aren't you?\n"
    "You've got to help me, please!")
}

text PetalburgWoods_Text_NoOneCrossesTeamAqua {
    format("Hunh? What do you think you're doing?\n"
    "What, you're going to protect him?\p"
    "No one who crosses Team Aqua gets any mercy, not even a kid!\p"
    "Come on and battle me!")
}

text PetalburgWoods_Text_YoureKiddingMe {
    format("You're kidding me! You're tough!")
}

text PetalburgWoods_Text_YouveGotSomeNerve {
    format("Grrr… You've got some nerve meddling with Team Aqua!\l"
    "Come on and battle me again!\p"
    "I wish I could say that, but I'm out of Pokémon…\p"
    "And, hey, we of Team Aqua are also after something in Rustboro.\p"
    "I'll let you go today!")
}

text PetalburgWoods_Text_ThatWasAwfullyClose {
    format("Whew…\n"
        "That was awfully close!\p"
        "Thanks to you, he didn't rob me of these important papers.\p"
        "I know, I'll give you an Escape Rope as my thanks!")
}

text PetalburgWoods_Text_TeamAquaAfterSomethingInRustboro {
    format("Didn't that Team Aqua thug say they were after something in Rustboro, too?")
}

text PetalburgWoods_Text_ICantBeWastingTime {
    format("Uh-oh! It's a crisis!\n"
    "I can't be wasting time!")
}

text PetalburgWoods_Text_YoureLoadedWithItems {
    format("You're loaded with items.\n"
    "I can't give you this Escape Rope.")
}

text PetalburgWoods_Text_GoBugPokemonTeam {
    format("I caught a whole bunch of Pokémon!\p"
    "Go, go, go!\n"
    "My Bug Pokémon team!")
}

text PetalburgWoods_Text_ICouldntWin {
    format("I have all these Pokémon, but I couldn't win…")
}

text PetalburgWoods_Text_ImOutOfPokeBalls {
    format("I caught a bunch of Pokémon.\n"
    "Now I'm out of Poké Balls.")
}

text PetalburgWoods_Text_InstantlyPopularWithBugPokemon {
    format("If you take Bug Pokémon to school, you get to be instantly popular!")
}

text PetalburgWoods_Text_CantBePopularIfILose {
    format("I can't be popular if I lose.")
}

text PetalburgWoods_Text_PeopleRespectYou {
    format("If you have a big Bug Pokémon, people respect you for it.")
}

text PetalburgWoods_Text_IWantRematch1 {
    format("I want a rematch when my Bug Pokémon grow up!\p"
        "I'm registering you in my Pokénav!")
}

text PetalburgWoods_Text_IWantRematch2 {
    format("I want a rematch when my Bug Pokémon grow up!\p"
    "I'm registering you in my Pokénav!")
}

text PetalburgWoods_Text_MyPokemonHaveGrown {
    format("My Bug Pokémon have grown.\n"
    "I'll be popular again.")
}

text PetalburgWoods_Text_CantBePopularIfLose {
    format("I can't be popular if I lose.")
}

text PetalburgWoods_Text_IveBeenTrainingHard {
    format("You get to be popular if you have strong Pokémon, right?\p"
         "I'll keep training hard.")
}

script PetalburgWoods_EventScript_Boy1 {
    msgbox(format("Yo, there!\n"
        "Your Pokémon doing okay?\p"
        "If your Pokémon are weak and you want to avoid battles, you should stay out of tall grass."), MSGBOX_NPC)
}

script PetalburgWoods_EventScript_Boy2 {
    msgbox(format("Sometimes, there are things on the ground even if you can't see them.\p"
        "That's why I always check where I'm walking."), MSGBOX_NPC)
}

script PetalburgWoods_EventScript_Girl {
    lock
    faceplayer
    if (!flag(FLAG_RECEIVED_MIRACLE_SEED)) {
        msgbox(format("Oh, neat!\n"
            "That's the Badge from Rustboro Gym!\p"
            "You must be a Trainer.\n"
            "You should try using this item."), MSGBOX_DEFAULT)
        giveitem(ITEM_CLEAR_AMULET)
        if (var(VAR_RESULT) == FALSE) { goto(Common_EventScript_ShowBagIsFull) }
        setflag(FLAG_RECEIVED_MIRACLE_SEED)
    }
    msgbox(format("It's a Clear Amulet.\p"
        "I think there are other kinds of items for Pokémon to hold."), MSGBOX_DEFAULT)
    release
}    

script PetalburgWoods_EventScript_Sign1 {
    msgbox(format("Trainer Tips\p"
        "Any Pokémon that could evolve by Level Up but has not gains boosted EXP Points.\p"
        "Use an Everstone to level your Pokémon faster."), MSGBOX_SIGN)
}

script PetalburgWoods_EventScript_Sign2 {
    msgbox(format("Trainer Tips\p"
        "In a Double Battle, you can attack your other Pokémon to activate their Ability."), MSGBOX_SIGN)
}

