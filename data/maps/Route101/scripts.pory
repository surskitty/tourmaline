const LOCALID_BIRCH = 2
const LOCALID_ZIGZAGOON = 4

mapscripts Route101_MapScripts {
    MAP_SCRIPT_ON_TRANSITION {
        call(ProfBirch_EventScript_UpdateLocation)
    }
    MAP_SCRIPT_ON_FRAME_TABLE {
        if (var(VAR_ROUTE101_STATE) == 0) {
            setflag(FLAG_HIDE_MAP_NAME_POPUP)
            setvar(VAR_ROUTE101_STATE, 1)
        }
    }
}

script Route101_EventScript_StartBirchRescue {
    lockall
    playbgm(MUS_HELP, TRUE)
    msgbox("H-help me!", MSGBOX_DEFAULT)
    closemessage
    setobjectxy(LOCALID_BIRCH, 0, 15)
    setobjectxy(LOCALID_ZIGZAGOON, 0, 16)
    applymovement(OBJ_EVENT_ID_PLAYER, Route101_Movement_EnterScene)
    applymovement(LOCALID_BIRCH, Route101_Movement_BirchRunAway1)
    applymovement(LOCALID_ZIGZAGOON, Route101_Movement_ZigzagoonChase1)
    waitmovement(0)
    applymovement(LOCALID_ZIGZAGOON, Route101_Movement_ZigzagoonChaseInCircles)
    applymovement(LOCALID_BIRCH, Route101_Movement_BirchRunInCircles)
    waitmovement(0)
    applymovement(LOCALID_BIRCH, Common_Movement_WalkInPlaceFasterRight)
    waitmovement(0)
    applymovement(LOCALID_ZIGZAGOON, Route101_Movement_ZigzagoonFaceBirch)
    applymovement(LOCALID_BIRCH, Route101_Movement_BirchFaceZigzagoon)
    waitmovement(0)
    msgbox("Hello! You over there!\n"
           "Please! Help!\p"
           "In my Bag!\n"
           "There's a PoKé Ball!", MSGBOX_DEFAULT)
    closemessage
    setvar(VAR_ROUTE101_STATE, 2)
    releaseall
}

script Route101_EventScript_BirchsBag {
    lock
    faceplayer
    setflag(FLAG_SYS_POKEMON_GET)
    setflag(FLAG_RESCUED_BIRCH)
    fadescreen(FADE_TO_BLACK)
    removeobject(LOCALID_ZIGZAGOON)
    setobjectxy(OBJ_EVENT_ID_PLAYER, 6, 13)
    applymovement(OBJ_EVENT_ID_PLAYER, Common_Movement_WalkInPlaceFasterLeft)
    waitmovement(0)
    special(ChooseStarter)
    waitstate
    applymovement(LOCALID_BIRCH, Route101_Movement_BirchApproachPlayer)
    waitmovement(0)
    msgbox(format("PROF. BIRCH: Whew…\p"
        "I was in the tall grass studying wild Pokémon when I was jumped.\p"
        "You saved me.\n"
        "Thanks a lot!\p"
        "Oh?\p"
        "Hi, you're {PLAYER}{KUN}!\p"
        "This is not the place to chat, so come\n"
        "by my Pokémon Lab later, okay?"), MSGBOX_DEFAULT)
    special(HealPlayerParty)
    setflag(FLAG_HIDE_ROUTE_101_BIRCH_ZIGZAGOON_BATTLE)
    clearflag(FLAG_HIDE_LITTLEROOT_TOWN_BIRCHS_LAB_BIRCH)
    setflag(FLAG_HIDE_ROUTE_101_BIRCH_STARTERS_BAG)
    setvar(VAR_BIRCH_LAB_STATE, 2)
    setvar(VAR_ROUTE101_STATE, 3)
    clearflag(FLAG_HIDE_MAP_NAME_POPUP)
    if (checkplayergender == MALE) {
        setflag(FLAG_HIDE_LITTLEROOT_TOWN_MAYS_HOUSE_RIVAL_BEDROOM)
    } else {
        setflag(FLAG_HIDE_LITTLEROOT_TOWN_BRENDANS_HOUSE_RIVAL_BEDROOM)
    }
    warp(MAP_LITTLEROOT_TOWN_PROFESSOR_BIRCHS_LAB, 6, 5)
    waitstate
    release
}       

raw `

Route101_EventScript_PreventExitSouth::
    lockall
    msgbox Route101_Text_DontLeaveMe, MSGBOX_DEFAULT
    closemessage
    applymovement OBJ_EVENT_ID_PLAYER, Route101_Movement_PreventExitSouth
    waitmovement 0
    releaseall
    end

Route101_EventScript_PreventExitWest::
    lockall
    msgbox Route101_Text_DontLeaveMe, MSGBOX_DEFAULT
    closemessage
    applymovement OBJ_EVENT_ID_PLAYER, Route101_Movement_PreventExitWest
    waitmovement 0
    releaseall
    end

Route101_EventScript_PreventExitNorth::
    lockall
    msgbox Route101_Text_DontLeaveMe, MSGBOX_DEFAULT
    closemessage
    applymovement OBJ_EVENT_ID_PLAYER, Route101_Movement_PreventExitNorth
    waitmovement 0
    releaseall
    end

Route101_Movement_BirchApproachPlayer:
    walk_right
    step_end

`

text Route101_Text_DontLeaveMe {
    "Wh-where are you going?!\n"
    "Don't leave me like this!"
}

script Route101_EventScript_Youngster {
    msgbox(format("If Pokémon get tired, take them to a Pokémon Center.\p"
                      "There's a Pokémon Center in Oldale Town right close by."), MSGBOX_NPC)
}

script Route101_EventScript_Boy {
    msgbox(format("Wild Pokémon will jump out at you in tall grass.\p"
                  "I heard they can gang up on you, too."), MSGBOX_NPC)
}

script Route101_EventScript_RouteSign {
    msgbox("Route 101\n{UP_ARROW} Oldale Town", MSGBOX_SIGN)
}

movement Route101_Movement_PreventExitSouth {
    walk_up
}

movement Route101_Movement_PreventExitWest {
    walk_right
}

movement Route101_Movement_PreventExitNorth {
    walk_down
}

movement Route101_Movement_ZigzagoonChaseInCircles {
    walk_fast_up * 3
    walk_fast_right * 3
    walk_fast_down * 2
    walk_fast_left * 3
    walk_fast_up * 2
    walk_fast_right * 3
    walk_fast_down * 2
    walk_fast_left * 3
    walk_fast_up * 2
    walk_fast_right * 3
    walk_fast_down * 2
    walk_fast_left * 3
}

movement Route101_Movement_ZigzagoonChase1 {
    walk_fast_up
    walk_fast_right * 4
    walk_fast_up
}

movement Route101_Movement_ZigzagoonFaceBirch {
    walk_in_place_fast_left * 4
}

movement Route101_Movement_EnterScene {
    walk_fast_up * 4
    walk_in_place_faster_left
}

movement Route101_Movement_BirchRunInCircles {
    walk_fast_up * 2
    walk_fast_right * 3
    walk_fast_down * 2
    walk_fast_left * 3
    walk_fast_up * 2
    walk_fast_right * 3
    walk_fast_down * 2
    walk_fast_left * 3
    walk_fast_up * 2
    walk_fast_right * 3
    walk_fast_down * 2
    walk_fast_left * 3
}

movement Route101_Movement_BirchRunAway1 {
    walk_fast_right * 4
    walk_fast_up * 2
}

movement Route101_Movement_BirchFaceZigzagoon {
    walk_in_place_fast_right * 4
}
