const LOCALID_RIVAL = 2
const LOCALID_RIVALS_STARTER = 3

mapscripts Route103_MapScripts {
    MAP_SCRIPT_ON_TRANSITION {
       call(Common_EventScript_SetupRivalGfxId)
       call(Route103_EventScript_SetupRivalStarterGfx)
       call(ProfBirch_EventScript_UpdateLocation)
    }
    MAP_SCRIPT_ON_LOAD {
       if (flag(FLAG_VISITED_LILYCOVE_CITY)) {
           setmetatile(45, 5, METATILE_General_CaveEntrance_Top, TRUE)
           setmetatile(45, 6, METATILE_General_CaveEntrance_Bottom, FALSE)
       }
       special(SetStationaryWilds)
    }
}

script Route103_EventScript_SetupRivalStarterGfx {
    if (var(VAR_STARTER_MON) == 0) {
        setvar(VAR_OBJ_GFX_ID_1, OBJ_EVENT_GFX_SPECIES(TORCHIC))
    } elif (var(VAR_STARTER_MON) == 1) {
        setvar(VAR_OBJ_GFX_ID_1, OBJ_EVENT_GFX_SPECIES(MUDKIP))
    } else {
        setvar(VAR_OBJ_GFX_ID_1, OBJ_EVENT_GFX_SPECIES(TREECKO))
    }
}

script Route103_EventScript_RivalStarter {
    applymovement(LOCALID_RIVALS_STARTER, Common_Movement_ExclamationMark)
    faceplayer
    waitse
    if (var(VAR_STARTER_MON) == 0) {
        playmoncry(SPECIES_TORCHIC, CRY_MODE_NORMAL)
        speakername("Torchic")
        msgbox(format("Ca-caw!"), MSGBOX_DEFAULT)
        waitmovement(0)
        waitmoncry
    } elif (var(VAR_STARTER_MON) == 1) {
        playmoncry(SPECIES_MUDKIP, CRY_MODE_NORMAL)
        speakername("Mudkip")
        msgbox(format("Kippa?"), MSGBOX_DEFAULT)
        waitmovement(0)
        waitmoncry
    } else {
        playmoncry(SPECIES_TREECKO, CRY_MODE_NORMAL)
        speakername("Treecko")
        msgbox(format("Gugugugu!"), MSGBOX_DEFAULT)
        waitmovement(0)
        waitmoncry
    }
    msgbox(format("Its Trainer is too busy to notice you."), MSGBOX_DEFAULT)
    release
}

script Route103_EventScript_Rival {
    lockall
    checkplayergender
    speakername(CommonName_Text_Rival)
    if (var(VAR_RESULT) == MALE) {
        msgbox(format("Let's see… The Pokémon found on Route 103 include…"), MSGBOX_DEFAULT)
        closemessage
        playbgm(MUS_ENCOUNTER_MAY, TRUE)
    } else {
        msgbox(format("Okay, so it's this one and that one that live on Route 103…"), MSGBOX_DEFAULT)
        closemessage
        playbgm(MUS_ENCOUNTER_BRENDAN, TRUE)
    }
    applymovement(LOCALID_RIVAL, Common_Movement_FacePlayer)
    waitmovement(0)
    applymovement(LOCALID_RIVAL, Common_Movement_ExclamationMark)
    waitmovement(0)
    applymovement(LOCALID_RIVAL, Common_Movement_Delay48)
    waitmovement(0)
    getcaughtmon(SPECIES_EEVEE)
    speakername(CommonName_Text_Rival)
    if (var(VAR_RESULT) == FALSE) {
        checkplayergender
        if (var(VAR_RESULT) == MALE) {
            msgbox(format("Hey, it's {PLAYER}!\p"
                "…Oh, yeah, Dad gave you a Pokémon.\p"
                "Pokémon hate feeling lonely,\n"
                "so let me give it a friend, too."), MSGBOX_DEFAULT)
        } else {
            msgbox(format("Oh, hi, {PLAYER}{KUN}!\p"
                "…Oh, I see, my dad gave you a Pokémon as a gift.\p"
                "You should have its friend, too."), MSGBOX_DEFAULT)
        }
        givemon(SPECIES_EEVEE, 5, item=ITEM_ORAN_BERRY, abilityNum=0, move1=MOVE_COVET, move2=MOVE_HELPING_HAND, move3=MOVE_GROWL, move4=MOVE_TAIL_WHIP)
        playfanfare(MUS_OBTAIN_ITEM)
        msgbox("{PLAYER} received Eevee!")
        waitfanfare
    }
    special(HealPlayerParty)
    speakername(CommonName_Text_Rival)
    checkplayergender
    if (var(VAR_RESULT) == MALE) {
        msgbox(Route103_Text_MayLetsBattle, MSGBOX_DEFAULT)
        switch (var(VAR_STARTER_MON)) {
            case 0: 
                trainerbattle_no_intro(TRAINER_MAY_ROUTE_103_TREECKO, Route103_Text_MayDefeated)
            case 1:
                trainerbattle_no_intro(TRAINER_MAY_ROUTE_103_TORCHIC, Route103_Text_MayDefeated)
            case 2: 
                trainerbattle_no_intro(TRAINER_MAY_ROUTE_103_MUDKIP, Route103_Text_MayDefeated)
        }
        applymovement(LOCALID_RIVALS_STARTER, EnterPokeballMovement)
        waitmovement(0)
        setflag(FLAG_DEFEATED_RIVAL_ROUTE103)
        speakername(CommonName_Text_Rival)
        msgbox(Route103_Text_MayTimeToHeadBack, MSGBOX_DEFAULT)
    } else {
        msgbox(Route103_Text_BrendanLetsBattle, MSGBOX_DEFAULT)
        switch (var(VAR_STARTER_MON)) {
            case 0:
                trainerbattle_no_intro(TRAINER_BRENDAN_ROUTE_103_TREECKO, Route103_Text_BrendanDefeated)
            case 1:
                trainerbattle_no_intro(TRAINER_BRENDAN_ROUTE_103_TORCHIC, Route103_Text_BrendanDefeated)
            case 2:
                trainerbattle_no_intro(TRAINER_BRENDAN_ROUTE_103_MUDKIP, Route103_Text_BrendanDefeated) 
        }
        applymovement(LOCALID_RIVALS_STARTER, EnterPokeballMovement)
        waitmovement(0)
        setflag(FLAG_DEFEATED_RIVAL_ROUTE103)
        speakername(CommonName_Text_Rival)
        msgbox(Route103_Text_BrendanTimeToHeadBack, MSGBOX_DEFAULT)
    }
    special(HealPlayerParty)
    closemessage
    if (var(VAR_FACING) == DIR_SOUTH) {
        applymovement(LOCALID_RIVAL, Route103_Movement_RivalExit1)
        waitmovement(0)
        playse(SE_LEDGE)
        applymovement(LOCALID_RIVAL, Route103_Movement_RivalExit2)
    } elif (var(VAR_FACING) == DIR_NORTH) {
        applymovement(OBJ_EVENT_ID_PLAYER, Route103_Movement_WatchRivalExitFacingNorth)
        applymovement(LOCALID_RIVAL, Route103_Movement_RivalExitFacingNorth1)
        waitmovement(0)
        playse(SE_LEDGE)
        applymovement(LOCALID_RIVAL, Route103_Movement_RivalExitFacingNorth2)
    }
    else {
        applymovement(OBJ_EVENT_ID_PLAYER, Route103_Movement_WatchRivalExitFacingEastOrWest)
        applymovement(LOCALID_RIVAL, Route103_Movement_RivalExit1)
        waitmovement(0)
        playse(SE_LEDGE)
        applymovement(LOCALID_RIVAL, Route103_Movement_RivalExit2)
    }
    waitmovement(0)
    removeobject(LOCALID_RIVAL)
    setvar(VAR_BIRCH_LAB_STATE, 4)
    clearflag(FLAG_HIDE_LITTLEROOT_TOWN_BIRCHS_LAB_RIVAL)
    setvar(VAR_OLDALE_RIVAL_STATE, 1)
    clearflag(FLAG_HIDE_OLDALE_TOWN_RIVAL)
    savebgm(MUS_DUMMY)
    fadedefaultbgm
    releaseall    
}

raw `
Route103_Movement_RivalExitFacingNorth1:
    walk_left
    walk_down
    step_end

Route103_Movement_RivalExitFacingNorth2:
    jump_2_down
    delay_16
    walk_down
    walk_down
    walk_down
    walk_down
    step_end

Route103_Movement_WatchRivalExitFacingNorth:
    delay_16
    delay_4
    walk_in_place_faster_left
    delay_16
    walk_in_place_faster_down
    step_end

Route103_Movement_RivalExit1:
    walk_down
    step_end

Route103_Movement_RivalExit2:
    jump_2_down
    delay_16
    walk_down
    walk_down
    walk_down
    step_end

Route103_Movement_WatchRivalExitFacingEastOrWest:
    delay_16
    walk_in_place_faster_down
    step_end

Route103_EventScript_Daisy::
    trainerbattle_single TRAINER_DAISY, Route103_Text_DaisyIntro, Route103_Text_DaisyDefeated
    msgbox Route103_Text_DaisyPostBattle, MSGBOX_AUTOCLOSE
    end
`

script Route103_EventScript_Amy {
    trainerbattle_double(TRAINER_AMY_AND_LIV_1, Route103_Text_AmyIntro, Route103_Text_AmyDefeated, Route103_Text_AmyNotEnoughPokemon, Route102_EventScript_AmyRegisterMatchCallAfterBattle)
    specialvar(VAR_RESULT, ShouldTryRematchBattle)
    if (var(VAR_RESULT) == TRUE) {
        trainerbattle_rematch_double(TRAINER_AMY_AND_LIV_1, Route103_Text_AmyRematchIntro, Route103_Text_AmyRematchDefeated, Route103_Text_AmyRematchNotEnoughPokemon)
        msgbox(Route103_Text_AmyRematchPostBattle, MSGBOX_AUTOCLOSE)
    } else {
    msgbox(Route103_Text_AmyPostBattle, MSGBOX_AUTOCLOSE)
    }
}

script Route102_EventScript_AmyRegisterMatchCallAfterBattle {
    msgbox(Route103_Text_AmyLivRegister, MSGBOX_DEFAULT)
    register_matchcall(TRAINER_AMY_AND_LIV_1)
    release
}

raw `
Route103_EventScript_Liv::
    trainerbattle_double TRAINER_AMY_AND_LIV_1, Route103_Text_LivIntro, Route103_Text_LivDefeated, Route103_Text_LivNotEnoughPokemon, Route102_EventScript_LivRegisterMatchCallAfterBattle

    specialvar VAR_RESULT, ShouldTryRematchBattle
    goto_if_eq VAR_RESULT, TRUE, Route102_EventScript_LivRematch
    msgbox Route103_Text_LivPostBattle, MSGBOX_AUTOCLOSE
    end

Route102_EventScript_LivRegisterMatchCallAfterBattle::
    msgbox Route103_Text_AmyLivRegister, MSGBOX_DEFAULT
    register_matchcall TRAINER_AMY_AND_LIV_1
    release
    end

Route102_EventScript_LivRematch::
    trainerbattle_rematch_double TRAINER_AMY_AND_LIV_1, Route103_Text_LivRematchIntro, Route103_Text_LivRematchDefeated, Route103_Text_LivRematchNotEnoughPokemon
    msgbox Route103_Text_LivRematchPostBattle, MSGBOX_AUTOCLOSE
    end
Route103_EventScript_Andrew::
    trainerbattle_single TRAINER_ANDREW, Route103_Text_AndrewIntro, Route103_Text_AndrewDefeated
    msgbox Route103_Text_AndrewPostBattle, MSGBOX_AUTOCLOSE
    end

Route103_EventScript_Miguel::
    trainerbattle_single TRAINER_MIGUEL_1, Route103_Text_MiguelIntro, Route103_Text_MiguelDefeated, Route102_EventScript_MiguelRegisterMatchCallAfterBattle
    specialvar VAR_RESULT, ShouldTryRematchBattle
    goto_if_eq VAR_RESULT, TRUE, Route103_EventScript_MiguelRematch
    msgbox Route103_Text_MiguelPostBattle, MSGBOX_DEFAULT
    release
    end

Route102_EventScript_MiguelRegisterMatchCallAfterBattle::
    special PlayerFaceTrainerAfterBattle
    waitmovement 0
    msgbox Route103_Text_MiguelRegister, MSGBOX_DEFAULT
    register_matchcall TRAINER_MIGUEL_1
    release
    end

Route103_EventScript_MiguelRematch::
    trainerbattle_rematch TRAINER_MIGUEL_1, Route103_Text_MiguelRematchIntro, Route103_Text_MiguelRematchDefeated
    msgbox Route103_Text_MiguelRematchPostBattle, MSGBOX_AUTOCLOSE
    end

Route103_EventScript_Marcos::
    trainerbattle_single TRAINER_MARCOS, Route103_Text_MarcosIntro, Route103_Text_MarcosDefeated
    msgbox Route103_Text_MarcosPostBattle, MSGBOX_AUTOCLOSE
    end

Route103_EventScript_Rhett::
    trainerbattle_single TRAINER_RHETT, Route103_Text_RhettIntro, Route103_Text_RhettDefeated
    msgbox Route103_Text_RhettPostBattle, MSGBOX_AUTOCLOSE
    end

Route103_EventScript_Pete::
    trainerbattle_single TRAINER_PETE, Route103_Text_PeteIntro, Route103_Text_PeteDefeated
    msgbox Route103_Text_PetePostBattle, MSGBOX_AUTOCLOSE
    end

Route103_EventScript_Isabelle::
    trainerbattle_single TRAINER_ISABELLE, Route103_Text_IsabelleIntro, Route103_Text_IsabelleDefeated
    msgbox Route103_Text_IsabellePostBattle, MSGBOX_AUTOCLOSE
    end
`

script Route103_EventScript_Boy {
    msgbox(format("My Pokémon are staggeringly tired…\n"
        "I should have brought a Potion…"), MSGBOX_NPC)
}

script Route103_EventScript_Man {
    msgbox("If you cross the sea from here,\n"
        "it'll be a shortcut to Oldale Town.\p"
        "Fufufu, that's useful, isn't it?", MSGBOX_NPC)
}

script Route103_EventScript_RouteSign {
    msgbox("Route 103\n"
        "{DOWN_ARROW} Oldale Town", MSGBOX_SIGN)
}

text Route103_Text_MayLetsBattle {
    format("Now you're ready for a quick battle!\p"
        "I'll give you a taste of what being a Trainer is like.")
}

text Route103_Text_MayDefeated {
    "Wow! That's great!\n"
    "{PLAYER}{KUN}, you're pretty good!"
}

text Route103_Text_MayTimeToHeadBack {
    format("I think I know why my dad has an eye out for you now.\p"
        "I mean, you just got that Pokémon, but it already likes you.\p"
        "You might be able to befriend any kind of Pokémon easily.\p"
        "Well, it's time to head back to the Lab.")
}

text Route103_Text_BrendanLetsBattle {
    format("Since we're here, how about a little battle?\p"
        "I'll teach you what being a Trainer's about!")
}

text Route103_Text_BrendanDefeated {
    "Huh, {PLAYER}, you're not too shabby."
}

text Route103_Text_BrendanTimeToHeadBack {
    format("I think I get it.\n"
        "I think I know why my dad has his eye out for you now.\p"
        "Look, your Pokémon already like you,\n even though you just got them.\p"
        "{PLAYER}, I get the feeling that you could befriend any Pokémon with ease.\p"
        "We should head back to the Lab.")
}
