mapscripts Route104_MapScripts {
    MAP_SCRIPT_ON_FRAME_TABLE [
        VAR_BOARD_BRINEY_BOAT_STATE, 1 {
            lockall
            goto(Route104_EventScript_SailToDewford)
        }
    ]
    MAP_SCRIPT_ON_TRANSITION {
        call(Common_EventScript_SetupRivalGfxId)
        call(Route104_EventScript_TrySetRivalPos)
        call(Route104_EventScript_ShowOrHideWhiteHerbFlorist)
    }
    MAP_SCRIPT_ON_LOAD {
        special(SetRoofBirds)
        special(SetStationaryWilds)
    }
}

raw `
@ NOTE: Route 104's sail to Dewford script references local IDs from Dewford's map.
@  These are labeled in DewfordTown/scripts.inc
.set LOCALID_RIVAL, 34

Route104_EventScript_ShowOrHideWhiteHerbFlorist::
    goto_if_unset FLAG_MET_PRETTY_PETAL_SHOP_OWNER, Route104_EventScript_HideWhiteHerbFlorist
    goto_if_unset FLAG_BADGE03_GET, Route104_EventScript_HideWhiteHerbFlorist
    clearflag FLAG_HIDE_ROUTE_104_WHITE_HERB_FLORIST
    return

Route104_EventScript_HideWhiteHerbFlorist::
    setflag FLAG_HIDE_ROUTE_104_WHITE_HERB_FLORIST
    return

Route104_EventScript_TrySetRivalPos::
    goto_if_ge VAR_BOARD_BRINEY_BOAT_STATE, 1, Route104_EventScript_DontSetRivalPos
    goto_if_set FLAG_MET_RIVAL_RUSTBORO, Route104_EventScript_DontSetRivalPos
    goto_if_unset FLAG_REGISTER_RIVAL_POKENAV, Route104_EventScript_DontSetRivalPos
    setobjectxyperm LOCALID_RIVAL, 17, 52
    return

Route104_EventScript_DontSetRivalPos::
    return

Route104_EventScript_Rival::
    lockall
    setvar VAR_0x8008, 1
    applymovement LOCALID_RIVAL, Common_Movement_FacePlayer
    waitmovement 0
    goto Route104_EventScript_RivalEncounter

Route104_EventScript_RivalTrigger::
    lockall
    setflag FLAG_HIDE_RUSTBORO_CITY_RIVAL
    setvar VAR_RUSTBORO_CITY_STATE, 8
    setvar VAR_ROUTE104_STATE, 2
    waitmovement 0
    applymovement OBJ_EVENT_ID_PLAYER, Common_Movement_WalkInPlaceFasterUp
    delay 10
    addobject LOCALID_RIVAL
    clearflag FLAG_HIDE_ROUTE_104_RIVAL
    applymovement OBJ_EVENT_ID_PLAYER, Route104_Movement_PlayerBackUp
    waitmovement 0
    applymovement LOCALID_RIVAL, Route104_Movement_RivalExitBrineysCottage
    waitmovement 0
    delay 20
    setvar VAR_0x8008, 0
    call RustboroCity_EventScript_PlayRivalMusic
    playse SE_PIN
    applymovement LOCALID_RIVAL, Common_Movement_ExclamationMark
    waitmovement 0
    applymovement LOCALID_RIVAL, Common_Movement_Delay48
    waitmovement 0
    goto Route104_EventScript_RivalEncounter

Route104_EventScript_RivalEncounter::
    checkplayergender
    goto_if_eq VAR_RESULT, MALE, Route104_EventScript_MayEncounter
    goto_if_eq VAR_RESULT, FEMALE, Route104_EventScript_BrendanEncounter
    end

Route104_EventScript_MayEncounter::
    goto_if_set FLAG_DEFEATED_RIVAL_ROUTE_104, Route104_EventScript_MayDefeated
    goto_if_set FLAG_REGISTER_RIVAL_POKENAV, Route104_EventScript_MayAskToBattle
    setflag FLAG_REGISTER_RIVAL_POKENAV
    speakername CommonName_Text_May
    msgbox Route104_Text_MayWeShouldRegister, MSGBOX_DEFAULT
    closemessage
    delay 30
    playfanfare MUS_REGISTER_MATCH_CALL
    speakername 0
    msgbox Route104_Text_RegisteredMay, MSGBOX_DEFAULT
    waitfanfare
    closemessage
    delay 30
    setflag FLAG_ENABLE_RIVAL_MATCH_CALL
    applymovement OBJ_EVENT_ID_PLAYER, Route104_Movement_PlayerFaceRival
    applymovement LOCALID_RIVAL, Route104_Movement_RivalWalkSlowLeft
    speakername CommonName_Text_May
    msgbox Route104_Text_MayHowsYourPokedex, MSGBOX_DEFAULT
    closemessage
    waitmovement 0
    applymovement LOCALID_RIVAL, Route104_Movement_RivalApproachPlayer
    waitmovement 0
    copyobjectxytoperm LOCALID_RIVAL
    speakername CommonName_Text_May
    msgbox Route104_Text_MayMinesDecentLetsBattle, MSGBOX_YESNO
    goto_if_eq VAR_RESULT, YES, Route104_EventScript_BattleMay
    speakername CommonName_Text_May
    msgbox Route104_Text_MayHaventRaisedPokemon, MSGBOX_DEFAULT
    call Route104_EventScript_RestoreMusic
    releaseall
    end

Route104_Movement_RivalWalkSlowLeft:
    walk_slow_left
    step_end

Route104_Movement_RivalApproachPlayer:
    walk_down
    face_right
    step_end

Route104_Movement_PlayerFaceRival:
    delay_4
    walk_in_place_faster_left
    step_end

Route104_EventScript_MayAskToBattle::
    speakername CommonName_Text_May
    msgbox Route104_Text_MayLetsBattle, MSGBOX_YESNO
    goto_if_eq VAR_RESULT, YES, Route104_EventScript_BattleMay
    speakername CommonName_Text_May
    msgbox Route104_Text_MayHaventRaisedPokemon, MSGBOX_DEFAULT
    releaseall
    end

Route104_EventScript_BattleMay::
    speakername CommonName_Text_May
    msgbox Route104_Text_MayIntro, MSGBOX_DEFAULT
    switch VAR_STARTER_MON
    case 0, Route104_EventScript_BattleMayTreecko
    case 1, Route104_EventScript_BattleMayTorchic
    case 2, Route104_EventScript_BattleMayMudkip
    end

Route104_EventScript_MayDefeated::
    speakername CommonName_Text_May
    msgbox Route104_Text_MayPostBattle, MSGBOX_DEFAULT
    call_if_eq VAR_0x8008, 0, Route104_EventScript_RestoreMusic
    releaseall
    end

Route104_EventScript_RestoreMusic::
    savebgm MUS_DUMMY
    fadedefaultbgm
    return

Route104_EventScript_BattleMayTreecko::
    trainerbattle_no_intro TRAINER_MAY_RUSTBORO_TREECKO, Route104_Text_MayDefeat
    setflag FLAG_DEFEATED_RIVAL_ROUTE_104
    goto Route104_EventScript_MayDefeated
    end

Route104_EventScript_BattleMayTorchic::
    trainerbattle_no_intro TRAINER_MAY_RUSTBORO_TORCHIC, Route104_Text_MayDefeat
    setflag FLAG_DEFEATED_RIVAL_ROUTE_104
    goto Route104_EventScript_MayDefeated
    end

Route104_EventScript_BattleMayMudkip::
    trainerbattle_no_intro TRAINER_MAY_RUSTBORO_MUDKIP, Route104_Text_MayDefeat
    setflag FLAG_DEFEATED_RIVAL_ROUTE_104
    goto Route104_EventScript_MayDefeated
    end

Route104_EventScript_BrendanEncounter::
    goto_if_set FLAG_DEFEATED_RIVAL_ROUTE_104, Route104_EventScript_BrendanDefeated
    goto_if_set FLAG_REGISTER_RIVAL_POKENAV, Route104_EventScript_BrendanAskToBattle
    setflag FLAG_REGISTER_RIVAL_POKENAV
    speakername CommonName_Text_Brendan
    msgbox Route104_Text_BrendanWeShouldRegister, MSGBOX_DEFAULT
    closemessage
    delay 30
    playfanfare MUS_REGISTER_MATCH_CALL
    msgbox Route104_Text_RegisteredBrendan, MSGBOX_DEFAULT
    waitfanfare
    closemessage
    delay 30
    setflag FLAG_ENABLE_RIVAL_MATCH_CALL
    applymovement OBJ_EVENT_ID_PLAYER, Route104_Movement_PlayerFaceRival
    applymovement LOCALID_RIVAL, Route104_Movement_RivalWalkSlowLeft
    speakername CommonName_Text_Brendan
    msgbox Route104_Text_BrendanHowsYourPokedex, MSGBOX_DEFAULT
    closemessage
    waitmovement 0
    applymovement LOCALID_RIVAL, Route104_Movement_RivalApproachPlayer
    waitmovement 0
    copyobjectxytoperm LOCALID_RIVAL
    speakername CommonName_Text_Brendan
    msgbox Route104_Text_BrendanDoingGreatLetsBattle, MSGBOX_YESNO
    goto_if_eq VAR_RESULT, YES, Route104_EventScript_BattleBrendan
    speakername CommonName_Text_Brendan
    msgbox Route104_Text_BrendanNoConfidence, MSGBOX_DEFAULT
    call Route104_EventScript_RestoreMusic
    releaseall
    end

Route104_EventScript_BrendanAskToBattle::
    speakername CommonName_Text_Brendan
    msgbox Route104_Text_BrendanLetsBattle, MSGBOX_YESNO
    goto_if_eq VAR_RESULT, YES, Route104_EventScript_BattleBrendan
    speakername CommonName_Text_Brendan
    msgbox Route104_Text_BrendanNoConfidence, MSGBOX_DEFAULT
    releaseall
    end

Route104_EventScript_BattleBrendan::
    speakername CommonName_Text_Brendan
    msgbox Route104_Text_BrendanIntro, MSGBOX_DEFAULT
    switch VAR_STARTER_MON
    case 0, Route104_EventScript_BattleBrendanTreecko
    case 1, Route104_EventScript_BattleBrendanTorchic
    case 2, Route104_EventScript_BattleBrendanMudkip
    end

Route104_EventScript_BrendanDefeated::
    speakername CommonName_Text_Brendan
    msgbox Route104_Text_BrendanPostBattle, MSGBOX_DEFAULT
    call_if_eq VAR_0x8008, 0, Route104_EventScript_RestoreMusic
    releaseall
    end

Route104_EventScript_BattleBrendanTreecko::
    trainerbattle_no_intro TRAINER_BRENDAN_RUSTBORO_TREECKO, Route104_Text_BrendanDefeat
    setflag FLAG_DEFEATED_RIVAL_ROUTE_104
    goto Route104_EventScript_BrendanDefeated
    end

Route104_EventScript_BattleBrendanTorchic::
    trainerbattle_no_intro TRAINER_BRENDAN_RUSTBORO_TORCHIC, Route104_Text_BrendanDefeat
    setflag FLAG_DEFEATED_RIVAL_ROUTE_104
    goto Route104_EventScript_BrendanDefeated
    end

Route104_EventScript_BattleBrendanMudkip::
    trainerbattle_no_intro TRAINER_BRENDAN_RUSTBORO_MUDKIP, Route104_Text_BrendanDefeat
    setflag FLAG_DEFEATED_RIVAL_ROUTE_104
    goto Route104_EventScript_BrendanDefeated
    end

Route104_Movement_PlayerBackUp:
    lock_facing_direction
    walk_down
    unlock_facing_direction
    step_end

Route104_Movement_RivalExitBrineysCottage:
    walk_down
    step_end

`

script Route104_EventScript_ExpertF {
    lock
    faceplayer
    if (!flag(FLAG_RECEIVED_CHESTO_BERRY_ROUTE_104)) {
        msgbox(format("If you see Berries growing in loamy soil, feel free to take them.\p"
            "But make sure you plant a Berry in the same spot. That's common courtesy.\p"
            "Here, I'll share this with you."), MSGBOX_DEFAULT)
        giveitem(ITEM_SALAC_BERRY)
        if (var(VAR_RESULT) == FALSE) { goto(Common_EventScript_ShowBagIsFull) }
        setflag(FLAG_RECEIVED_CHESTO_BERRY_ROUTE_104)
    }
    msgbox(format("The way you look, you must be a Trainer, no?\p"
        "Trainers often give their Pokémon Berries to hold.\p"
        "It's up to you whether to grow some or use them."), MSGBOX_DEFAULT)
    release
}

script Route104_EventScript_WhiteHerbFlorist {
    lock
    faceplayer
    if (!flag(FLAG_RECEIVED_WHITE_HERB)) {
        msgbox(format("This Flower Shop started selling saplings recently.\p"
            "It made me so happy, I went overboard shopping.\p"
            "I don't need this White Herb anymore.\n"
            "Would you take it, please?"), MSGBOX_DEFAULT)
        giveitem(ITEM_WHITE_HERB)
        if (var(VAR_RESULT) == FALSE) { goto(Common_EventScript_ShowBagIsFull) }
        setflag(FLAG_RECEIVED_WHITE_HERB)
        release
    } else {
        msgbox(format("This Flower Shop started selling saplings recently.\p"
            "It made me so happy, I went overboard shopping.\p"
            "Where should I put them?"), MSGBOX_NPC)
    }
}

script Route104_EventScript_Girl1 {
    msgbox(format("That seaside cottage is where Mr. Briney lives.\p"
    "He was once a mighty sailor who never feared the sea, however stormy."), MSGBOX_NPC)
}

script Route104_EventScript_BugCatcher {
    msgbox(format("The sea, huh?\p"
        "I wonder what it's like at the bottom of the sea?"), MSGBOX_NPC)
}

script Route104_EventScript_BrineysCottageSign {
    msgbox("Mr. Briney's Cottage", MSGBOX_SIGN)
}

script Route104_EventScript_RouteSignPetalburg {
    msgbox("Route 1O4\n"
        "{RIGHT_ARROW} Petalburg City$", MSGBOX_SIGN)
}

script Route104_EventScript_RouteSignRustboro {
    msgbox("Route 1O4\n"
        "{UP_ARROW} Rustboro City$", MSGBOX_SIGN)
}

script Route104_EventScript_FlowerShopSign {
    msgbox("Pretty Petal Flower Shop", MSGBOX_SIGN)
}

script Route104_EventScript_TrainerTipsDoubleBattles {
    msgbox(format("Trainer Tips\p"
        "Most Trainers in the Hoenn region love 2-on-2 Pokémon battles called Double Battles.\p"
        "Try to give your Pokémon a Helping Hand."), MSGBOX_SIGN)
}

script Route104_EventScript_Boy1 {
    msgbox(format("I heard it's easier to catch Pokémon when you're more experienced with them."), MSGBOX_NPC)
}

script Route104_EventScript_Woman {
    msgbox(format("You're a thief if you try to steal someone else's Pokémon.\p"
        "You should throw Poké Balls only at wild Pokémon."), MSGBOX_NPC)
}

script Route104_EventScript_Boy2 {
    lock
    faceplayer
    if (flag(FLAG_RECEIVED_TM_BULLET_SEED)) {
        msgbox(format("Some Marill like to eat all the seeds after I spit them out…"), MSGBOX_DEFAULT)
        release
    } else {
        msgbox(format("I like filling my mouth with seeds, then spitting them out fast!\p"
            "You can have this, so you try it out!\p"
            "Use it on a Pokémon, and it will learn a move for firing seeds rapidly."), MSGBOX_DEFAULT)
        giveitem(ITEM_TM_BULLET_SEED)
        goto_if_eq(VAR_RESULT, FALSE, Common_EventScript_ShowBagIsFull)
        setflag(FLAG_RECEIVED_TM_BULLET_SEED)
        release
    }
}

script Route104_EventScript_Girl2 {
    msgbox(format("Oh, no, I'm not a Trainer.\p"
        "But that's right, if Trainers lock eyes, it's a challenge to battle.\p"
        "Be careful if you only have one healthy Pokémon."), MSGBOX_NPC)
}

raw `
Route104_EventScript_SailToDewford::
    setobjectsubpriority LOCALID_BRINEY_R104, MAP_ROUTE104, 0
    setobjectsubpriority OBJ_EVENT_ID_PLAYER, MAP_ROUTE104, 0
    applymovement LOCALID_BRINEY_R104, Route104_Movement_BrineyBoardBoat
    waitmovement 0
    removeobject LOCALID_BRINEY_R104
    applymovement OBJ_EVENT_ID_PLAYER, Route104_Movement_PlayerBoardBoat
    waitmovement 0
    hideobjectat OBJ_EVENT_ID_PLAYER, MAP_ROUTE104
    call Common_EventScript_PlayBrineysBoatMusic
    goto_if_set FLAG_ENABLE_NORMAN_MATCH_CALL, Route104_EventScript_SailToDewfordNoCall
    goto_if_unset FLAG_ENABLE_NORMAN_MATCH_CALL, Route104_EventScript_SailToDewfordDadCalls
    end

Route104_EventScript_SailToDewfordNoCall::
    applymovement LOCALID_BOAT_R104, Route104_Movement_SailToDewford
    applymovement OBJ_EVENT_ID_PLAYER, Route104_Movement_SailToDewford
    waitmovement 0
    goto Route104_EventScript_ArriveInDewford

Route104_EventScript_SailToDewfordDadCalls::
    applymovement LOCALID_BOAT_R104, Route104_Movement_SailToDewfordBeforeDadCalls
    applymovement OBJ_EVENT_ID_PLAYER, Route104_Movement_SailToDewfordBeforeDadCalls
    waitmovement 0
    pokenavcall Route104_Text_DadPokenavCall
    waitmessage
    delay 30
    playfanfare MUS_REGISTER_MATCH_CALL
    msgbox Route104_Text_RegisteredDadInPokenav, MSGBOX_DEFAULT
    waitfanfare
    closemessage
    delay 30
    setflag FLAG_ENABLE_NORMAN_MATCH_CALL
    applymovement OBJ_EVENT_ID_PLAYER, Route104_Movement_SailToDewfordAfterDadCalls
    applymovement LOCALID_BOAT_R104, Route104_Movement_SailToDewfordAfterDadCalls, MAP_ROUTE104
    waitmovement LOCALID_BOAT_R104, MAP_ROUTE104
    waitmovement 0
    goto Route104_EventScript_ArriveInDewford

`

script Route104_EventScript_ArriveInDewford {
    delay(50)
    applymovement(OBJ_EVENT_ID_PLAYER, Route104_Movement_PlayerExitBoat)
    waitmovement(0)
    call(Common_EventScript_StopBrineysBoatMusic)
    showobjectat(OBJ_EVENT_ID_PLAYER, MAP_DEWFORD_TOWN)
    applymovement(OBJ_EVENT_ID_PLAYER, Route104_Movement_PlayerMoveForBriney)
    waitmovement(0)
    setobjectxyperm(LOCALID_BRINEY_DEWFORD, 12, 8)
    addobject(LOCALID_BRINEY_DEWFORD)
    setobjectsubpriority(LOCALID_BRINEY_DEWFORD, MAP_DEWFORD_TOWN, 0)
    clearflag(FLAG_HIDE_MR_BRINEY_DEWFORD_TOWN)
    applymovement(LOCALID_BRINEY_DEWFORD, Route104_Movement_BrineyExitBoat)
    waitmovement(0)
    addobject(LOCALID_BOAT_DEWFORD)
    clearflag(FLAG_HIDE_MR_BRINEY_BOAT_DEWFORD_TOWN)
    call(Common_EventScript_StopBrineysBoatMusic)
    setflag(FLAG_HIDE_ROUTE_104_MR_BRINEY_BOAT)
    hideobjectat(LOCALID_BOAT_R104, MAP_ROUTE104)
    copyvar(VAR_BRINEY_LOCATION, VAR_0x8008)
    resetobjectsubpriority(OBJ_EVENT_ID_PLAYER, MAP_ROUTE104)
    resetobjectsubpriority(LOCALID_BRINEY_DEWFORD, MAP_DEWFORD_TOWN)
    copyobjectxytoperm(LOCALID_BRINEY_DEWFORD)
    setvar(VAR_BOARD_BRINEY_BOAT_STATE, 0)
    if (flag(FLAG_DELIVERED_STEVEN_LETTER)) {
    speakername(CommonName_Text_MrBriney)
        msgbox(DewfordTown_Text_BrineyLandedInDewford, MSGBOX_DEFAULT)
    } else {
    speakername(CommonName_Text_MrBriney)
        msgbox(Route104_Text_LandedInDewfordDeliverLetter, MSGBOX_DEFAULT) }
    releaseall
}

movement Route104_Movement_SailToDewfordBeforeDadCalls {
    walk_down * 4
    walk_fast_down * 4
    walk_faster_down * 12
    walk_fast_down * 4
    walk_fast_right * 2
    walk_faster_right * 6
    walk_fast_right * 2
    walk_fast_down * 4
    walk_faster_down * 59
}

movement Route104_Movement_SailToDewfordAfterDadCalls {
    walk_faster_down * 25
    walk_fast_down * 4
    walk_fast_right * 4
    walk_faster_right * 40
    walk_fast_right * 4
    walk_right * 2
    walk_down * 2
    walk_fast_down * 15
    walk_down * 2
}

movement Route104_Movement_SailToDewford {
    walk_down * 4
    walk_fast_down * 4
    walk_faster_down * 12
    walk_fast_down * 4
    walk_fast_right * 2
    walk_faster_right * 6
    walk_fast_right * 2
    walk_fast_down * 4
    walk_faster_down * 83
    walk_fast_down * 4
    walk_fast_right * 4
    walk_faster_right * 40
    walk_fast_right * 4
    walk_right * 2
    walk_down * 2
    walk_fast_down * 15
    walk_down * 2
}

raw `
Route104_Movement_PlayerBoardBoat:
    walk_left
    walk_down
    walk_down
    step_end

Route104_Movement_PlayerExitBoat:
    walk_down
    step_end

Route104_Movement_PlayerMoveForBriney:
    walk_down
    walk_left
    walk_in_place_faster_right
    step_end

Route104_Movement_BrineyBoardBoat:
    walk_down
    walk_down
    step_end

Route104_Movement_BrineyExitBoat:
    walk_down
    walk_in_place_faster_left
    step_end

Route104_EventScript_Ivan::
    trainerbattle_single TRAINER_IVAN, Route104_Text_IvanIntro, Route104_Text_IvanDefeat
    msgbox Route104_Text_IvanPostBattle, MSGBOX_AUTOCLOSE
    end

Route104_EventScript_Billy::
    trainerbattle_single TRAINER_BILLY, Route104_Text_BillyIntro, Route104_Text_BillyDefeat
    msgbox Route104_Text_BillyPostBattle, MSGBOX_AUTOCLOSE
    end

Route104_EventScript_Haley::
    trainerbattle_single TRAINER_HALEY_1, Route104_Text_HaleyIntro, Route104_Text_HaleyDefeat, Route104_EventScript_TryRegisterHaleyAfterBattle
    specialvar VAR_RESULT, ShouldTryRematchBattle
    goto_if_eq VAR_RESULT, TRUE, Route104_EventScript_RematchHaley
    setvar VAR_0x8004, TRAINER_HALEY_1
    specialvar VAR_RESULT, IsTrainerRegistered
    goto_if_eq VAR_RESULT, FALSE, Route104_EventScript_TryRegisterHaley
    msgbox Route104_Text_HaleyPostBattle, MSGBOX_DEFAULT
    release
    end

Route104_EventScript_TryRegisterHaleyAfterBattle::
    special PlayerFaceTrainerAfterBattle
    waitmovement 0
    goto_if_set FLAG_HAS_MATCH_CALL, Route104_EventScript_RegisterHaleyAfterBattle
    release
    end

Route104_EventScript_RegisterHaleyAfterBattle::
    msgbox Route104_Text_HaleyRegister2, MSGBOX_DEFAULT
    register_matchcall TRAINER_HALEY_1
    release
    end

Route104_EventScript_TryRegisterHaley::
    goto_if_set FLAG_HAS_MATCH_CALL, Route104_EventScript_RegisterHaley
    msgbox Route104_Text_HaleyPostBattle, MSGBOX_DEFAULT
    release
    end

Route104_EventScript_RegisterHaley::
    msgbox Route104_Text_HaleyRegister1, MSGBOX_DEFAULT
    register_matchcall TRAINER_HALEY_1
    release
    end

Route104_EventScript_RematchHaley::
    trainerbattle_rematch TRAINER_HALEY_1, Route104_Text_HaleyRematchIntro, Route104_Text_HaleyRematchDefeat
    msgbox Route104_Text_HaleyPostRematch, MSGBOX_AUTOCLOSE
    end

Route104_EventScript_Winston::
    trainerbattle_single TRAINER_WINSTON_1, Route104_Text_WinstonIntro, Route104_Text_WinstonDefeat, Route104_EventScript_TryRegisterWinstonAfterBattle
    specialvar VAR_RESULT, ShouldTryRematchBattle
    goto_if_eq VAR_RESULT, TRUE, Route104_EventScript_RematchWinston
    setvar VAR_0x8004, TRAINER_WINSTON_1
    specialvar VAR_RESULT, IsTrainerRegistered
    goto_if_eq VAR_RESULT, FALSE, Route104_EventScript_TryRegisterWinston
    msgbox Route104_Text_WinstonPostBattle, MSGBOX_DEFAULT
    release
    end

Route104_EventScript_TryRegisterWinstonAfterBattle::
    special PlayerFaceTrainerAfterBattle
    waitmovement 0
    goto_if_set FLAG_HAS_MATCH_CALL, Route104_EventScript_RegisterWinstonAfterBattle
    release
    end

Route104_EventScript_RegisterWinstonAfterBattle::
    msgbox Route104_Text_WinstonRegister2, MSGBOX_DEFAULT
    register_matchcall TRAINER_WINSTON_1
    release
    end

Route104_EventScript_TryRegisterWinston::
    goto_if_set FLAG_HAS_MATCH_CALL, Route104_EventScript_RegisterWinston
    msgbox Route104_Text_WinstonPostBattle, MSGBOX_DEFAULT
    release
    end

Route104_EventScript_RegisterWinston::
    msgbox Route104_Text_WinstonRegister1, MSGBOX_DEFAULT
    register_matchcall TRAINER_WINSTON_1
    release
    end

Route104_EventScript_RematchWinston::
    trainerbattle_rematch TRAINER_WINSTON_1, Route104_Text_WinstonRematchIntro, Route104_Text_WinstonRematchDefeat
    msgbox Route104_Text_WinstonPostRematch, MSGBOX_AUTOCLOSE
    end

Route104_EventScript_Cindy::
    trainerbattle_single TRAINER_CINDY_1, Route104_Text_CindyIntro, Route104_Text_CindyDefeat, Route104_EventScript_TryRegisterCindyAfterBattle
    specialvar VAR_RESULT, ShouldTryRematchBattle
    goto_if_eq VAR_RESULT, TRUE, Route104_EventScript_RematchCindy
    setvar VAR_0x8004, TRAINER_CINDY_1
    specialvar VAR_RESULT, IsTrainerRegistered
    goto_if_eq VAR_RESULT, FALSE, Route104_EventScript_TryRegisterCindy
    msgbox Route104_Text_CindyPostBattle, MSGBOX_DEFAULT
    release
    end

Route104_EventScript_TryRegisterCindyAfterBattle::
    special PlayerFaceTrainerAfterBattle
    waitmovement 0
    goto_if_set FLAG_HAS_MATCH_CALL, Route104_EventScript_RegisterCindyAfterBattle
    release
    end

Route104_EventScript_RegisterCindyAfterBattle::
    msgbox Route104_Text_CindyRegister2, MSGBOX_DEFAULT
    register_matchcall TRAINER_CINDY_1
    release
    end

Route104_EventScript_TryRegisterCindy::
    goto_if_set FLAG_HAS_MATCH_CALL, Route104_EventScript_RegisterCindy
    msgbox Route104_Text_CindyPostBattle, MSGBOX_DEFAULT
    release
    end

Route104_EventScript_RegisterCindy::
    msgbox Route104_Text_CindyRegister1, MSGBOX_DEFAULT
    register_matchcall TRAINER_CINDY_1
    release
    end

Route104_EventScript_RematchCindy::
    trainerbattle_rematch TRAINER_CINDY_1, Route104_Text_CindyRematchIntro, Route104_Text_CindyRematchDefeat
    msgbox Route104_Text_CindyPostRematch, MSGBOX_AUTOCLOSE
    end

Route104_EventScript_Gina::
    trainerbattle_double TRAINER_GINA_AND_MIA_1, Route104_Text_GinaIntro, Route104_Text_GinaDefeat, Route104_Text_GinaNotEnoughMons
    msgbox Route104_Text_GinaPostBattle, MSGBOX_DEFAULT
    release
    end

Route104_EventScript_Mia::
    trainerbattle_double TRAINER_GINA_AND_MIA_1, Route104_Text_MiaIntro, Route104_Text_MiaDefeat, Route104_Text_MiaNotEnoughMons
    msgbox Route104_Text_MiaPostBattle, MSGBOX_DEFAULT
    release
    end

Route104_EventScript_Darian::
    trainerbattle_single TRAINER_DARIAN, Route104_Text_DarianIntro, Route104_Text_DarianDefeat
    msgbox Route104_Text_DarianPostBattle, MSGBOX_AUTOCLOSE
    end

`
text Route104_Text_MayHowsYourPokedex {
    format("Oh, by the way, {PLAYER}{KUN}, how's your Pokédex coming along?")
}

text Route104_Text_MayMinesDecentLetsBattle {
    "Mine's looking pretty decent.\n"
    "So…\l"
    "How about a little battle?"
}

text Route104_Text_MayLetsBattle {
    "So, what do you think?\n"
    "How about a little battle here?"
}

text Route104_Text_MayDefeat {
    "Yikes!\n"
    "You're better than I expected!"
}

text Route104_Text_MayPostBattle {
    format("I can tell you've gotten pretty good with the way you handle Pokémon.\p"
        "But instead of only making them battle, you should be like Mr. Briney.\p"
        "It's important to become friends with Pokémon, too.")
}

text Route104_Text_MayHaventRaisedPokemon {
    format("Oh, what's the matter?\p"
        "Haven't you caught or raised your Pokémon very much?\p"
        "That's not very good for a Trainer!")
}

text Route104_Text_MayIntro {
    format("You just became a Trainer, {PLAYER}{KUN}. I'm not going to lose!")
}

text Route104_Text_BrendanWeShouldRegister {
    format("Oh, hey, {PLAYER}!\p"
        "Cool, you had Devon install the Match Call system on your Pokénav!\p"
        "Let's register each other in our Pokénavs so we can keep in touch.")
}

text Route104_Text_MayWeShouldRegister {
    format("Oh, hi, {PLAYER}{KUN}!\p"
        "Devon upgraded your Pokénav with the Match Call system, huh?\p"
        "We should register each other so we can get in contact anytime.")
}

text Route104_Text_RegisteredMay {
    format("{PLAYER} registered May in the Pokénav.")
}

text Route104_Text_RegisteredBrendan {
    format("{PLAYER} registered Brendan in the Pokénav.")
}

text Route104_Text_BrendanHowsYourPokedex {
    "{PLAYER}, how's your Pokédex?\n"
    "Have you filled in any pages yet?"
}

text Route104_Text_BrendanDoingGreatLetsBattle {
    format("Me, I'm doing great!\p"
        "Want to check out how good I am with a battle?")
}

text Route104_Text_BrendanNoConfidence {
    format("What's the matter? Don't have any confidence in your Pokémon?")
}

text Route104_Text_BrendanLetsBattle {
    "What's up?\n"
    "Want to have a battle with me?"
}

text Route104_Text_BrendanIntro {
    format("I know you just became a Trainer, but I won't go easy!")
}

text Route104_Text_BrendanDefeat {
    "Hmm…\n"
    "You're pretty good."
}

text Route104_Text_BrendanPostBattle {
    format("You've gotten pretty decent at handling Pokémon.\p"
        "But, you know, you shouldn't just be making Pokémon battle.\p"
        "Like Mr. Briney, it's important to become friends with them, too.")
}
