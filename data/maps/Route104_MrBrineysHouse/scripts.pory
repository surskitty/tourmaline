mapscripts Route104_MrBrineysHouse_MapScripts {
    MAP_SCRIPT_ON_TRANSITION {
        setflag(FLAG_LANDMARK_MR_BRINEY_HOUSE)
        call_if_eq(VAR_BRINEY_HOUSE_STATE, 1, Route104_MrBrineysHouse_EventScript_SetBrineyPeekoPos)
        call_if_set(FLAG_RECEIVED_POKENAV, Route104_MrBrineysHouse_EventScript_HideRustboroRival)
    }
}

raw `
.set LOCALID_BRINEY, 1
.set LOCALID_PEEKO, 2

Route104_MrBrineysHouse_EventScript_HideRustboroRival::
    setflag FLAG_HIDE_RUSTBORO_CITY_RIVAL
    return

Route104_MrBrineysHouse_EventScript_SetBrineyPeekoPos::
    setobjectxyperm LOCALID_BRINEY, 9, 3
    setobjectmovementtype LOCALID_BRINEY, MOVEMENT_TYPE_WALK_SEQUENCE_DOWN_LEFT_UP_RIGHT
    setobjectxyperm LOCALID_PEEKO, 9, 6
    setobjectmovementtype LOCALID_PEEKO, MOVEMENT_TYPE_WALK_SEQUENCE_LEFT_UP_RIGHT_DOWN
    return

Route104_MrBrineysHouse_EventScript_Briney::
    lock
    faceplayer
    call_if_unset FLAG_MR_BRINEY_SAILING_INTRO, Route104_MrBrineysHouse_EventScript_SailingIntro
    goto_if_unset FLAG_DELIVERED_STEVEN_LETTER, Route104_MrBrineysHouse_EventScript_SailBothDeliveries
    goto_if_unset FLAG_DELIVERED_DEVON_GOODS, Route104_MrBrineysHouse_EventScript_SailDeliverPackage
    goto Route104_MrBrineysHouse_EventScript_WhereAreWeBound
    end

Route104_MrBrineysHouse_EventScript_SailingIntro::
    setflag FLAG_MR_BRINEY_SAILING_INTRO
    speakername CommonName_Text_MrBriney
    msgbox Route104_MrBrineysHouse_Text_WaitUpPeeko, MSGBOX_DEFAULT
    speakername CommonName_Text_MrBriney
    msgbox Route104_MrBrineysHouse_Text_ItsYouLetsSailToDewford, MSGBOX_YESNO
    goto_if_eq VAR_RESULT, NO, Route104_MrBrineysHouse_EventScript_DeclineDeliverySailing
    goto Route104_MrBrineysHouse_EventScript_SailToDewford
    end

Route104_MrBrineysHouse_EventScript_WhereAreWeBound::
    speakername CommonName_Text_MrBriney
    message Route104_MrBrineysHouse_Text_WhereAreWeBound
    waitmessage
    multichoicedefault 20, 8, MULTI_BRINEY_OFF_DEWFORD, 1, FALSE
    switch VAR_RESULT
    case 0, Route104_MrBrineysHouse_EventScript_SailToDewford
    case 1, Route104_MrBrineysHouse_EventScript_DeclineSailing
    case MULTI_B_PRESSED, Route104_MrBrineysHouse_EventScript_DeclineSailing
    end

Route104_MrBrineysHouse_EventScript_SailBothDeliveries::
    speakername CommonName_Text_MrBriney
    msgbox Route104_MrBrineysHouse_Text_NeedToMakeDeliveriesSailToDewford, MSGBOX_YESNO
    goto_if_eq VAR_RESULT, NO, Route104_MrBrineysHouse_EventScript_DeclineDeliverySailing
    goto Route104_MrBrineysHouse_EventScript_SailToDewford
    end

Route104_MrBrineysHouse_EventScript_SailDeliverPackage::
    speakername CommonName_Text_MrBriney
    msgbox Route104_MrBrineysHouse_Text_NeedToDeliverPackageSailToDewford, MSGBOX_YESNO
    goto_if_eq VAR_RESULT, NO, Route104_MrBrineysHouse_EventScript_DeclineDeliverySailing
    goto Route104_MrBrineysHouse_EventScript_SailToDewford
    end

Route104_MrBrineysHouse_EventScript_DeclineDeliverySailing::
    speakername CommonName_Text_MrBriney
    msgbox Route104_MrBrineysHouse_Text_DeclineDeliverySail, MSGBOX_DEFAULT
    release
    end

Route104_MrBrineysHouse_EventScript_DeclineSailing::
    speakername CommonName_Text_MrBriney
    msgbox Route104_MrBrineysHouse_Text_TellMeWheneverYouWantToSail, MSGBOX_DEFAULT
    release
    end

Route104_MrBrineysHouse_EventScript_SailToDewford::
    speakername CommonName_Text_MrBriney
    msgbox Route104_MrBrineysHouse_Text_SetSailForDewford, MSGBOX_DEFAULT
    call EventScript_BackupMrBrineyLocation
    setvar VAR_BOARD_BRINEY_BOAT_STATE, 1
    clearflag FLAG_HIDE_ROUTE_104_MR_BRINEY
    setflag FLAG_HIDE_BRINEYS_HOUSE_MR_BRINEY
    setflag FLAG_HIDE_BRINEYS_HOUSE_PEEKO
    setvar VAR_RUSTBORO_CITY_STATE, 8
    setvar VAR_ROUTE104_STATE, 2
    setflag FLAG_HIDE_RUSTBORO_CITY_RIVAL
    setflag FLAG_HIDE_ROUTE_104_RIVAL
    warp MAP_ROUTE104, 13, 51
    waitstate
    releaseall
    end

`

script Route104_MrBrineysHouse_EventScript_Peeko {
    lock
    faceplayer
    waitse
    playmoncry(SPECIES_WINGULL, CRY_MODE_NORMAL)
    speakername("Peeko")
    msgbox(format("Pii piihyoro!"), MSGBOX_DEFAULT)
    waitmoncry
    release
}

text Route104_MrBrineysHouse_Text_WaitUpPeeko {
    format("Hold on, lass!\n"
    "Wait up, Peeko!")
}

text Route104_MrBrineysHouse_Text_ItsYouLetsSailToDewford {
    format("Hm? You're {PLAYER}{KUN}!\n"
    "You saved my darling Peeko!\l"
    "We owe so much to you!\p"
    "What's that?\n"
    "You want to sail with me?\p"
    "Hmhmâ€¦\p"
    "You have a Letter bound for Dewford and a package for Slateport, then?\p"
    "Quite the busy life you must lead!\p"
    "But what you're asking is no problem at all.\p"
    "You've come to the right man!\n"
    "We'll set sail for Dewford.")
}

text Route104_MrBrineysHouse_Text_SetSailForDewford {
    format("Dewford it is, then!\p"
    "Anchors aweigh!\n"
    "Peeko, we're setting sail, my darling!")
}

text Route104_MrBrineysHouse_Text_DeclineDeliverySail {
    format("Is that so?\n"
    "Your deliveries can wait?\p"
    "You just go on and tell me whenever you want to set sail!")
}

text Route104_MrBrineysHouse_Text_NeedToMakeDeliveriesSailToDewford {
    format("Ahoy!\n"
    "I know exactly what you want to say!\p"
    "You're to deliver a Letter to Dewford and a package to Slateport.\p"
    "What you need me to do is no problem at all--I'm the man for the job!\p"
    "First, we'll set sail for Dewford.")
}

text Route104_MrBrineysHouse_Text_NeedToDeliverPackageSailToDewford {
    format("Ahoy!\n"
    "I know exactly what you want to say!\p"
    "You're to deliver a package to Capt. Stern in Slateport.\p"
    "What you need me to do is no problem at all--I'm the man for the job!\p"
    "First, we'll set sail for Dewford.")
}

text Route104_MrBrineysHouse_Text_WhereAreWeBound {
    format("Ahoy!\n"
    "For you, I'll go out to sea anytime!\p"
    "Now, my friend, where are we bound?")
}

text Route104_MrBrineysHouse_Text_TellMeWheneverYouWantToSail {
    format("Is that so?\n"
    "Well, Peeko owes her life to you.\p"
    "You just go on and tell me whenever you want to set sail!")
}

