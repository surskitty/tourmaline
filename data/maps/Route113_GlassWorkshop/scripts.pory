mapscripts Route113_GlassWorkshop_MapScripts {
    MAP_SCRIPT_ON_TRANSITION {
        setflag(FLAG_LANDMARK_GLASS_WORKSHOP)
        call_if_eq(VAR_GLASS_WORKSHOP_STATE, 1, Route113_GlassWorkshop_EventScript_ReenterWorkshopAfterSootSack)
    }
}

raw `
    .set BLUE_FLUTE_PRICE,   100
    .set YELLOW_FLUTE_PRICE, 200
    .set RED_FLUTE_PRICE,    200
    .set WHITE_FLUTE_PRICE,  400
    .set BLACK_FLUTE_PRICE,  400
    .set PRETTY_CHAIR_PRICE, 600
    .set PRETTY_DESK_PRICE,  600
    .set LOWEST_ASH_PRICE,   BLUE_FLUTE_PRICE

Route113_GlassWorkshop_EventScript_ReenterWorkshopAfterSootSack::
    setvar VAR_GLASS_WORKSHOP_STATE, 2
    return

Route113_GlassWorkshop_EventScript_GlassWorker::
    lock
    faceplayer
    goto_if_ge VAR_GLASS_WORKSHOP_STATE, 10, Route113_GlassWorkshop_EventScript_GiveItemAfterNoRoom
    goto_if_eq VAR_GLASS_WORKSHOP_STATE, 2, Route113_GlassWorkshop_EventScript_CheckCollectedAsh
    goto_if_eq VAR_GLASS_WORKSHOP_STATE, 1, Route113_GlassWorkshop_EventScript_ExplainSootSack
    msgbox Route113_GlassWorkshop_Text_GoCollectAshWithThis, MSGBOX_DEFAULT
    giveitem ITEM_SOOT_SACK
    goto_if_eq VAR_RESULT, FALSE, Common_EventScript_ShowBagIsFull
    setvar VAR_GLASS_WORKSHOP_STATE, 1
    msgbox Route113_GlassWorkshop_Text_ExplainSootSack, MSGBOX_DEFAULT
    release
    end

Route113_GlassWorkshop_EventScript_ExplainSootSack::
    msgbox Route113_GlassWorkshop_Text_ExplainSootSack, MSGBOX_DEFAULT
    release
    end

Route113_GlassWorkshop_EventScript_CheckCollectedAsh::
    checkitem ITEM_SOOT_SACK
    goto_if_eq VAR_RESULT, FALSE, Route113_GlassWorkshop_EventScript_SootSackNotInBag
    msgbox Route113_GlassWorkshop_Text_LetsSeeCollectedAshes, MSGBOX_DEFAULT
    goto_if_lt VAR_ASH_GATHER_COUNT, LOWEST_ASH_PRICE, Route113_GlassWorkshop_EventScript_NotEnoughAsh
    message Route113_GlassWorkshop_Text_WhichGlassItemWoudYouLike
    waitmessage
    goto Route113_GlassWorkshop_EventScript_ChooseGlassItem
    end

Route113_GlassWorkshop_EventScript_SootSackNotInBag::
    msgbox Route113_GlassWorkshop_Text_HaventGotYourSootSack, MSGBOX_DEFAULT
    release
    end

Route113_GlassWorkshop_EventScript_ChooseGlassItem::
    setvar VAR_0x8009, 0
    setvar VAR_0x8004, SCROLL_MULTI_GLASS_WORKSHOP_VENDOR
    special ShowScrollableMultichoice
    waitstate
    switch VAR_RESULT
    case 0, Route113_GlassWorkshop_EventScript_BlueFlute
    case 1, Route113_GlassWorkshop_EventScript_YellowFlute
    case 2, Route113_GlassWorkshop_EventScript_RedFlute
    case 3, Route113_GlassWorkshop_EventScript_WhiteFlute
    case 4, Route113_GlassWorkshop_EventScript_BlackFlute
    case 5, Route113_GlassWorkshop_EventScript_PrettyChair
    case 6, Route113_GlassWorkshop_EventScript_PrettyDesk
    case 7, Route113_GlassWorkshop_EventScript_CancelGlassItemSelect
    case MULTI_B_PRESSED, Route113_GlassWorkshop_EventScript_CancelGlassItemSelect
    end

Route113_GlassWorkshop_EventScript_BlueFlute::
    setvar VAR_0x8008, ITEM_BLUE_FLUTE
    bufferitemname STR_VAR_1, VAR_0x8008
    setvar VAR_0x800A, BLUE_FLUTE_PRICE
    goto_if_lt VAR_ASH_GATHER_COUNT, BLUE_FLUTE_PRICE, Route113_GlassWorkshop_EventScript_NotEnoughAshForItem
    msgbox Route113_GlassWorkshop_Text_IsThatTheItemForYou, MSGBOX_YESNO
    goto_if_eq VAR_RESULT, NO, Route113_GlassWorkshop_EventScript_ChooseDifferentItem
    setvar VAR_GLASS_WORKSHOP_STATE, 10
    subvar VAR_ASH_GATHER_COUNT, BLUE_FLUTE_PRICE
    goto Route113_GlassWorkshop_EventScript_MakeGlassItem
    end

Route113_GlassWorkshop_EventScript_YellowFlute::
    setvar VAR_0x8008, ITEM_YELLOW_FLUTE
    bufferitemname STR_VAR_1, VAR_0x8008
    setvar VAR_0x800A, YELLOW_FLUTE_PRICE
    goto_if_lt VAR_ASH_GATHER_COUNT, YELLOW_FLUTE_PRICE, Route113_GlassWorkshop_EventScript_NotEnoughAshForItem
    msgbox Route113_GlassWorkshop_Text_IsThatTheItemForYou, MSGBOX_YESNO
    goto_if_eq VAR_RESULT, NO, Route113_GlassWorkshop_EventScript_ChooseDifferentItem
    setvar VAR_GLASS_WORKSHOP_STATE, 11
    subvar VAR_ASH_GATHER_COUNT, YELLOW_FLUTE_PRICE
    goto Route113_GlassWorkshop_EventScript_MakeGlassItem
    end

Route113_GlassWorkshop_EventScript_RedFlute::
    setvar VAR_0x8008, ITEM_RED_FLUTE
    bufferitemname STR_VAR_1, VAR_0x8008
    setvar VAR_0x800A, RED_FLUTE_PRICE
    goto_if_lt VAR_ASH_GATHER_COUNT, RED_FLUTE_PRICE, Route113_GlassWorkshop_EventScript_NotEnoughAshForItem
    msgbox Route113_GlassWorkshop_Text_IsThatTheItemForYou, MSGBOX_YESNO
    goto_if_eq VAR_RESULT, NO, Route113_GlassWorkshop_EventScript_ChooseDifferentItem
    setvar VAR_GLASS_WORKSHOP_STATE, 12
    subvar VAR_ASH_GATHER_COUNT, RED_FLUTE_PRICE
    goto Route113_GlassWorkshop_EventScript_MakeGlassItem
    end

Route113_GlassWorkshop_EventScript_WhiteFlute::
    setvar VAR_0x8008, ITEM_WHITE_FLUTE
    bufferitemname STR_VAR_1, VAR_0x8008
    setvar VAR_0x800A, WHITE_FLUTE_PRICE
    goto_if_lt VAR_ASH_GATHER_COUNT, WHITE_FLUTE_PRICE, Route113_GlassWorkshop_EventScript_NotEnoughAshForItem
    msgbox Route113_GlassWorkshop_Text_IsThatTheItemForYou, MSGBOX_YESNO
    goto_if_eq VAR_RESULT, NO, Route113_GlassWorkshop_EventScript_ChooseDifferentItem
    setvar VAR_GLASS_WORKSHOP_STATE, 13
    subvar VAR_ASH_GATHER_COUNT, WHITE_FLUTE_PRICE
    goto Route113_GlassWorkshop_EventScript_MakeGlassItem
    end

Route113_GlassWorkshop_EventScript_BlackFlute::
    setvar VAR_0x8008, ITEM_BLACK_FLUTE
    bufferitemname STR_VAR_1, VAR_0x8008
    setvar VAR_0x800A, BLACK_FLUTE_PRICE
    goto_if_lt VAR_ASH_GATHER_COUNT, BLACK_FLUTE_PRICE, Route113_GlassWorkshop_EventScript_NotEnoughAshForItem
    msgbox Route113_GlassWorkshop_Text_IsThatTheItemForYou, MSGBOX_YESNO
    goto_if_eq VAR_RESULT, NO, Route113_GlassWorkshop_EventScript_ChooseDifferentItem
    setvar VAR_GLASS_WORKSHOP_STATE, 14
    subvar VAR_ASH_GATHER_COUNT, BLACK_FLUTE_PRICE
    goto Route113_GlassWorkshop_EventScript_MakeGlassItem
    end

Route113_GlassWorkshop_EventScript_PrettyChair::
    setvar VAR_0x8009, 1
    setvar VAR_0x8008, DECOR_PRETTY_CHAIR
    bufferdecorationname STR_VAR_1, VAR_0x8008
    setvar VAR_0x800A, PRETTY_CHAIR_PRICE
    goto_if_lt VAR_ASH_GATHER_COUNT, PRETTY_CHAIR_PRICE, Route113_GlassWorkshop_EventScript_NotEnoughAshForItem
    msgbox Route113_GlassWorkshop_Text_IsThatTheItemForYou, MSGBOX_YESNO
    goto_if_eq VAR_RESULT, NO, Route113_GlassWorkshop_EventScript_ChooseDifferentItem
    setvar VAR_GLASS_WORKSHOP_STATE, 15
    subvar VAR_ASH_GATHER_COUNT, PRETTY_CHAIR_PRICE
    goto Route113_GlassWorkshop_EventScript_MakeGlassItem
    end

Route113_GlassWorkshop_EventScript_PrettyDesk::
    setvar VAR_0x8009, 1
    setvar VAR_0x8008, DECOR_PRETTY_DESK
    bufferdecorationname STR_VAR_1, VAR_0x8008
    setvar VAR_0x800A, PRETTY_DESK_PRICE
    goto_if_lt VAR_ASH_GATHER_COUNT, PRETTY_DESK_PRICE, Route113_GlassWorkshop_EventScript_NotEnoughAshForItem
    msgbox Route113_GlassWorkshop_Text_IsThatTheItemForYou, MSGBOX_YESNO
    goto_if_eq VAR_RESULT, NO, Route113_GlassWorkshop_EventScript_ChooseDifferentItem
    setvar VAR_GLASS_WORKSHOP_STATE, 16
    subvar VAR_ASH_GATHER_COUNT, PRETTY_DESK_PRICE
    goto Route113_GlassWorkshop_EventScript_MakeGlassItem
    end

Route113_GlassWorkshop_EventScript_CancelGlassItemSelect::
    msgbox Route113_GlassWorkshop_Text_AllThatAshButDontWantAnything, MSGBOX_DEFAULT
    release
    end

Route113_GlassWorkshop_EventScript_NotEnoughAsh::
    setvar VAR_0x800A, LOWEST_ASH_PRICE
    subvar VAR_0x800A, VAR_ASH_GATHER_COUNT
    buffernumberstring STR_VAR_1, VAR_0x800A
    msgbox Route113_GlassWorkshop_Text_NotEnoughAshNeedX, MSGBOX_DEFAULT
    release
    end

Route113_GlassWorkshop_EventScript_NotEnoughAshForItem::
    subvar VAR_0x800A, VAR_ASH_GATHER_COUNT
    buffernumberstring STR_VAR_2, VAR_0x800A
    message Route113_GlassWorkshop_Text_NotEnoughAshToMakeItem
    waitmessage
    goto Route113_GlassWorkshop_EventScript_ChooseGlassItem
    end

Route113_GlassWorkshop_EventScript_ChooseDifferentItem::
    message Route113_GlassWorkshop_Text_WhichWouldYouLike
    waitmessage
    goto Route113_GlassWorkshop_EventScript_ChooseGlassItem
    end

Route113_GlassWorkshop_EventScript_MakeGlassItem::
    msgbox Route113_GlassWorkshop_Text_IllMakeItemForYou, MSGBOX_DEFAULT
    closemessage
    fadescreen FADE_TO_BLACK
    playse SE_SELECT
    delay 30
    fadescreen FADE_FROM_BLACK
    msgbox Route113_GlassWorkshop_Text_IveFinishedGlassItem, MSGBOX_DEFAULT
    call_if_eq VAR_0x8009, 0, Route113_GlassWorkshop_EventScript_GiveGlassFlute
    call_if_eq VAR_0x8009, 1, Route113_GlassWorkshop_EventScript_GiveGlassDecor
    setvar VAR_GLASS_WORKSHOP_STATE, 2
    release
    end

Route113_GlassWorkshop_EventScript_GiveGlassFlute::
    giveitem VAR_0x8008
    goto_if_eq VAR_RESULT, FALSE, Route113_GlassWorkshop_EventScript_NoRoomForFlute
    return

Route113_GlassWorkshop_EventScript_GiveGlassDecor::
    givedecoration VAR_0x8008
    goto_if_eq VAR_RESULT, FALSE, Route113_GlassWorkshop_EventScript_NoRoomForDecor
    return

Route113_GlassWorkshop_EventScript_NoRoomForFlute::
    call Common_EventScript_BagIsFull
    msgbox Route113_GlassWorkshop_Text_NoRoomInBag, MSGBOX_DEFAULT
    release
    end

Route113_GlassWorkshop_EventScript_NoRoomForDecor::
    call Common_EventScript_NoRoomForDecor
    msgbox Route113_GlassWorkshop_Text_NoRoomInPC, MSGBOX_DEFAULT
    release
    end

Route113_GlassWorkshop_EventScript_GiveItemAfterNoRoom::
    switch VAR_GLASS_WORKSHOP_STATE
    case 10, Route113_GlassWorkshop_EventScript_GiveBlueFlute
    case 11, Route113_GlassWorkshop_EventScript_GiveYellowFlute
    case 12, Route113_GlassWorkshop_EventScript_GiveRedFlute
    case 13, Route113_GlassWorkshop_EventScript_GiveWhiteFlute
    case 14, Route113_GlassWorkshop_EventScript_GiveBlackFlute
    case 15, Route113_GlassWorkshop_EventScript_GivePrettyChair
    case 16, Route113_GlassWorkshop_EventScript_GivePrettyDesk
    end

Route113_GlassWorkshop_EventScript_GiveBlueFlute::
    setvar VAR_0x8009, 0
    setvar VAR_0x8008, ITEM_BLUE_FLUTE
    bufferitemname STR_VAR_1, VAR_0x8008
    goto Route113_GlassWorkshop_EventScript_TryGiveItemAgain
    end

Route113_GlassWorkshop_EventScript_GiveYellowFlute::
    setvar VAR_0x8009, 0
    setvar VAR_0x8008, ITEM_YELLOW_FLUTE
    bufferitemname STR_VAR_1, VAR_0x8008
    goto Route113_GlassWorkshop_EventScript_TryGiveItemAgain
    end

Route113_GlassWorkshop_EventScript_GiveRedFlute::
    setvar VAR_0x8009, 0
    setvar VAR_0x8008, ITEM_RED_FLUTE
    bufferitemname STR_VAR_1, VAR_0x8008
    goto Route113_GlassWorkshop_EventScript_TryGiveItemAgain
    end

Route113_GlassWorkshop_EventScript_GiveWhiteFlute::
    setvar VAR_0x8009, 0
    setvar VAR_0x8008, ITEM_WHITE_FLUTE
    bufferitemname STR_VAR_1, VAR_0x8008
    goto Route113_GlassWorkshop_EventScript_TryGiveItemAgain
    end

Route113_GlassWorkshop_EventScript_GiveBlackFlute::
    setvar VAR_0x8009, 0
    setvar VAR_0x8008, ITEM_BLACK_FLUTE
    bufferitemname STR_VAR_1, VAR_0x8008
    goto Route113_GlassWorkshop_EventScript_TryGiveItemAgain
    end

Route113_GlassWorkshop_EventScript_GivePrettyChair::
    setvar VAR_0x8009, 1
    setvar VAR_0x8008, DECOR_PRETTY_CHAIR
    bufferdecorationname STR_VAR_1, DECOR_PRETTY_CHAIR
    goto Route113_GlassWorkshop_EventScript_TryGiveItemAgain
    end

Route113_GlassWorkshop_EventScript_GivePrettyDesk::
    setvar VAR_0x8009, 1
    setvar VAR_0x8008, DECOR_PRETTY_DESK
    bufferdecorationname STR_VAR_1, DECOR_PRETTY_DESK
    goto Route113_GlassWorkshop_EventScript_TryGiveItemAgain
    end

Route113_GlassWorkshop_EventScript_TryGiveItemAgain::
    msgbox Route113_GlassWorkshop_Text_IveFinishedGlassItem, MSGBOX_DEFAULT
    call_if_eq VAR_0x8009, 0, Route113_GlassWorkshop_EventScript_GiveGlassFlute
    call_if_eq VAR_0x8009, 1, Route113_GlassWorkshop_EventScript_GiveGlassDecor
    setvar VAR_GLASS_WORKSHOP_STATE, 2
    release
    end
`

script Route113_GlassWorkshop_EventScript_NinjaBoy {
    msgbox(format("It's fun to blow a glass flute while my boss is talking.\p"
    "Huff-huff! Puff-puff!"), MSGBOX_NPC)
}

text Route113_GlassWorkshop_Text_GoCollectAshWithThis {
    format("This area is covered in volcanic ash,\n"
    "huff-puff!\p"
    "I'm specially gifted, huff-puff.\p"
    "I make glass out of volcanic ash\n"
    "and make items, huff-puff.\p"
    "Go collect ashes with this, huff-puff.")
}

text Route113_GlassWorkshop_Text_ExplainSootSack {
    format("Just take that Soot Sack and walk\n"
    "through piles of ash, huff-puff.\p"
    "And it will fill up with the volcanic ash,\n"
    "huff-puff.\p"
    "Once you think you've collected a good\n"
    "amount, come see me, huff-puff.")
}

text Route113_GlassWorkshop_Text_LetsSeeCollectedAshes {
    format("Have you collected ashes, huff-puff?\n"
    "Let me see, huff-puff.")
}

text Route113_GlassWorkshop_Text_NotEnoughAshNeedX {
    format("Hmmm…\n"
    "There's not enough ash here, huff-puff.\l"
    "I can't make glass with this, huff-puff.\p"
    "Let's see… {STR_VAR_1} is the number of steps\n"
    "you'll need to walk for me to make you\l"
    "a Blue Flute, huff-puff.")
}

text Route113_GlassWorkshop_Text_WhichGlassItemWoudYouLike {
    format("Oh!\n"
    "You've got a lot of ashes, huff-puff!\p"
    "I'll make you a glass item, huff-puff!\n"
    "Which one would you like, huff-puff?")
}

text Route113_GlassWorkshop_Text_IsThatTheItemForYou {
    format("A {STR_VAR_1}, huff-puff?\n"
    "Is that the one for you, huff-puff?")
}

text Route113_GlassWorkshop_Text_WhichWouldYouLike {
    format("Which one would you like, huff-puff?")
}

text Route113_GlassWorkshop_Text_IllMakeItemForYou {
    format("A {STR_VAR_1} it is, then, huff-puff!\p"
    "Okay! I'll make it for you, huff-puff.\n"
    "Just wait a little while, huff-puff.")
}

text Route113_GlassWorkshop_Text_NotEnoughAshToMakeItem {
    format("A {STR_VAR_1}, huff-puff?\p"
    "There's not enough ash here to make\n"
    "that, though, huff-puff.\p"
    "Let's see… {STR_VAR_2} is the number of steps\n"
    "you'll need to walk through volcanic ash\l"
    "for me to make that item, huff-puff.\p"
    "Which item would you rather have me\n"
    "make instead, huff-puff?")
}

text Route113_GlassWorkshop_Text_AllThatAshButDontWantAnything {
    format("You've collected all that ash, but you\n"
    "don't want anything, huff-puff?")
}

text Route113_GlassWorkshop_Text_IveFinishedGlassItem {
    format("Ah, I've finished your {STR_VAR_1}.\n"
    "Take it, huff-puff.")
}

text Route113_GlassWorkshop_Text_NoRoomInBag {
    format("Oh?\n"
    "You've no room in your Bag, huff-puff.\p"
    "I'll hold on to it, so come back later,\n"
    "huff-puff.")
}

text Route113_GlassWorkshop_Text_NoRoomInPC {
    format("Oh?\n"
    "You've no room in your PC, huff-puff?\p"
    "I'll hold on to it, so come back later,\n"
    "huff-puff.")
}

text Route113_GlassWorkshop_Text_HaventGotYourSootSack {
    format("Hah? You haven't got your Soot Sack with you, huff-puff.\p"
    "You have to keep it with you to collect volcanic ash, huff-puff.")
}

script Route113_GlassWorkshop_Script_Tinkatuff {
    lock
    faceplayer
    waitse
    speakername("Hunter's Hodad")
    playmoncry(SPECIES_TINKATUFF, CRY_MODE_DOUBLES)
    setseenmon(SPECIES_TINKATUFF)
    msgbox("It's busy with its toolkit.", MSGBOX_DEFAULT)
    waitmoncry
    release
}
