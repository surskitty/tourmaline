mapscripts Route118_MapScripts {
    MAP_SCRIPT_ON_TRANSITION {
    	call(GabbyAndTy_EventScript_UpdateLocation)
        call_if_eq(VAR_SHOULD_END_ABNORMAL_WEATHER, 1, AbnormalWeather_EventScript_HideMapNamePopup)
        call_if_eq(VAR_ABNORMAL_WEATHER_LOCATION, ABNORMAL_WEATHER_ROUTE_118_EAST, AbnormalWeather_StartGroudonWeather)
        call_if_eq(VAR_ABNORMAL_WEATHER_LOCATION, ABNORMAL_WEATHER_ROUTE_118_WEST, AbnormalWeather_StartGroudonWeather)
    }
    MAP_SCRIPT_ON_LOAD {
        call_if_eq(VAR_ABNORMAL_WEATHER_LOCATION, ABNORMAL_WEATHER_ROUTE_118_EAST, AbnormalWeather_EventScript_PlaceTilesRoute118East)
        call_if_eq(VAR_ABNORMAL_WEATHER_LOCATION, ABNORMAL_WEATHER_ROUTE_118_WEST, AbnormalWeather_EventScript_PlaceTilesRoute118West)
        special(SetRoofBirds)
        special(SetStationaryWilds)
    }
    MAP_SCRIPT_ON_FRAME_TABLE [
        VAR_SHOULD_END_ABNORMAL_WEATHER, 1: AbnormalWeather_EventScript_EndEventAndCleanup_1
    ]
}

raw `
.set LOCALID_STEVEN, 19

Route118_EventScript_StevenTrigger0::
	lockall
	setvar VAR_0x8008, 0
	applymovement OBJ_EVENT_ID_PLAYER, Common_Movement_FaceUp
	waitmovement 0
	applymovement LOCALID_STEVEN, Route118_Movement_StevenApproachLedge0
	waitmovement 0
	goto Route118_EventScript_StevenTrigger
	end

Route118_EventScript_StevenTrigger1::
	lockall
	setvar VAR_0x8008, 1
	applymovement OBJ_EVENT_ID_PLAYER, Common_Movement_FaceUp
	waitmovement 0
	goto Route118_EventScript_StevenTrigger
	end

Route118_EventScript_StevenTrigger2::
	lockall
	setvar VAR_0x8008, 2
	applymovement OBJ_EVENT_ID_PLAYER, Common_Movement_FaceUp
	waitmovement 0
	applymovement LOCALID_STEVEN, Route118_Movement_StevenApproachLedge2
	waitmovement 0
	goto Route118_EventScript_StevenTrigger
	end

Route118_EventScript_StevenTrigger::
	playse SE_LEDGE
	applymovement LOCALID_STEVEN, Route118_Movement_StevenJumpLedge
	waitmovement 0
	delay 30
	speakername CommonName_Text_Steven
	msgbox Route118_Text_StevenQuestions, MSGBOX_DEFAULT
	closemessage
	call_if_eq VAR_0x8008, 0, Route118_EventScript_StevenExit0
	call_if_eq VAR_0x8008, 1, Route118_EventScript_StevenExit1
	call_if_eq VAR_0x8008, 2, Route118_EventScript_StevenExit2
	setvar VAR_ROUTE118_STATE, 1
	removeobject LOCALID_STEVEN
	releaseall
	end

Route118_EventScript_StevenExit0::
	applymovement OBJ_EVENT_ID_PLAYER, Route118_Movement_PlayerWatchStevenExit
	applymovement LOCALID_STEVEN, Route118_Movement_StevenExit0
	waitmovement 0
	return

Route118_EventScript_StevenExit1::
	applymovement OBJ_EVENT_ID_PLAYER, Route118_Movement_PlayerWatchStevenExit
	applymovement LOCALID_STEVEN, Route118_Movement_StevenExit1
	waitmovement 0
	return

Route118_EventScript_StevenExit2::
	applymovement OBJ_EVENT_ID_PLAYER, Route118_Movement_PlayerWatchStevenExit
	applymovement LOCALID_STEVEN, Route118_Movement_StevenExit2
	waitmovement 0
	return

Route118_Movement_PlayerWatchStevenExit:
	delay_16
	walk_in_place_faster_right
	step_end

Route118_Movement_StevenJumpLedge:
	jump_2_down
	delay_16
	walk_down
	step_end

Route118_EventScript_Rose::
	trainerbattle_single TRAINER_ROSE_1, Route118_Text_RoseIntro, Route118_Text_RoseDefeat, Route118_EventScript_RegisterRose
	specialvar VAR_RESULT, ShouldTryRematchBattle
	goto_if_eq VAR_RESULT, TRUE, Route118_EventScript_RematchRose
	msgbox Route118_Text_RosePostBattle, MSGBOX_DEFAULT
	release
	end

Route118_EventScript_RegisterRose::
	special PlayerFaceTrainerAfterBattle
	waitmovement 0
	msgbox Route118_Text_RoseRegister, MSGBOX_DEFAULT
	register_matchcall TRAINER_ROSE_1
	release
	end

Route118_EventScript_RematchRose::
	trainerbattle_rematch TRAINER_ROSE_1, Route118_Text_RoseRematchIntro, Route118_Text_RoseRematchDefeat
	msgbox Route118_Text_RosePostRematch, MSGBOX_AUTOCLOSE
	end

Route118_EventScript_Barny::
	trainerbattle_single TRAINER_BARNY, Route118_Text_BarnyIntro, Route118_Text_BarnyDefeat
	msgbox Route118_Text_BarnyPostBattle, MSGBOX_AUTOCLOSE
	end

Route118_EventScript_Wade::
	trainerbattle_single TRAINER_WADE, Route118_Text_WadeIntro, Route118_Text_WadeDefeat
	msgbox Route118_Text_WadePostBattle, MSGBOX_AUTOCLOSE
	end

Route118_EventScript_Dalton::
	trainerbattle_single TRAINER_DALTON_1, Route118_Text_DaltonIntro, Route118_Text_DaltonDefeat, Route118_EventScript_RegisterDalton
	specialvar VAR_RESULT, ShouldTryRematchBattle
	goto_if_eq VAR_RESULT, TRUE, Route118_EventScript_RematchDalton
	msgbox Route118_Text_DaltonPostBattle, MSGBOX_DEFAULT
	release
	end

Route118_EventScript_RegisterDalton::
	special PlayerFaceTrainerAfterBattle
	waitmovement 0
	msgbox Route118_Text_DaltonRegister, MSGBOX_DEFAULT
	register_matchcall TRAINER_DALTON_1
	release
	end

Route118_EventScript_RematchDalton::
	trainerbattle_rematch TRAINER_DALTON_1, Route118_Text_DaltonRematchIntro, Route118_Text_DaltonRematchDefeat
	msgbox Route118_Text_DaltonPostRematch, MSGBOX_AUTOCLOSE
	end

Route118_EventScript_Perry::
	trainerbattle_single TRAINER_PERRY, Route118_Text_PerryIntro, Route118_Text_PerryDefeat
	msgbox Route118_Text_PerryPostBattle, MSGBOX_AUTOCLOSE
	end

Route118_EventScript_Chester::
	trainerbattle_single TRAINER_CHESTER, Route118_Text_ChesterIntro, Route118_Text_ChesterDefeat
	msgbox Route118_Text_ChesterPostBattle, MSGBOX_AUTOCLOSE
	end

Route118_EventScript_Deandre::
	trainerbattle_single TRAINER_DEANDRE, Route118_Text_DeandreIntro, Route118_Text_DeandreDefeat
	msgbox Route118_Text_DeandrePostBattle, MSGBOX_AUTOCLOSE
	end
`

movement Route118_Movement_StevenApproachLedge0 { walk_left }
movement Route118_Movement_StevenApproachLedge2 { walk_right }
movement Route118_Movement_StevenExit0 { walk_right * 10 }
movement Route118_Movement_StevenExit1 { walk_right * 9 }
movement Route118_Movement_StevenExit2 { walk_right * 8 }

text Route118_Text_StevenQuestions {
	format("Hi, {PLAYER}{KUN}!\p"
	"It's me, Steven!\n"
	"We met in the cave near Dewford.\p"
	"Have you met many kinds of Pokémon since I last saw you?\p"
	"There are many, many kinds of Pokémon in this wide world of ours.\p"
	"If you wanted to raise only your favorites, that's fine.\p"
	"Perhaps you find it more fun to raise all sorts of Pokémon.\p"
	"As a Trainer of Pokémon, what do you think?\p"
	"Of course, it's not any of my business.\p"
	"It would be nice if we were to meet again somewhere.")
}

script Route118_EventScript_GoodRodFisherman {
	lock
	faceplayer
	goto_if_set(FLAG_RECEIVED_GOOD_ROD, Route118_EventScript_ReceivedGoodRod)
	msgbox(format("Hmm!\n"
	    "A Good Rod is really good!\p"
	    "Wouldn't you agree?"), MSGBOX_YESNO)
	goto_if_eq(VAR_RESULT, YES, Route118_EventScript_ReceiveGoodRod)
	goto_if_eq(VAR_RESULT, NO, Route118_EventScript_DeclineGoodRod)
}

script Route118_EventScript_ReceiveGoodRod {
	msgbox(format("Hmm!\nWe're of identical minds!\p"
        "Hmm!\nTake this Good Rod!"), MSGBOX_DEFAULT)
	giveitem(ITEM_GOOD_ROD)
	goto_if_eq(VAR_RESULT, FALSE, Common_EventScript_ShowBagIsFull)
	setflag(FLAG_RECEIVED_GOOD_ROD)
	msgbox(format("Wherever there's water, try your luck at fishing."), MSGBOX_DEFAULT)
	release
}

script Route118_EventScript_DeclineGoodRod {
	msgbox(format("Don't you like to fish?"), MSGBOX_DEFAULT)
	release
}

script Route118_EventScript_ReceivedGoodRod {
	msgbox(format("Try catching all sorts of Pokémon with your Good Rod."), MSGBOX_DEFAULT)
	release
}

script Route118_EventScript_Girl {
	msgbox(format("Even if there isn't a boat, you can cross bodies of water if you have a Pokémon that knows Surf.\p"
    	"Pokémon can be counted on to do so much!"), MSGBOX_NPC)
}

script Route118_EventScript_RouteSignMauville {
	msgbox(format("Route 118\n"
    	"{LEFT_ARROW} MAUVILLE CITY"), MSGBOX_SIGN)
}

script Route118_EventScript_RouteSign119 {
	msgbox(format("Route 118\n"
    	"{UP_ARROW} ROUTE 119"), MSGBOX_SIGN)
}
