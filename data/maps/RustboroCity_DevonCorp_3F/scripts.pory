mapscripts RustboroCity_DevonCorp_3F_MapScripts {
    MAP_SCRIPT_ON_TRANSITION {
        call_if_eq(VAR_DEVON_CORP_3F_STATE, 0, RustboroCity_DevonCorp_3F_EventScript_SetEmployeePos)
    }
    MAP_SCRIPT_ON_WARP_INTO_MAP_TABLE [
        VAR_DEVON_CORP_3F_STATE, 0 {
            turnobject(OBJ_EVENT_ID_PLAYER, DIR_EAST)
        }
    ]
    MAP_SCRIPT_ON_FRAME_TABLE [
        VAR_DEVON_CORP_3F_STATE, 0: RustboroCity_DevonCorp_3F_EventScript_MeetPresident
    ]
}

text RustboroCity_DevonCorp_Name_MrStone { "Mr. Stone" }

script RustboroCity_DevonCorp_3F_EventScript_RareRocksDisplay {
	msgbox(format("It's a collection of rare rocks and stones assembled by the President."), MSGBOX_SIGN)
}

script RustboroCity_DevonCorp_3F_EventScript_MrStone {
	lock
	faceplayer
	speakername(RustboroCity_DevonCorp_Name_MrStone)
	if (flag(FLAG_RECEIVED_EXP_SHARE)) {
	    msgbox(format("Since my youth, I've immersed myself in work.\p"
	        "Consequently, I'm not familiar with trends and that sort of thing.\p"
	        "But do young people all want to be Trainers in the Pokémon League?"), MSGBOX_DEFAULT)
	} elif (flag(FLAG_DELIVERED_STEVEN_LETTER)) {
	    msgbox(format("You delivered my Letter?\n"
	        "Thank you kindly!\p"
	        "This is my way of thanking you.\n"
	        "It's a charm for your Pokédex."), MSGBOX_DEFAULT)
	    giveitem(ITEM_CATCHING_CHARM)
	    goto_if_eq(VAR_RESULT, FALSE, Common_EventScript_ShowBagIsFull)
	    setflag(FLAG_RECEIVED_EXP_SHARE)
	    speakername(RustboroCity_DevonCorp_Name_MrStone)
	    msgbox(format("An Exp. Share gives all Pokémon in your party battle experience, even if they didn't fight.\p"
	    "It's quite useful, but you can disable it if you must."), MSGBOX_DEFAULT)
	} else {
    	msgbox(format("I'm counting on you!"), MSGBOX_DEFAULT)
	}
	closemessage
	applymovement(VAR_LAST_TALKED, Common_Movement_FaceOriginalDirection)
	waitmovement(0)
	release
}

script RustboroCity_DevonCorp_3F_EventScript_Employee {
	lock
	faceplayer
	if (flag(FLAG_RECEIVED_REPEAT_BALL)) {
	    msgbox(format("Devon's new products, the Repeat Ball and Timer Ball, have become hugely popular among Trainers."), MSGBOX_DEFAULT)
	}
	else {
	    msgbox(format("If you visit the Shipyard in Slateport, you should go see Capt. Stern."), MSGBOX_DEFAULT)
	}
	release
}


raw `
.set LOCALID_DEVON_EMPLOYEE, 2

RustboroCity_DevonCorp_3F_EventScript_SetEmployeePos::
	setobjectxyperm LOCALID_DEVON_EMPLOYEE, 3, 2
	setobjectmovementtype LOCALID_DEVON_EMPLOYEE, MOVEMENT_TYPE_FACE_LEFT
	return

RustboroCity_DevonCorp_3F_EventScript_MeetPresident::
	lockall
	msgbox RustboroCity_DevonCorp_3F_Text_ThisIs3rdFloorWaitHere, MSGBOX_DEFAULT
	closemessage
	applymovement LOCALID_DEVON_EMPLOYEE, RustboroCity_DevonCorp_3F_Movement_EmployeeWalkOffscreen
	waitmovement 0
	delay 80
	applymovement LOCALID_DEVON_EMPLOYEE, RustboroCity_DevonCorp_3F_Movement_EmployeeReturnToPlayer
	waitmovement 0
	msgbox RustboroCity_DevonCorp_3F_Text_WordWithPresidentComeWithMe, MSGBOX_DEFAULT
	closemessage
	playbgm MUS_FOLLOW_ME, FALSE
	applymovement LOCALID_DEVON_EMPLOYEE, RustboroCity_DevonCorp_3F_Movement_LeadPlayerToPresident
	applymovement OBJ_EVENT_ID_PLAYER, RustboroCity_DevonCorp_3F_Movement_PlayerFollowToPresident
	waitmovement 0
	msgbox RustboroCity_DevonCorp_3F_Text_PleaseGoAhead, MSGBOX_DEFAULT
	closemessage
	fadedefaultbgm
	applymovement LOCALID_DEVON_EMPLOYEE, RustboroCity_DevonCorp_3F_Movement_EmployeeFaceDesk
	applymovement OBJ_EVENT_ID_PLAYER, RustboroCity_DevonCorp_3F_Movement_PlayerApproachDesk
	waitmovement 0
	speakername RustboroCity_DevonCorp_Name_MrStone
	msgbox RustboroCity_DevonCorp_3F_Text_MrStoneIHaveFavor, MSGBOX_DEFAULT
	giveitem ITEM_LETTER
	speakername RustboroCity_DevonCorp_Name_MrStone
	msgbox RustboroCity_DevonCorp_3F_Text_MrStoneWantYouToHaveThis, MSGBOX_DEFAULT
	playfanfare MUS_OBTAIN_ITEM
	speakername 0
	message RustboroCity_DevonCorp_3F_Text_ReceivedPokenav
	waitfanfare
	setflag FLAG_SYS_POKENAV_GET
	setflag FLAG_RECEIVED_POKENAV
	setflag FLAG_HIDE_RUSTBORO_CITY_POKEMON_SCHOOL_SCOTT
	speakername RustboroCity_DevonCorp_Name_MrStone
	msgbox RustboroCity_DevonCorp_3F_Text_MrStoneExplainPokenav, MSGBOX_DEFAULT
	closemessage
	giveitem ITEM_EXP_SHARE
	speakername RustboroCity_DevonCorp_Name_MrStone
	msgbox RustboroCity_DevonCorp_3F_Text_MrStoneRestUp, MSGBOX_DEFAULT
	call Common_EventScript_OutOfCenterPartyHeal
	msgbox RustboroCity_DevonCorp_3F_Text_MrStoneGoWithCautionAndCare, MSGBOX_DEFAULT
	setflag FLAG_HIDE_ROUTE_116_WANDAS_BOYFRIEND
	clearflag FLAG_HIDE_RUSTURF_TUNNEL_WANDAS_BOYFRIEND
	clearflag FLAG_HIDE_RUSTURF_TUNNEL_WANDA
	clearflag FLAG_HIDE_BRINEYS_HOUSE_MR_BRINEY
	clearflag FLAG_HIDE_BRINEYS_HOUSE_PEEKO
	setvar VAR_BRINEY_LOCATION, 1
	clearflag FLAG_HIDE_RUSTBORO_CITY_RIVAL
	setvar VAR_DEVON_CORP_3F_STATE, 1
	setvar VAR_RUSTBORO_CITY_STATE, 6
	releaseall
	end

RustboroCity_DevonCorp_3F_Movement_EmployeeFaceDesk:
	delay_16
	walk_in_place_faster_down
	step_end

RustboroCity_DevonCorp_3F_Movement_EmployeeWalkOffscreen:
	walk_right
	walk_right
	walk_right
	walk_down
	walk_right
	walk_right
	walk_right
	walk_right
	step_end

RustboroCity_DevonCorp_3F_Movement_EmployeeReturnToPlayer:
	walk_left
	walk_left
	walk_left
	walk_left
	walk_up
	walk_left
	walk_left
	walk_left
	step_end

RustboroCity_DevonCorp_3F_Movement_PlayerFollowToPresident:
	walk_right
	walk_right
	walk_right
	walk_right
	walk_down
	walk_right
	walk_right
	walk_right
	walk_right
	walk_right
	walk_right
	walk_right
	walk_right
	step_end

RustboroCity_DevonCorp_3F_Movement_PlayerApproachDesk:
	delay_16
	delay_16
	walk_down
	walk_down
	walk_in_place_faster_right
	step_end

`

text RustboroCity_DevonCorp_3F_Text_MrStoneIHaveFavor {
	format("I'm Mr. Stone, the President of the Devon Corporation.\p"
	"I'd just got word about you!\p"
	"You saved our staff not just once, but twice!\p"
	"I have a favor to ask of an amazing person like you.\p"
	"I understand that you're delivering a package to Slateport's Shipyard.\p"
	"Well, on the way, could you stop off in Dewford Town?\p"
	"I was hoping that you'd deliver a Letter to Steven in Dewford.")
}

text RustboroCity_DevonCorp_3F_Text_MrStoneWantYouToHaveThis {
	format("Now, you should know that I am a great President.\p"
	"I'd never be so cheap as to ask a favor for nothing in return.\p"
	"I'd like you to try our new product.")
}

text RustboroCity_DevonCorp_3F_Text_ReceivedPokenav {
	format("{PLAYER} received a Pokénav.")
}

text RustboroCity_DevonCorp_3F_Text_MrStoneExplainPokenav {
	format("That device…\p"
	"It's a Pokémon Navigator, or Pokénav for short.\p"
	"It's an indispensable tool for any Trainer on an adventure.\p"
	"It has a map of the Hoenn region, and it links up with this, too.\p")
}

text RustboroCity_DevonCorp_3F_Text_MrStoneRestUp {
	format("That right there is an Experience Share, a device that helps all of your Pokémon grow together.\p"
	"If they seem to be growing too fast, however, you can turn it off.\p"
	"By the way, I've heard that sinister criminals--Magma and Aqua, I believe--\l"
	"have been making trouble far and wide.\p"
	"I think it would be best if you rested up before you go on your way.")
}

text RustboroCity_DevonCorp_3F_Text_MrStoneGoWithCautionAndCare {
	format("Well, then, {PLAYER}{KUN}, go with caution and care!")
}

text RustboroCity_DevonCorp_3F_Text_ThisIs3rdFloorWaitHere {
	format("This is the Devon Corporation's third floor.\p"
	"Our President's Office is on this floor.\p"
	"Anyway, I can't tell you how much I'm grateful for what you've done.\p"
	"Um… By the way, that parcel you got back for us…\p"
	"Could I get you to deliver that to the Shipyard in Slateport?\p"
	"It would be awful if those robbers tried to take it again.\p"
	"Uh… Hehehe, so, uh, could I count on you to do that for me?\p"
	"Oh, that's right.\n"
	"Could you wait here a second?")
}

text RustboroCity_DevonCorp_3F_Text_WordWithPresidentComeWithMe {
	format("Our President would like to have a word with you.\p"
	"Please come with me.")
}

text RustboroCity_DevonCorp_3F_Text_PleaseGoAhead {
	format("Please, go ahead.")
}

movement RustboroCity_DevonCorp_3F_Movement_LeadPlayerToPresident {
	walk_right * 3
	walk_down
	walk_right * 9
	walk_in_place_faster_left
}
