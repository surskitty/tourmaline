mapscripts RustboroCity_House1_MapScripts {
    MAP_SCRIPT_ON_TRANSITION {
        call(RustboroCity_House1_EventScript_SetupZigzagoonGfx)
    }
    MAP_SCRIPT_ON_RESUME {
        call(RustboroCity_House1_EventScript_SetupZigzagoonGfx)
    }
}

script RustboroCity_House1_EventScript_SetupZigzagoonGfx {
    if (flag(FLAG_RUSTBORO_NPC_TRADE_COMPLETED)) {
        clearflag(FLAG_MET_RIVAL_MOM)
    } else {
        setflag(FLAG_MET_RIVAL_MOM)
    }
}

script RustboroCity_House1_EventScript_Zigzagoon {
    lock
    faceplayer
    waitse
    playmoncry(SPECIES_ZIGZAGOON, CRY_MODE_NORMAL)
    speakername("Zigzagoon")
    msgbox("Guguuh!", MSGBOX_DEFAULT)
    waitmoncry
    release
}

raw `

.set LOCALID_ZIGZAGOON_GALAR, 3
.set LOCALID_ZIGZAGOON_HOENN, 4

RustboroCity_House1_EventScript_Trader::
    lock
    faceplayer
    goto_if_set FLAG_RUSTBORO_NPC_TRADE_COMPLETED, RustboroCity_House1_EventScript_TradeCompleted
    setvar VAR_0x8008, INGAME_TRADE_SEEDOT
    copyvar VAR_0x8004, VAR_0x8008
    specialvar VAR_RESULT, GetInGameTradeSpeciesInfo
    copyvar VAR_0x8009, VAR_RESULT
    msgbox RustboroCity_House1_Text_IllTradeIfYouWant, MSGBOX_YESNO
    goto_if_eq VAR_RESULT, NO, RustboroCity_House1_EventScript_DeclineTrade
    special ChoosePartyMon
    waitstate
    copyvar VAR_0x800A, VAR_0x8004
    goto_if_eq VAR_0x8004, PARTY_NOTHING_CHOSEN, RustboroCity_House1_EventScript_DeclineTrade
    copyvar VAR_0x8005, VAR_0x800A
    specialvar VAR_RESULT, GetTradeSpecies
    copyvar VAR_0x800B, VAR_RESULT
    goto_if_ne VAR_RESULT, VAR_0x8009, RustboroCity_House1_EventScript_NotRequestedMon
    copyvar VAR_0x8004, VAR_0x8008
    copyvar VAR_0x8005, VAR_0x800A
    special CreateInGameTradePokemon
    setflag FLAG_RUSTBORO_NPC_TRADE_COMPLETED
    clearflag FLAG_MET_RIVAL_MOM
    special DoInGameTradeScene
    removeobject LOCALID_ZIGZAGOON_GALAR 
    addobject LOCALID_ZIGZAGOON_HOENN
    waitstate
    msgbox RustboroCity_House1_Text_PleaseBeGoodToMyPokemon, MSGBOX_DEFAULT
    release
    end

RustboroCity_House1_EventScript_DeclineTrade::
    msgbox RustboroCity_House1_Text_YouDontWantToThatsOkay, MSGBOX_DEFAULT
    release
    end

RustboroCity_House1_EventScript_NotRequestedMon::
    bufferspeciesname STR_VAR_1, VAR_0x8009
    msgbox RustboroCity_House1_Text_DoesntLookLikeMonToMe, MSGBOX_DEFAULT
    release
    end

RustboroCity_House1_EventScript_TradeCompleted::
    msgbox RustboroCity_House1_Text_AnyPokemonCanBeCute, MSGBOX_DEFAULT
    release
    end

`

text RustboroCity_House1_Text_IllTradeIfYouWant {
   format("Huh? My Pokémon is cute?\n"
    "Sure, I knew that.\p"
    "But if you really want, I'm willing to trade it to you.\p"
    "I'll trade you my {STR_VAR_2} for a {STR_VAR_1} if you want.")
}

text RustboroCity_House1_Text_PleaseBeGoodToMyPokemon {
   format("Eheheh…\n"
    "Please be good to my Pokémon.")
}

text RustboroCity_House1_Text_DoesntLookLikeMonToMe {
   format("Huh? That doesn't look anything like a {STR_VAR_1} to me.")
}

text RustboroCity_House1_Text_YouDontWantToThatsOkay {
   format("Oh, if you don't want to, that's okay.\n"
    "But my Pokémon is cute, you know…")
}

text RustboroCity_House1_Text_AnyPokemonCanBeCute {
   format("Any Pokémon can be cute if you raise it with care and kindness.")
}

script RustboroCity_House1_EventScript_Hiker {
    msgbox(format("In all sorts of places, there are all sorts of Pokémon and people.\p"
    "I find that fascinating, so I go to all sorts of places."), MSGBOX_NPC)
}

