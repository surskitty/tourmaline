mapscripts RustboroCity_PokemonSchool_MapScripts {}

script RustboroCity_PokemonSchool_EventScript_Blackboard {
    lockall
    msgbox(format("The blackboard lists status changes that may affect Pokémon in battle."), MSGBOX_DEFAULT)
    goto(RustboroCity_PokemonSchool_EventScript_ChooseBlackboardTopic)
}

script RustboroCity_PokemonSchool_EventScript_ChooseBlackboardTopic {
    message("Which topic do you want to read?")
    waitmessage
    multichoicegrid(8, 1, MULTI_STATUS_INFO, 3, FALSE)
    if ((var(VAR_RESULT) != MULTI_B_PRESSED) && (var(VAR_RESULT) != 5)) {
        if (var(VAR_RESULT) == 0) { 
            msgbox(RustboroCity_PokemonSchool_Text_ExplainPoison, MSGBOX_DEFAULT)
        } elif (var(VAR_RESULT) == 1) { 
            msgbox(RustboroCity_PokemonSchool_Text_ExplainParalysis, MSGBOX_DEFAULT)
        } elif (var(VAR_RESULT) == 2) { 
            msgbox(RustboroCity_PokemonSchool_Text_ExplainSleep, MSGBOX_DEFAULT)
        } elif (var(VAR_RESULT) == 3) { 
            msgbox(RustboroCity_PokemonSchool_Text_ExplainBurn, MSGBOX_DEFAULT)
        } else { 
            msgbox(RustboroCity_PokemonSchool_Text_ExplainFreeze, MSGBOX_DEFAULT)
        } 
        goto(RustboroCity_PokemonSchool_EventScript_ChooseBlackboardTopic)
    }
    else { releaseall }
}

text RustboroCity_PokemonSchool_Text_ExplainPoison {
    format("When most Pokémon are poisoned, they steadily lose HP.\p"
        "Most Shroomish find poisons nourishing.\p"
        "Salandit and Seviper can poison even other Poison-type Pokémon.")
}

text RustboroCity_PokemonSchool_Text_ExplainParalysis {
    format("When most Pokémon become paralyzed, their Speed drops.\p"
        "Poochyena run faster instead, and Electric-type Pokémon can't be paralyzed at all.")
}

text RustboroCity_PokemonSchool_Text_ExplainSleep {
    format("If a Pokémon falls asleep, it will be unable to attack, but they can't sleep in high electricity.")
}

text RustboroCity_PokemonSchool_Text_ExplainBurn {
    format("A burn reduces Attack power except when using Facade.\p"
        "The Petalburg Gym leader specializes in burning his own Pokémon.")
}

text RustboroCity_PokemonSchool_Text_ExplainFreeze {
    format("If a Pokémon is frozen, it becomes completely helpless.\p"
        "It can be thawed by any Fire-type move, Scald, or Scorching Sands.\p"
        "Be careful not to knock out your own Pokémon.")
}

text RustboroCity_PokemonSchool_Text_StudentsWhoDontStudyGetQuickClaw {
    format("Students who don't study get a little taste of my Quick Claw.\p"
    "Whether or not you are a good student will be evident from the way you use this item.")
}

text RustboroCity_PokemonSchool_Text_ExplainQuickClaw {
    format("A Pokémon holding the Quick Claw will occasionally speed up and get to move before its opponent.\p"
        "There are many other items that are meant to be held by Pokémon.\p"
        "Just those alone will give you many topics to study!")
}

script RustboroCity_PokemonSchool_EventScript_SchoolKidM {
    msgbox(format("A Pokémon holding a Berry will heal itself…\p"
        "There are many kinds of items that Pokémon can hold…\p"
        "Boy, it sure is hard taking notes down…"), MSGBOX_NPC)
}

script RustboroCity_PokemonSchool_EventScript_GameboyKid1 {
    msgbox(format("I'm trading Pokémon with my friend right now."), MSGBOX_NPC)
}

script RustboroCity_PokemonSchool_EventScript_GameboyKid2 {
    msgbox(format("I always wanted a Seedot, and I'm finally getting one!"), MSGBOX_NPC)
}

script RustboroCity_PokemonSchool_EventScript_RichBoy {
    msgbox(format("Pokémon can hold items, but they don't know what to do with man-made items like Potion and Antidote."), MSGBOX_NPC)
}

script RustboroCity_PokemonSchool_EventScript_Lass {
    msgbox(format("You know how some Pokémon moves can confuse a Pokémon?\p"
        "A confused Pokémon will sometimes attack itself without meaning to.\p"
        "But once it leaves battle, it will return to normal."), MSGBOX_NPC)
}

script RustboroCity_PokemonSchool_EventScript_StudentNotebook {
    msgbox(format("It's this student's notebook…\p"
        "Pokémon are to be caught using Poké Balls.\p"
        "Up to six Pokémon can accompany a Trainer.\p"
        "A Trainer is someone who catches Pokémon, raises them, and battles with them.\p"
        "A Trainer's mission is to defeat the strong Trainers who await challengers in Pokémon Gyms."), MSGBOX_SIGN)
}


raw `
RustboroCity_PokemonSchool_EventScript_Teacher::
    lock
    faceplayer
    goto_if_set FLAG_RECEIVED_QUICK_CLAW, RustboroCity_PokemonSchool_EventScript_GaveQuickClaw
    call_if_eq VAR_FACING, DIR_EAST, RustboroCity_PokemonSchool_EventScript_TeacherCheckOnStudentsEast
    call_if_eq VAR_FACING, DIR_WEST, RustboroCity_PokemonSchool_EventScript_TeacherCheckOnStudentsWest
    msgbox RustboroCity_PokemonSchool_Text_StudentsWhoDontStudyGetQuickClaw, MSGBOX_DEFAULT
    giveitem ITEM_QUICK_CLAW
    goto_if_eq VAR_RESULT, 0, Common_EventScript_ShowBagIsFull
    closemessage
    applymovement VAR_LAST_TALKED, Common_Movement_WalkInPlaceFasterDown
    waitmovement 0
    setflag FLAG_RECEIVED_QUICK_CLAW
    release
    end

RustboroCity_PokemonSchool_EventScript_TeacherCheckOnStudentsEast::
    applymovement VAR_LAST_TALKED, RustboroCity_PokemonSchool_Movement_TeacherCheckOnStudentsEast
    waitmovement 0
    return

RustboroCity_PokemonSchool_EventScript_TeacherCheckOnStudentsWest::
    applymovement VAR_LAST_TALKED, RustboroCity_PokemonSchool_Movement_TeacherCheckOnStudentsWest
    waitmovement 0
    return

RustboroCity_PokemonSchool_EventScript_GaveQuickClaw::
    msgbox RustboroCity_PokemonSchool_Text_ExplainQuickClaw, MSGBOX_DEFAULT
    closemessage
    applymovement VAR_LAST_TALKED, Common_Movement_WalkInPlaceFasterDown
    waitmovement 0
    release
    end

RustboroCity_PokemonSchool_Movement_TeacherCheckOnStudentsWest:
    walk_left
    walk_down
    walk_down
    walk_right
    walk_in_place_faster_down
    delay_16
    delay_16
    delay_16
    walk_down
    walk_in_place_faster_left
    delay_16
    delay_16
    walk_right
    delay_16
    delay_16
    delay_8
    walk_up
    walk_left
    walk_left
    walk_up
    walk_up
    walk_right
    step_end

RustboroCity_PokemonSchool_Movement_TeacherCheckOnStudentsEast:
    walk_right
    walk_right
    walk_down
    walk_down
    walk_left
    walk_left
    walk_in_place_faster_down
    delay_16
    delay_16
    delay_16
    walk_down
    walk_in_place_faster_left
    delay_16
    delay_16
    walk_right
    delay_16
    delay_16
    delay_8
    walk_up
    walk_right
    walk_up
    walk_up
    walk_left
    walk_left
    step_end

RustboroCity_PokemonSchool_EventScript_Scott::
    lock
    faceplayer
    goto_if_set FLAG_MET_SCOTT_AFTER_OBTAINING_STONE_BADGE, RustboroCity_PokemonSchool_EventScript_ScottWatchStudents
    goto_if_set FLAG_MET_SCOTT_RUSTBORO, RustboroCity_PokemonSchool_EventScript_ScottSpokeAlready
    goto_if_set FLAG_BADGE01_GET, RustboroCity_PokemonSchool_EventScript_ScottGreetHasBadge
    msgbox RustboroCity_PokemonSchool_Text_ScottMetAlreadyCut, MSGBOX_DEFAULT
    addvar VAR_SCOTT_STATE, 1
    setflag FLAG_MET_SCOTT_RUSTBORO
    release
    end

RustboroCity_PokemonSchool_EventScript_ScottSpokeAlready::
    goto_if_set FLAG_BADGE01_GET, RustboroCity_PokemonSchool_EventScript_ScottNoticeBadge
    msgbox RustboroCity_PokemonSchool_Text_StudentTalentLevelUnknown, MSGBOX_DEFAULT
    release
    end

RustboroCity_PokemonSchool_EventScript_ScottGreetHasBadge::
    msgbox RustboroCity_PokemonSchool_Text_ScottMetAlreadyStoneBadge, MSGBOX_DEFAULT
    goto RustboroCity_PokemonSchool_EventScript_MetScottAfterBadge
    end

RustboroCity_PokemonSchool_EventScript_ScottNoticeBadge::
    msgbox RustboroCity_PokemonSchool_Text_ScottStoneBadge, MSGBOX_DEFAULT
    goto RustboroCity_PokemonSchool_EventScript_MetScottAfterBadge
    end

RustboroCity_PokemonSchool_EventScript_MetScottAfterBadge::
    addvar VAR_SCOTT_STATE, 1
    setflag FLAG_MET_SCOTT_AFTER_OBTAINING_STONE_BADGE
    release
    end

RustboroCity_PokemonSchool_EventScript_ScottWatchStudents::
    msgbox RustboroCity_PokemonSchool_Text_StudentTalentLevelUnknown, MSGBOX_DEFAULT
    release
    end

`

text RustboroCity_PokemonSchool_Text_ScottMetAlreadyCut {
    format("Hello? Didn't we meet before?\n"
        "I think back in Petalburg City.\p"
        "Let me introduce myself.\n"
        "My name's Scott.\p"
        "I've been traveling everywhere in search of outstanding Trainers.\p"
        "More specifically, I'm looking for Pokémon battle experts.\p"
        "So, what brings you to this School?\n"
        "Are you a Trainer, too?\p"
        "The first thing you should do is to have a Pokémon learn the move Cut.\p"
        "If I remember correctly, someone in this town has Cut.")
}

text RustboroCity_PokemonSchool_Text_StudentTalentLevelUnknown {
    format("SCOTT: Hmm…\p"
        "The talent levels of the students here are unknown. The potential's there.")
}

text RustboroCity_PokemonSchool_Text_ScottStoneBadge {
    format("SCOTT: Oh, what's that?\p"
        "It's a Stone Badge, isn't it?\n"
        "That's pretty impressive, I'd say.\p"
        "But, you know, I would've loved to see you in battle.\p"
        "It's hard to tell what you're like as a Trainer from a League Badge.")
}

text RustboroCity_PokemonSchool_Text_ScottMetAlreadyStoneBadge {
    format("Hello? Didn't we meet before?\n"
        "I think back in Petalburg City.\p"
        "Let me introduce myself.\n"
        "My name's Scott.\p"
        "I've been traveling everywhere in search of outstanding Trainers.\p"
        "More specifically, I'm looking for Pokémon battle experts.\p"
        "Oh, what's that?\n"
        "It's a Stone Badge, isn't it?\p"
        "That's pretty impressive, I'd say, for a starting Trainer like yourself.\p"
        "But, you know, I would've loved to see you in battle.\p"
        "It's hard to tell what you're like as a Trainer from a League Badge.")
}
