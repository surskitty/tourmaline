mapscripts SeafloorCavern_Room9_MapScripts {}

raw `
.set LOCALID_KYOGRE, 1
.set LOCALID_ARCHIE, 2
.set LOCALID_MAXIE, 3
.set LOCALID_GRUNT_1, 4
.set LOCALID_GRUNT_2, 5
.set LOCALID_KYOGRE_SLEEPING, 7

SeafloorCavern_Room9_EventScript_ArchieAwakenKyogre::
    lockall
    setvar VAR_0x8004, LOCALID_ARCHIE
    setvar VAR_0x8005, LOCALID_MAXIE
    setvar VAR_0x8006, LOCALID_GRUNT_1
    setvar VAR_0x8007, LOCALID_GRUNT_2
    applymovement OBJ_EVENT_ID_PLAYER, Common_Movement_WalkInPlaceFasterUp
    waitmovement 0
    applymovement OBJ_EVENT_ID_PLAYER, SeafloorCavern_Room9_Movement_Delay32
    waitmovement 0
    playbgm MUS_ENCOUNTER_AQUA, FALSE
    speakername CommonName_Text_Archie
    msgbox SeafloorCavern_Room9_Text_ArchieHoldItRightThere, MSGBOX_DEFAULT
    closemessage
    addobject VAR_0x8004
    applymovement OBJ_EVENT_ID_PLAYER, Common_Movement_WalkInPlaceFasterLeft
    waitmovement 0
    applymovement VAR_0x8004, SeafloorCavern_Room9_Movement_ArchieApproachPlayer
    waitmovement 0
    msgbox SeafloorCavern_Room9_Text_ArchieSoItWasYou, MSGBOX_DEFAULT
    applymovement VAR_0x8004, Common_Movement_WalkInPlaceFasterUp
    waitmovement 0
    msgbox SeafloorCavern_Room9_Text_ArchieBeholdKyogre, MSGBOX_DEFAULT
    applymovement VAR_0x8004, Common_Movement_FacePlayer
    waitmovement 0
    msgbox SeafloorCavern_Room9_Text_ArchieYouMustDisappear, MSGBOX_DEFAULT
    trainerbattle_no_intro TRAINER_ARCHIE, SeafloorCavern_Room9_Text_ArchieDefeat
    msgbox SeafloorCavern_Room9_Text_ArchieWithThisRedOrb, MSGBOX_DEFAULT
    setweather WEATHER_NONE
    doweather
    special Script_FadeOutMapMusic
    waitstate
    speakername 0
    msgbox SeafloorCavern_Room9_Text_RedOrbShinesByItself, MSGBOX_DEFAULT
    special WaitWeather
    waitstate
    setvar VAR_RESULT, 1
    playse SE_M_DETECT
    dofieldeffectsparkle 16, 42, 0
    waitfieldeffect FLDEFF_SPARKLE
    closemessage
    setvar VAR_RESULT, 0
    playfanfare MUS_AWAKEN_LEGEND
    playse SE_ORB
    special DoOrbEffect
    applymovement VAR_0x8004, Common_Movement_WalkInPlaceFasterUp
    applymovement OBJ_EVENT_ID_PLAYER, Common_Movement_WalkInPlaceFasterUp
    waitmovement 0
    delay 150
    removeobject LOCALID_KYOGRE_SLEEPING
    addobject LOCALID_KYOGRE
    waitstate
    delay 60
    applymovement LOCALID_KYOGRE, SeafloorCavern_Room9_Movement_KyogreApproach
    waitmovement 0
    special FadeOutOrbEffect
    waitstate
    setvar VAR_0x8004, 1  @ vertical pan
    setvar VAR_0x8005, 1  @ horizontal pan
    setvar VAR_0x8006, 8  @ num shakes
    setvar VAR_0x8007, 5  @ shake delay
    special ShakeCamera
    waitstate
    applymovement LOCALID_KYOGRE, SeafloorCavern_Room9_Movement_KyogreExit
    waitmovement 0
    removeobject LOCALID_KYOGRE
    delay 4
    setvar VAR_0x8004, 2  @ vertical pan
    setvar VAR_0x8005, 2  @ horizontal pan
    setvar VAR_0x8006, 8  @ num shakes
    setvar VAR_0x8007, 5  @ shake delay
    special ShakeCamera
    waitstate
    delay 30
    setvar VAR_0x8004, LOCALID_ARCHIE
    setvar VAR_0x8005, LOCALID_MAXIE
    setvar VAR_0x8006, LOCALID_GRUNT_1
    setvar VAR_0x8007, LOCALID_GRUNT_2
    speakername CommonName_Text_Archie
    msgbox SeafloorCavern_Room9_Text_ArchieWhereDidKyogreGo, MSGBOX_DEFAULT
    playse SE_PC_LOGIN
    applymovement OBJ_EVENT_ID_PLAYER, Common_Movement_WalkInPlaceFasterLeft
    waitmovement 0
    speakername CommonName_Text_Archie
    msgbox SeafloorCavern_Room9_Text_ArchieAMessageFromOutside, MSGBOX_DEFAULT
    closemessage
    applymovement VAR_0x8004, SeafloorCavern_Room9_Movement_ArchieListenMessage
    waitmovement 0
    speakername CommonName_Text_Archie
    msgbox SeafloorCavern_Room9_Text_ArchieWhatRainingTooHard, MSGBOX_DEFAULT
    closemessage
    playse SE_PC_OFF
    delay 20
    applymovement VAR_0x8004, Common_Movement_WalkInPlaceFasterDown
    waitmovement 0
    msgbox SeafloorCavern_Room9_Text_ArchieWhyDidKyogreDisappear, MSGBOX_DEFAULT
    closemessage
    addobject VAR_0x8005
    addobject VAR_0x8006
    addobject VAR_0x8007
    applymovement VAR_0x8007, SeafloorCavern_Room9_Movement_MagmaGruntArrive
    applymovement VAR_0x8006, SeafloorCavern_Room9_Movement_MagmaGruntArrive
    applymovement VAR_0x8005, SeafloorCavern_Room9_Movement_MaxieArrive
    waitmovement 0
    applymovement VAR_0x8004, Common_Movement_WalkInPlaceFasterLeft
    waitmovement 0
    speakername CommonName_Text_Maxie
    msgbox SeafloorCavern_Room9_Text_MaxieWhatHaveYouWrought, MSGBOX_DEFAULT
    playse SE_PIN
    applymovement VAR_0x8004, Common_Movement_ExclamationMark
    waitmovement 0
    applymovement VAR_0x8004, Common_Movement_Delay48
    waitmovement 0
    speakername CommonName_Text_Archie
    msgbox SeafloorCavern_Room9_Text_ArchieDontGetAllHighAndMighty, MSGBOX_DEFAULT
    speakername CommonName_Text_Maxie
    msgbox SeafloorCavern_Room9_Text_MaxieWeDontHaveTimeToArgue, MSGBOX_DEFAULT
    closemessage
    applymovement VAR_0x8005, SeafloorCavern_Room9_Movement_MaxieExit
    applymovement VAR_0x8004, SeafloorCavern_Room9_Movement_ArchieExit
    waitmovement 0
    speakername CommonName_Text_Maxie
    msgbox SeafloorCavern_Room9_Text_MaxieComeOnPlayer, MSGBOX_DEFAULT
    setvar VAR_ROUTE128_STATE, 1
    setvar VAR_SOOTOPOLIS_CITY_STATE, 1
    clearflag FLAG_HIDE_SOOTOPOLIS_CITY_STEVEN
    clearflag FLAG_HIDE_SOOTOPOLIS_CITY_ARCHIE
    clearflag FLAG_HIDE_SOOTOPOLIS_CITY_MAXIE
    clearflag FLAG_HIDE_SOOTOPOLIS_CITY_RESIDENTS
    clearflag FLAG_HIDE_SOOTOPOLIS_CITY_GROUDON
    clearflag FLAG_HIDE_SOOTOPOLIS_CITY_KYOGRE
    setflag FLAG_HIDE_SOOTOPOLIS_CITY_MAN_1
    setflag FLAG_LEGENDARIES_IN_SOOTOPOLIS
    clearflag FLAG_HIDE_ROUTE_128_ARCHIE
    clearflag FLAG_HIDE_ROUTE_128_MAXIE
    setflag FLAG_SYS_WEATHER_CTRL
    setflag FLAG_KYOGRE_ESCAPED_SEAFLOOR_CAVERN
    setflag FLAG_HIDE_MOSSDEEP_CITY_STEVENS_HOUSE_STEVEN
    setvar VAR_STEVENS_HOUSE_STATE, 2
    setvar VAR_SEAFLOOR_CAVERN_STATE, 1
    setflag FLAG_HIDE_SEAFLOOR_CAVERN_ROOM_9_ARCHIE
    setflag FLAG_HIDE_SEAFLOOR_CAVERN_ROOM_9_MAXIE
    setflag FLAG_HIDE_SEAFLOOR_CAVERN_ROOM_9_MAGMA_GRUNTS
    setflag FLAG_HIDE_SEAFLOOR_CAVERN_ROOM_9_KYOGRE
    setflag FLAG_HIDE_SEAFLOOR_CAVERN_AQUA_GRUNTS
    setflag FLAG_HIDE_MAP_NAME_POPUP
    warp MAP_ROUTE128, 38, 22
    waitstate
    releaseall
    end

SeafloorCavern_Room9_Movement_ArchieApproachPlayer:
    walk_right
    walk_right
    walk_right
    walk_right
    walk_right
    walk_right
    walk_right
    step_end

SeafloorCavern_Room9_Movement_Unused1:
    walk_left
    walk_left
    step_end

SeafloorCavern_Room9_Movement_ArchieListenMessage:
    walk_left
    delay_16
    step_end

SeafloorCavern_Room9_Movement_Unused2:
    walk_right
    step_end

SeafloorCavern_Room9_Movement_ArchieExit:
    face_up
    lock_facing_direction
    walk_down
    unlock_facing_direction
    walk_in_place_faster_right
    step_end

SeafloorCavern_Room9_Movement_KyogreApproach:
    delay_16
    delay_16
    walk_slow_down
    delay_16
    delay_16
    delay_16
    walk_slow_down
    delay_16
    delay_16
    delay_16
    step_end

SeafloorCavern_Room9_Movement_KyogreExit:
    slide_up
    slide_up
    step_end

SeafloorCavern_Room9_Movement_MaxieArrive:
    walk_fast_right
    walk_fast_right
    walk_fast_right
    walk_fast_right
    walk_fast_right
    step_end

SeafloorCavern_Room9_Movement_MaxieExit:
    walk_right
    walk_right
    step_end

SeafloorCavern_Room9_Movement_MagmaGruntArrive:
    walk_fast_right
    walk_fast_right
    walk_fast_right
    walk_fast_right
    step_end

SeafloorCavern_Room9_Movement_Delay32:
    delay_16
    delay_16
    step_end
`

text SeafloorCavern_Room9_Text_ArchieHoldItRightThere {
    format("Hold it right there.")
}

text SeafloorCavern_Room9_Text_ArchieSoItWasYou {
    format("Fufufu…\n"
    "So it was you, after all.")
}

text SeafloorCavern_Room9_Text_ArchieBeholdKyogre {
    format("Behold!\p"
    "See how beautiful it is, the sleeping\n"
    "form of the ancient Pokémon Kyogre!\p"
    "I have waited so long for this day to come…")
}

text SeafloorCavern_Room9_Text_ArchieYouMustDisappear {
    format("It surprises me, how you've managed to chase me here.\p"
    "But that's all over now.\p"
    "For the realization of my dream, you must disappear now!")
}

text SeafloorCavern_Room9_Text_ArchieDefeat {
    format("What?!\n"
    "I lost to a mere child like you?!")
}

text SeafloorCavern_Room9_Text_ArchieWithThisRedOrb {
    format("Fufufu…\p"
    "I commend you. I must recognize that you are truly gifted.\p"
    "But!\n"
    "I have this in my possession!\p"
    "With this Red Orb, I can make Kyogre…")
}

text SeafloorCavern_Room9_Text_RedOrbShinesByItself {
    format("The Red Orb suddenly began shining by itself!")
}

text SeafloorCavern_Room9_Text_ArchieWhereDidKyogreGo {
    format("What?!\p"
    "I didn't do anything.\n"
    "Why did the Red Orb…\p"
    "Where did Kyogre go?")
}

text SeafloorCavern_Room9_Text_ArchieAMessageFromOutside {
    format("Hm? It's a message from our members outside…")
}

text SeafloorCavern_Room9_Text_ArchieWhatRainingTooHard {
    format("Yes, what is it?\p"
    "Hm…\n"
    "It's raining heavily?\p"
    "Good… That should have happened.\n"
    "That is why we awakened Kyogre,\l"
    "to realize Team Aqua's vision of\l"
    "expanding the sea.\p"
    "What?!\p"
    "It's raining far harder than we envisioned? You're in danger?\p"
    "That can't be…\n"
    "That's just not possible…\p"
    "Hold your position and monitor the situation!")
}

text SeafloorCavern_Room9_Text_ArchieWhyDidKyogreDisappear {
    format("There's something wrong…\p"
    "The Red Orb is supposed to awaken and control Kyogre…\p"
    "But… Why?\n"
    "Why did Kyogre disappear?\p"
    "Why?!")
}

text SeafloorCavern_Room9_Text_MaxieWhatHaveYouWrought {
    format("What have you wrought?\p"
    "Archie… You've finally awoken Kyogre,\n"
    "haven't you?\p"
    "What will happen to the world if this\n"
    "downpour continues for all eternity?\p"
    "The world's landmass will drown in the deepening sea…")
}

text SeafloorCavern_Room9_Text_ArchieDontGetAllHighAndMighty {
    format("W-what?!\n"
    "Don't get all high and mighty with me!\p"
    "Wasn't it you, Team Magma, that\n"
    "infuriated Groudon?\p"
    "So long as I have this Red Orb,\n"
    "I should be able to control Kyogre…\p"
    "I should be able to control it…")
}

text SeafloorCavern_Room9_Text_MaxieWeDontHaveTimeToArgue {
    format("We don't have the time to argue about it here!\p"
    "Get outside and see for yourself!\p"
    "See if what you've wrought is the world that we desired!")
}

text SeafloorCavern_Room9_Text_MaxieComeOnPlayer {
    format("{PLAYER}, come on, you have to get out of here, too!")
}
