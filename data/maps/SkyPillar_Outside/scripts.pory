
mapscripts SkyPillar_Outside_MapScripts {
    MAP_SCRIPT_ON_TRANSITION {
        call_if_eq(VAR_SOOTOPOLIS_CITY_STATE, 3, SkyPillar_Outside_EventScript_HideMapNamePopup)
        call_if_ge(VAR_SOOTOPOLIS_CITY_STATE, 4, SkyPillar_Outside_EventScript_CheckSetAbnormalWeather)
    }
    MAP_SCRIPT_ON_LOAD {
        call_if_set(FLAG_WALLACE_GOES_TO_SKY_PILLAR, SkyPillar_Outside_EventScript_OpenDoor)
        special(SetRoofBirds)
        special(SetStationaryWilds)
    }
    MAP_SCRIPT_ON_FRAME_TABLE [
    VAR_SOOTOPOLIS_CITY_STATE, 3: SkyPillar_Outside_EventScript_WallaceScene
    ]
}

script SkyPillar_Outside_EventScript_HideMapNamePopup           { setflag(FLAG_HIDE_MAP_NAME_POPUP) }
script SkyPillar_Outside_EventScript_CheckSetAbnormalWeather    { call_if_set(FLAG_SYS_WEATHER_CTRL, Common_EventScript_SetAbnormalWeather) }


raw `
.set LOCALID_WALLACE, 1

SkyPillar_Outside_EventScript_OpenDoor::
    setmetatile 14, 4, METATILE_Pacifidlog_SkyPillar_DoorOpen_Top, FALSE
    setmetatile 14, 5, METATILE_Pacifidlog_SkyPillar_DoorOpen_Bottom, FALSE
    return

SkyPillar_Outside_EventScript_WallaceScene::
    lockall
    applymovement LOCALID_WALLACE, SkyPillar_Outside_Movement_WallaceApproachPlayer
    waitmovement 0
    applymovement OBJ_EVENT_ID_PLAYER, Common_Movement_WalkInPlaceFasterLeft
    waitmovement 0
    speakername CommonName_Text_Wallace
    msgbox SkyPillar_Outside_Text_OpenedDoorToSkyPillar, MSGBOX_DEFAULT
    closemessage
    delay 30
    setvar VAR_0x8004, 1  @ vertical pan
    setvar VAR_0x8005, 1  @ horizontal pan
    setvar VAR_0x8006, 8  @ num shakes
    setvar VAR_0x8007, 3  @ shake delay
    special ShakeCamera
    waitstate
    delay 40
    speakername CommonName_Text_Wallace
    msgbox SkyPillar_Outside_Text_EarthquakeNotMomentToWaste, MSGBOX_DEFAULT
    closemessage
    applymovement OBJ_EVENT_ID_PLAYER, SkyPillar_Outside_Movement_PlayerClimbSkyPillar
    applymovement LOCALID_WALLACE, SkyPillar_Outside_Movement_WallaceClimbSkyPillar
    waitmovement 0
    setvar VAR_0x8004, 1   @ vertical pan
    setvar VAR_0x8005, 3   @ horizontal pan
    setvar VAR_0x8006, 20  @ num shakes
    setvar VAR_0x8007, 5   @ shake delay
    special ShakeCamera
    waitstate
    delay 20
    applymovement LOCALID_WALLACE, Common_Movement_WalkInPlaceFasterLeft
    waitmovement 0
    delay 10
    applymovement LOCALID_WALLACE, Common_Movement_WalkInPlaceFasterRight
    waitmovement 0
    delay 20
    applymovement LOCALID_WALLACE, Common_Movement_WalkInPlaceFasterDown
    waitmovement 0
    delay 30
    speakername CommonName_Text_Wallace
    msgbox SkyPillar_Outside_Text_SituationGettingWorse, MSGBOX_DEFAULT
    closemessage
    setflag FLAG_SYS_WEATHER_CTRL
    setweather WEATHER_ABNORMAL
    doweather
    special WaitWeather
    waitstate
    delay 30
    speakername CommonName_Text_Wallace
    msgbox SkyPillar_Outside_Text_GotToGoBackForSootopolis, MSGBOX_DEFAULT
    closemessage
    playse SE_EXIT
    fadescreenswapbuffers FADE_TO_BLACK
    clearflag FLAG_HIDE_MAP_NAME_POPUP
    setvar VAR_SOOTOPOLIS_CITY_STATE, 4
    removeobject LOCALID_WALLACE
    clearflag FLAG_HIDE_SOOTOPOLIS_CITY_WALLACE
    fadescreenswapbuffers FADE_FROM_BLACK
    releaseall
    end
`

movement SkyPillar_Outside_Movement_WallaceApproachPlayer {
    walk_down * 7
    walk_right * 3
}

script SkyPillar_Outside_EventScript_Wallace {}
script SkyPillar_Outside_EventScript_ClosedDoor { msgbox(format("The door is closed."), MSGBOX_SIGN) }

text SkyPillar_Outside_Text_OpenedDoorToSkyPillar {
    format("Oh, my, I'm terribly sorry!\p"
    "In my haste, I didn't notice that I'd left you behind!\p"
    "I've opened the locked door of the Sky Pillar.\p"
    "{PLAYER}{KUN}, let's be on our way!")
}

text SkyPillar_Outside_Text_EarthquakeNotMomentToWaste {
    format("It's an earthquake!\p"
    "There's not a moment to waste!\n"
    "We've got to hurry!")
}

text SkyPillar_Outside_Text_SituationGettingWorse {
    format("Hmm…\n"
    "The situation is getting worse…")
}

text SkyPillar_Outside_Text_GotToGoBackForSootopolis {
    format("This isn't good…\p"
        "The weather distortion is spreading even here…\p"
        "{PLAYER}{KUN}.\p"
        "Rayquaza should be farther up from here.\p"
        "I'm worried about Sootopolis.\n"
        "I've got to go back.\p"
        "Everything is in your hands now.\n"
        "Don't fail us!")
}

movement SkyPillar_Outside_Movement_WallaceClimbSkyPillar {
    walk_left * 3
    walk_up * 4
    walk_right
    walk_up * 4
}

movement SkyPillar_Outside_Movement_PlayerClimbSkyPillar {
    walk_left * 4
    walk_up * 4
    walk_right
    walk_up * 3
}
