mapscripts SootopolisCity_Gym_1F_MapScripts {
    MAP_SCRIPT_ON_FRAME_TABLE [
        VAR_ICE_STEP_COUNT, 8: SootopolisCity_Gym_1F_EventScript_UnlockFirstStairs
        VAR_ICE_STEP_COUNT, 28: SootopolisCity_Gym_1F_EventScript_UnlockSecondStairs
        VAR_ICE_STEP_COUNT, 67: SootopolisCity_Gym_1F_EventScript_UnlockThirdStairs
        VAR_ICE_STEP_COUNT, 0: SootopolisCity_Gym_1F_EventScript_FallThroughIce
    ]
    MAP_SCRIPT_ON_RESUME {
        setstepcallback(STEP_CB_SOOTOPOLIS_ICE)
    }
    MAP_SCRIPT_ON_LOAD {
        call(SootopolisCity_Gym_1F_EventScript_CheckSetStairMetatiles)
        special(SetSootopolisGymCrackedIceMetatiles)
    }
    MAP_SCRIPT_ON_TRANSITION {
        setvar(VAR_ICE_STEP_COUNT, 1)
    }
}

raw `
SootopolisCity_Gym_1F_EventScript_CheckSetStairMetatiles::
    goto_if_lt VAR_ICE_STEP_COUNT, 8, SootopolisCity_Gym_1F_EventScript_StopCheckingStairs  @ All stairs ice
    goto_if_lt VAR_ICE_STEP_COUNT, 28, SootopolisCity_Gym_1F_EventScript_OpenFirstStairs
    goto_if_lt VAR_ICE_STEP_COUNT, 67, SootopolisCity_Gym_1F_EventScript_OpenFirstAndSecondStairs
    setmetatile 8, 4, METATILE_SootopolisGym_Stairs, FALSE
    setmetatile 8, 5, METATILE_SootopolisGym_Stairs, FALSE
SootopolisCity_Gym_1F_EventScript_OpenFirstAndSecondStairs::
    setmetatile 8, 10, METATILE_SootopolisGym_Stairs, FALSE
    setmetatile 8, 11, METATILE_SootopolisGym_Stairs, FALSE
SootopolisCity_Gym_1F_EventScript_OpenFirstStairs::
    setmetatile 8, 15, METATILE_SootopolisGym_Stairs, FALSE
    setmetatile 8, 16, METATILE_SootopolisGym_Stairs, FALSE
SootopolisCity_Gym_1F_EventScript_StopCheckingStairs::
    return

SootopolisCity_Gym_1F_EventScript_UnlockFirstStairs::
    addvar VAR_ICE_STEP_COUNT, 1
    delay 40
    playse SE_ICE_STAIRS
    call SootopolisCity_Gym_1F_EventScript_CheckSetStairMetatiles
    special DrawWholeMapView
    end

SootopolisCity_Gym_1F_EventScript_UnlockSecondStairs::
    addvar VAR_ICE_STEP_COUNT, 1
    delay 40
    playse SE_ICE_STAIRS
    call SootopolisCity_Gym_1F_EventScript_CheckSetStairMetatiles
    special DrawWholeMapView
    end

SootopolisCity_Gym_1F_EventScript_UnlockThirdStairs::
    addvar VAR_ICE_STEP_COUNT, 1
    delay 40
    playse SE_ICE_STAIRS
    call SootopolisCity_Gym_1F_EventScript_CheckSetStairMetatiles
    special DrawWholeMapView
    end

SootopolisCity_Gym_1F_EventScript_FallThroughIce::
    lockall
    delay 20
    applymovement OBJ_EVENT_ID_PLAYER, SootopolisCity_Gym_1F_Movement_FallThroughIce
    waitmovement 0
    playse SE_FALL
    delay 60
    warphole MAP_SOOTOPOLIS_CITY_GYM_B1F
    waitstate
    end

SootopolisCity_Gym_1F_Movement_FallThroughIce:
    set_invisible
    step_end

SootopolisCity_Gym_1F_EventScript_Juan::
    special HealPlayerParty
    trainerbattle_single TRAINER_JUAN_1, SootopolisCity_Gym_1F_Text_JuanIntro, SootopolisCity_Gym_1F_Text_JuanDefeat, SootopolisCity_Gym_1F_EventScript_JuanDefeated, NO_MUSIC
    specialvar VAR_RESULT, ShouldTryRematchBattle
    goto_if_eq VAR_RESULT, TRUE, SootopolisCity_Gym_1F_EventScript_JuanRematch
    msgbox SootopolisCity_Gym_1F_Text_JuanDefeat2, MSGBOX_DEFAULT
    goto_if_unset FLAG_RECEIVED_TM_WATER_PULSE, SootopolisCity_Gym_1F_EventScript_GiveWaterPulse2
    goto_if_unset FLAG_BADGE06_GET, SootopolisCity_Gym_1F_EventScript_GoGetFortreeBadge
    msgbox SootopolisCity_Gym_1F_Text_JuanPostBattle, MSGBOX_DEFAULT
    release
    end

SootopolisCity_Gym_1F_EventScript_JuanDefeated::
    speakername 0
    message SootopolisCity_Gym_1F_Text_ReceivedRainBadge
    waitmessage
    call Common_EventScript_PlayGymBadgeFanfare
    speakername SootopolisCity_Gym_Text_Juan 
    msgbox SootopolisCity_Gym_1F_Text_ExplainRainBadgeTakeThis, MSGBOX_DEFAULT
    setflag FLAG_DEFEATED_SOOTOPOLIS_GYM
    setflag FLAG_BADGE08_GET
    setflag FLAG_HIDE_SOOTOPOLIS_CITY_RESIDENTS
    setflag FLAG_HIDE_SOOTOPOLIS_CITY_STEVEN
    setflag FLAG_HIDE_SOOTOPOLIS_CITY_WALLACE
    setvar VAR_SOOTOPOLIS_CITY_STATE, 6
    clearflag FLAG_HIDE_SOOTOPOLIS_CITY_MAN_1
    setvar VAR_0x8008, 8
    call Common_EventScript_SetGymTrainers
    call SootopolisCity_Gym_1F_EventScript_GiveWaterPulse
    closemessage
    delay 30
    playfanfare MUS_REGISTER_MATCH_CALL
    speakername 0
    msgbox SootopolisCity_Gym_1F_Text_RegisteredJuan, MSGBOX_DEFAULT
    waitfanfare
    closemessage
    delay 30
    setflag FLAG_ENABLE_JUAN_MATCH_CALL
    release
    end

SootopolisCity_Gym_1F_EventScript_GiveWaterPulse::
    giveitem ITEM_TM_WATER_PULSE
    goto_if_eq VAR_RESULT, FALSE, Common_EventScript_BagIsFull
    msgbox SootopolisCity_Gym_1F_Text_ExplainWaterPulse, MSGBOX_DEFAULT
    setflag FLAG_RECEIVED_TM_WATER_PULSE
    return

SootopolisCity_Gym_1F_EventScript_GiveWaterPulse2::
    giveitem ITEM_TM_WATER_PULSE
    goto_if_eq VAR_RESULT, FALSE, Common_EventScript_ShowBagIsFull
    msgbox SootopolisCity_Gym_1F_Text_ExplainWaterPulse, MSGBOX_DEFAULT
    setflag FLAG_RECEIVED_TM_WATER_PULSE
    release
    end

SootopolisCity_Gym_1F_EventScript_GoGetFortreeBadge::
    msgbox SootopolisCity_Gym_1F_Text_GoGetFortreeBadge, MSGBOX_DEFAULT
    release
    end

SootopolisCity_Gym_1F_EventScript_JuanRematch::
    trainerbattle_rematch_double TRAINER_JUAN_1, SootopolisCity_Gym_1F_Text_JuanPreRematch, SootopolisCity_Gym_1F_Text_JuanRematchDefeat, SootopolisCity_Gym_1F_Text_JuanRematchNeedTwoMons
    msgbox SootopolisCity_Gym_1F_Text_JuanPostRematch, MSGBOX_AUTOCLOSE
    end

SootopolisCity_Gym_1F_EventScript_GymGuide::
    lock
    faceplayer
    goto_if_set FLAG_DEFEATED_SOOTOPOLIS_GYM, SootopolisCity_Gym_1F_EventScript_GymGuidePostVictory
    msgbox SootopolisCity_Gym_1F_Text_GymGuideAdvice, MSGBOX_DEFAULT
    release
    end

SootopolisCity_Gym_1F_EventScript_GymGuidePostVictory::
    msgbox SootopolisCity_Gym_1F_Text_GymGuidePostVictory, MSGBOX_DEFAULT
    release
    end

SootopolisCity_Gym_1F_EventScript_LeftGymStatue::
    lockall
    goto_if_set FLAG_BADGE08_GET, SootopolisCity_Gym_1F_EventScript_GymStatueCertified
    goto SootopolisCity_Gym_1F_EventScript_GymStatue
    end

SootopolisCity_Gym_1F_EventScript_RightGymStatue::
    lockall
    goto_if_set FLAG_BADGE08_GET, SootopolisCity_Gym_1F_EventScript_GymStatueCertified
    goto SootopolisCity_Gym_1F_EventScript_GymStatue
    end

SootopolisCity_Gym_1F_EventScript_GymStatueCertified::
    msgbox SootopolisCity_Gym_1F_Text_GymStatueCertified, MSGBOX_DEFAULT
    releaseall
    end

SootopolisCity_Gym_1F_EventScript_GymStatue::
    msgbox SootopolisCity_Gym_1F_Text_GymStatue, MSGBOX_DEFAULT
    releaseall
    end
`

text SootopolisCity_Gym_1F_Text_GymGuideAdvice {
    format("Yo! How's it going, Champion-\n"
    "bound {PLAYER}?\p"
    "Sootopolis's Gym Leader Juan is\n"
    "a master of Water-type Pokémon.\p"
    "And, to get to Juan, an icy floor\n"
    "will hamper your progress…\p"
    "Listen, I'm sorry, but that's all the\n"
    "advice that I have for you.\p"
    "The rest of the way, you have to\n"
    "go for it yourself!")
}

text SootopolisCity_Gym_1F_Text_GymGuidePostVictory {
    format("Yow! You've beaten even Juan, who\n"
    "was supposedly the best in all Hoenn!\p"
    "Okay! Check out your Trainer Card.\p"
    "If you've gotten all the Badges, you're\n"
    "set for the Pokémon League challenge!")
}

text SootopolisCity_Gym_Text_Juan { "Juan" }

text SootopolisCity_Gym_1F_Text_JuanIntro {
    format("Let me ask you.\n"
    "Did you know?\l"
    "Ah, I should not be so coy.\p"
    "It was I who taught Wallace everything\n"
    "there is to know about Pokémon.\p"
    "Once, I had given up my position as\n"
    "the Gym Leader.\p"
    "In my place, I had entrusted WALLACE\n"
    "with the Gym.\p"
    "However, a compelling reason arose for\n"
    "me to make a comeback.\p"
    "Ah, but enough chatter.\n"
    "Let us begin our match, shall we?\p"
    "Please, you shall bear witness to\n"
    "our artistry.\p"
    "A grand illusion of water sculpted\n"
    "by Pokémon and myself!")
}

text SootopolisCity_Gym_1F_Text_JuanDefeat {
    format("Ahahaha, excellent!\n"
    "Very well, you are the winner.\p"
    "From you, I sense the brilliant shine\n"
    "of skill that will overcome all.")
}

text SootopolisCity_Gym_1F_Text_JuanDefeat2 {
    format("However, compared with me or even\n"
    "Wallace, you are lacking in elegance.\p"
    "Perhaps I should make you a loan of my outfit?\p"
    "… … … … … …\n"
    "… … … … … …\p"
    "Hahaha, I merely jest!\p"
    "Rather than my clothes, I shall reward\n"
    "you with this, the Rain Badge!")
}

text SootopolisCity_Gym_1F_Text_ReceivedRainBadge {
    format("{PLAYER} received the Rain Badge\n"
    "from Juan.")
}

text SootopolisCity_Gym_1F_Text_ExplainRainBadgeTakeThis {
    format("Having the Rain Badge shall assure you the full obedience of all your Pokémon to your every command.\p"
    "The Rain Badge also allows the use\n"
    "of the HM move Waterfall to scale\l"
    "walls of cascading water.\p"
    "And, so that you never forget the\n"
    "battle we shared, take this…")
}

text SootopolisCity_Gym_1F_Text_ExplainWaterPulse {
    format("The Technical Machine I handed you contains Water Pulse.\p"
    "In use, it will occasionally confuse\n"
    "the target with ultrasonic waves.\p"
    "… … … … … …")
}

text SootopolisCity_Gym_1F_Text_RegisteredJuan { format("Registered Gym Leader Juan in the Pokénav.") }

text SootopolisCity_Gym_1F_Text_GymStatue {
    format("Sootopolis City Pokémon Gym")
}

text SootopolisCity_Gym_1F_Text_GymStatueCertified {
    format("Sootopolis City Pokémon Gym\p"
    "Juan's Certified Players:\n"
    "{PLAYER}")
}

text SootopolisCity_Gym_1F_Text_JuanPreRematch {
    format("Ah, this Gym had returned to its usual state of serenity…\p"
    "But our young typhoon has returned\n"
    "to put us to the test again!\p"
    "Well, my friend, most certainly!\p"
    "I shall be delighted to dance with you\n"
    "as often as you wish!")
}

text SootopolisCity_Gym_1F_Text_JuanRematchDefeat {
    format("Ahahaha, you are the winner!\n"
    "You have defeated me again!")
}

text SootopolisCity_Gym_1F_Text_JuanPostRematch {
    format("If I told you to become my apprentice, you will refuse, I am sure.\p"
    "I would like to make a gift of my coat to you.\p"
    "But again, you will refuse.\n"
    "I imagine that to be so.\p"
    "And that, my friend, is a certain sign of nobility!")
}

text SootopolisCity_Gym_1F_Text_JuanRematchNeedTwoMons {
    format("Ah, this Gym had returned to its usual state of serenity…\p"
    "But our young typhoon has returned to put us to the test again!\p"
    "Well, my friend, most certainly!\n"
    "I shall be delighted to dance with you…\p"
    "Ah, no, no, no.\n"
    "You have with you but one Pokémon.\p"
    "I wish that you would return with two, perhaps more, Pokémon, please.")
}

text SootopolisCity_Gym_1F_Text_JuanPostBattle {
    format("The Trainers who have gathered all the Gym Badges of Hoenn should make way to the ultimate destination.\p"
    "The Pokémon League.\p"
    "Travel to the easternmost reaches of Hoenn, to the island Ever Grande.\p"
    "There, you shall find the Pokémon League.")
}

text SootopolisCity_Gym_1F_Text_GoGetFortreeBadge {
    format("There remains but one Badge to obtain in Hoenn.\p"
    "If you wish to challenge the Pokémon League, you must obtain the last Badge from the Gym in Fortree.")
}

