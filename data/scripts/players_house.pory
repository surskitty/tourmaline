script PlayersHouse_1F_EventScript_EnterHouseMovingIn {
    speakername(CommonName_Text_Mom)
    msgbox(PlayersHouse_1F_Text_IsntItNiceInHere, MSGBOX_DEFAULT)
    applymovement(VAR_0x8004, Common_Movement_FacePlayer)
    waitmovement(0)
    call_if_eq(VAR_0x8005, MALE, PlayersHouse_1F_EventScript_MomFacePlayerMovingInMale)
    call_if_eq(VAR_0x8005, FEMALE, PlayersHouse_1F_EventScript_MomFacePlayerMovingInFemale)
    msgbox(format("The mover's Pokémon do all the work of moving us in and cleaning up after.\l"
        "This is so convenient!\p"
        "{PLAYER}, your room is upstairs.\n"
        "Go check it out, dear!\p"
        "Your Dad bought you a new clock to mark our move here.\l"
        "Don't forget to set it!"), MSGBOX_DEFAULT)
    closemessage
    setvar(VAR_LITTLEROOT_INTRO_STATE, 4)
    applymovement(OBJ_EVENT_ID_PLAYER, Common_Movement_WalkUp)
    applymovement(VAR_0x8004, Common_Movement_WalkInPlaceFasterUp)
    waitmovement(0)
    releaseall
}

script PlayersHouse_1F_EventScript_MomFacePlayerMovingInMale {
    applymovement(OBJ_EVENT_ID_PLAYER, Common_Movement_WalkInPlaceFasterRight)
    waitmovement(0)
}

script PlayersHouse_1F_EventScript_MomFacePlayerMovingInFemale {
    applymovement(OBJ_EVENT_ID_PLAYER, Common_Movement_WalkInPlaceFasterLeft)
    waitmovement(0)
}

script PlayersHouse_2F_EventScript_BlockStairsUntilClockIsSet { setvar(VAR_LITTLEROOT_INTRO_STATE, 5) }

script PlayersHouse_1F_EventScript_MomGoSeeRoom {
    speakername(CommonName_Text_Mom)
    msgbox(format("Well, {PLAYER}?\p"
        "Aren't you interested in seeing your very own room?"), MSGBOX_DEFAULT)
    closemessage
    applymovement(VAR_0x8004, Common_Movement_WalkInPlaceFasterUp)
    applymovement(OBJ_EVENT_ID_PLAYER, PlayersHouse_1F_Movement_MovePlayerAwayFromDoor)
    waitmovement(0)
    releaseall
}

script LittlerootTown_BrendansHouse_2F_EventScript_WallClock {
    lockall
    setvar(VAR_0x8004, MALE)
    goto(PlayersHouse_2F_EventScript_WallClock)
}

script LittlerootTown_MaysHouse_2F_EventScript_WallClock {
    lockall
    setvar(VAR_0x8004, FEMALE)
    goto(PlayersHouse_2F_EventScript_WallClock)
}

script PlayersHouse_2F_EventScript_WallClock {
    goto_if_set(FLAG_SET_WALL_CLOCK, PlayersHouse_2F_EventScript_CheckWallClock)
    msgbox(format("The clock is stopped…\p"
            "Better set it and start it!"), MSGBOX_DEFAULT)
    call(PlayersHouse_2F_EventScript_SetWallClock)
    delay(30)
    setvar(VAR_LITTLEROOT_INTRO_STATE, 6)
    setflag(FLAG_SET_WALL_CLOCK)
    setflag(FLAG_HIDE_LITTLEROOT_TOWN_PLAYERS_HOUSE_VIGOROTH_1)
    setflag(FLAG_HIDE_LITTLEROOT_TOWN_PLAYERS_HOUSE_VIGOROTH_2)
    checkplayergender
    call_if_eq(VAR_RESULT, MALE, PlayersHouse_2F_EventScript_MomComesUpstairsMale)
    call_if_eq(VAR_RESULT, FEMALE, PlayersHouse_2F_EventScript_MomComesUpstairsFemale)
    playse(SE_EXIT)
    removeobject(VAR_0x8008)
    releaseall
}

raw `

@ Local IDs are the same for both players houses
.set LOCALID_MOM, 1
.set LOCALID_DAD, 5
.set LOCALID_MOM_2F, 14


PlayersHouse_2F_EventScript_MomComesUpstairsMale::
    setvar VAR_0x8008, LOCALID_MOM_2F
    addobject VAR_0x8008
    applymovement VAR_0x8008, PlayersHouse_2F_Movement_MomEntersMale
    waitmovement 0
    applymovement OBJ_EVENT_ID_PLAYER, Common_Movement_WalkInPlaceFasterRight
    waitmovement 0
    speakername CommonName_Text_Mom
    msgbox PlayersHouse_2F_Text_HowDoYouLikeYourRoom, MSGBOX_DEFAULT
    closemessage
    applymovement VAR_0x8008, PlayersHouse_2F_Movement_MomExitsMale
    waitmovement 0
    return

PlayersHouse_2F_EventScript_MomComesUpstairsFemale::
    setvar VAR_0x8008, LOCALID_MOM_2F
    addobject VAR_0x8008
    applymovement VAR_0x8008, PlayersHouse_2F_Movement_MomEntersFemale
    waitmovement 0
    applymovement OBJ_EVENT_ID_PLAYER, Common_Movement_WalkInPlaceFasterLeft
    waitmovement 0
    speakername CommonName_Text_Mom
    msgbox PlayersHouse_2F_Text_HowDoYouLikeYourRoom, MSGBOX_DEFAULT
    closemessage
    applymovement VAR_0x8008, PlayersHouse_2F_Movement_MomExitsFemale
    waitmovement 0
    return

PlayersHouse_2F_EventScript_CheckWallClock::
    incrementgamestat GAME_STAT_CHECKED_CLOCK
    fadescreen FADE_TO_BLACK
    special Special_ViewWallClock
    waitstate
    releaseall
    end

PlayersHouse_2F_EventScript_SetWallClock::
    fadescreen FADE_TO_BLACK
    special StartWallClock
    waitstate
    return

PlayersHouse_2F_Movement_MomEntersMale:
    delay_8
    walk_down
    walk_in_place_faster_left
    delay_16
    delay_8
    walk_left
    step_end

PlayersHouse_2F_Movement_MomExitsMale:
    walk_right
    walk_up
    delay_8
    step_end

PlayersHouse_2F_Movement_MomEntersFemale:
    delay_8
    walk_down
    walk_in_place_faster_right
    delay_16
    delay_8
    walk_right
    step_end

PlayersHouse_2F_Movement_MomExitsFemale:
    walk_left
    walk_up
    delay_8
    step_end

PlayersHouse_1F_EventScript_SetWatchedBroadcast::
    setvar VAR_LITTLEROOT_INTRO_STATE, 7
    releaseall
    end

PlayersHouse_1F_EventScript_PetalburgGymReportMale::
    applymovement VAR_0x8005, Common_Movement_WalkInPlaceFasterRight
    waitmovement 0
    call PlayersHouse_1F_EventScript_MomNoticeGymBroadcast
    applymovement OBJ_EVENT_ID_PLAYER, PlayersHouse_1F_Movement_PlayerApproachTVForGymMale
    waitmovement 0
    playbgm MUS_ENCOUNTER_INTERVIEWER, FALSE
    speakername CommonName_Text_Mom
    msgbox PlayersHouse_1F_Text_MaybeDadWillBeOn, MSGBOX_DEFAULT
    closemessage
    applymovement VAR_0x8005, PlayersHouse_1F_Movement_MomMakeRoomToSeeTVMale
    waitmovement 0
    applymovement OBJ_EVENT_ID_PLAYER, PlayersHouse_1F_Movement_PlayerMoveToTVMale
    waitmovement 0
    call PlayersHouse_1F_EventScript_WatchGymBroadcast
    applymovement OBJ_EVENT_ID_PLAYER, Common_Movement_WalkInPlaceFasterLeft
    waitmovement 0
    speakername CommonName_Text_Mom
    msgbox PlayersHouse_1F_Text_ItsOverWeMissedHim, MSGBOX_DEFAULT
    speakername CommonName_Text_Mom
    msgbox PlayersHouse_1F_Text_GoIntroduceYourselfNextDoor, MSGBOX_DEFAULT
    closemessage
    setvar VAR_TEMP_1, 1
    applymovement VAR_0x8005, PlayersHouse_1F_Movement_MomReturnToSeatMale
    waitmovement 0
    goto PlayersHouse_1F_EventScript_SetWatchedBroadcast
    end

PlayersHouse_1F_EventScript_PetalburgGymReportFemale::
    applymovement VAR_0x8005, Common_Movement_WalkInPlaceFasterLeft
    waitmovement 0
    call PlayersHouse_1F_EventScript_MomNoticeGymBroadcast
    applymovement OBJ_EVENT_ID_PLAYER, PlayersHouse_1F_Movement_PlayerApproachTVForGymFemale
    waitmovement 0
    playbgm MUS_ENCOUNTER_INTERVIEWER, FALSE
    speakername CommonName_Text_Mom
    msgbox PlayersHouse_1F_Text_MaybeDadWillBeOn, MSGBOX_DEFAULT
    closemessage
    applymovement VAR_0x8005, PlayersHouse_1F_Movement_MomMakeRoomToSeeTVFemale
    waitmovement 0
    applymovement OBJ_EVENT_ID_PLAYER, PlayersHouse_1F_Movement_PlayerMoveToTVFemale
    waitmovement 0
    call PlayersHouse_1F_EventScript_WatchGymBroadcast
    applymovement OBJ_EVENT_ID_PLAYER, Common_Movement_WalkInPlaceFasterRight
    waitmovement 0
    speakername CommonName_Text_Mom
    msgbox PlayersHouse_1F_Text_ItsOverWeMissedHim, MSGBOX_DEFAULT
    speakername CommonName_Text_Mom
    msgbox PlayersHouse_1F_Text_GoIntroduceYourselfNextDoor, MSGBOX_DEFAULT
    closemessage
    setvar VAR_TEMP_1, 1
    applymovement VAR_0x8005, PlayersHouse_1F_Movement_MomReturnToSeatFemale
    waitmovement 0
    goto PlayersHouse_1F_EventScript_SetWatchedBroadcast
    end

PlayersHouse_1F_EventScript_MomNoticeGymBroadcast::
    playse SE_PIN
    applymovement VAR_0x8005, Common_Movement_ExclamationMark
    waitmovement 0
    applymovement VAR_0x8005, Common_Movement_Delay48
    waitmovement 0
    msgbox PlayersHouse_1F_Text_OhComeQuickly, MSGBOX_DEFAULT
    closemessage
    return

PlayersHouse_1F_EventScript_WatchGymBroadcast::
    applymovement OBJ_EVENT_ID_PLAYER, Common_Movement_WalkInPlaceFasterUp
    waitmovement 0
    speakername CommonName_Text_Interviewer
    msgbox PlayersHouse_1F_Text_ReportFromPetalburgGym, MSGBOX_DEFAULT
    fadedefaultbgm
    special TurnOffTVScreen
    setflag FLAG_SYS_TV_HOME
    delay 35
    return

PlayersHouse_1F_Movement_MomApproachDadMale:
    walk_up
    walk_right
    walk_right
    walk_right
    walk_right
    walk_down
    walk_in_place_faster_right
    step_end

PlayersHouse_1F_Movement_MomApproachDadFemale:
    walk_up
    walk_left
    walk_left
    walk_left
    walk_left
    walk_down
    walk_in_place_faster_left
    step_end

PlayersHouse_1F_Movement_MomApproachPlayerMale:
    walk_right
    step_end

PlayersHouse_1F_Movement_MomApproachPlayerFemale:
    walk_left
    step_end

PlayersHouse_1F_Movement_MomNoticesLatiBroadcastMale:
    walk_in_place_faster_left
    delay_16
    delay_16
    delay_16
    delay_16
    walk_in_place_faster_right
    delay_16
    delay_16
    step_end

PlayersHouse_1F_Movement_MomNoticesLatiBroadcastFemale:
    walk_in_place_faster_right
    delay_16
    delay_16
    delay_16
    delay_16
    walk_in_place_faster_left
    delay_16
    delay_16
    step_end

PlayersHouse_1F_Movement_MomApproachPlayerAfterTVMale:
    walk_up
    walk_left
    walk_left
    step_end

PlayersHouse_1F_Movement_MomApproachPlayerAfterTVFemale:
    walk_up
    walk_right
    walk_right
    step_end

PlayersHouse_1F_Movement_MomMakeRoomToSeeTVMale:
    walk_left
    walk_in_place_faster_right
    step_end

PlayersHouse_1F_Movement_MomMakeRoomToSeeTVFemale:
    walk_right
    walk_in_place_faster_left
    step_end

PlayersHouse_1F_Movement_MomReturnToSeatMale:
    walk_left
    walk_down
    walk_in_place_faster_right
    step_end

PlayersHouse_1F_Movement_MomReturnToSeatFemale:
    walk_right
    walk_down
    walk_in_place_faster_left
    step_end

PlayersHouse_1F_EventScript_Mom::
    lock
    faceplayer
    goto_if_eq VAR_LITTLEROOT_HOUSES_STATE_MAY, 4, PlayersHouse_1F_EventScript_DontPushYourselfTooHard
    goto_if_eq VAR_LITTLEROOT_HOUSES_STATE_BRENDAN, 4, PlayersHouse_1F_EventScript_DontPushYourselfTooHard
    goto_if_set FLAG_HAS_MATCH_CALL, PlayersHouse_1F_EventScript_TryRegisterMom
    goto_if_set FLAG_RESCUED_BIRCH, PlayersHouse_1F_EventScript_MomHealsParty
    goto_if_eq VAR_TEMP_1, 1, PlayersHouse_1F_EventScript_SeeYouHoney
    goto_if_eq VAR_LITTLEROOT_INTRO_STATE, 7, PlayersHouse_1F_EventScript_DidYouMeetProfBirch
    msgbox PlayersHouse_1F_Text_IsntItNiceInHere, MSGBOX_DEFAULT
    release
    end

PlayersHouse_1F_EventScript_DontPushYourselfTooHard::
    speakername CommonName_Text_Mom
    msgbox PlayersHouse_1F_Text_DontPushYourselfTooHard, MSGBOX_DEFAULT
    release
    end

PlayersHouse_1F_EventScript_TryRegisterMom::
    goto_if_set FLAG_ENABLE_MOM_MATCH_CALL, PlayersHouse_1F_EventScript_CheckGiveAmuletCoin
    speakername CommonName_Text_Mom
    msgbox PlayersHouse_1F_Text_IsThatAPokenav, MSGBOX_DEFAULT
    closemessage
    delay 30
    playfanfare MUS_REGISTER_MATCH_CALL
    speakername 0
    msgbox PlayersHouse_1F_Text_RegisteredMom, MSGBOX_DEFAULT
    waitfanfare
    closemessage
    delay 30
    setflag FLAG_ENABLE_MOM_MATCH_CALL
    release
    end

PlayersHouse_1F_EventScript_CheckGiveAmuletCoin::
    goto_if_set FLAG_BADGE05_GET, PlayersHouse_1F_EventScript_TryGiveAmuletCoin
    goto PlayersHouse_1F_EventScript_MomHealsParty

PlayersHouse_1F_EventScript_TryGiveAmuletCoin::
    goto_if_set FLAG_RECEIVED_AMULET_COIN, PlayersHouse_1F_EventScript_MomHealsParty
    speakername CommonName_Text_Mom
    msgbox PlayersHouse_1F_Text_GotDadsBadgeHeresSomethingFromMom, MSGBOX_DEFAULT
    giveitem ITEM_AMULET_COIN
    goto_if_eq VAR_RESULT, FALSE, Common_EventScript_ShowBagIsFull
    speakername CommonName_Text_Mom
    msgbox PlayersHouse_1F_Text_DontPushYourselfTooHard, MSGBOX_DEFAULT
    setflag FLAG_RECEIVED_AMULET_COIN
    release
    end

PlayersHouse_1F_EventScript_MomHealsParty::
    speakername CommonName_Text_Mom
    msgbox PlayersHouse_1F_Text_YouShouldRestABit, MSGBOX_DEFAULT
    goto PlayersHouse_1F_EventScript_HealParty
    end

PlayersHouse_1F_EventScript_SeeYouHoney::
    speakername CommonName_Text_Mom
    msgbox PlayersHouse_1F_Text_SeeYouHoney, MSGBOX_DEFAULT
    release
    end

PlayersHouse_1F_EventScript_HealParty::
    closemessage
    call Common_EventScript_OutOfCenterPartyHeal
    incrementgamestat GAME_STAT_RESTED_AT_HOME
    speakername CommonName_Text_Mom
    msgbox PlayersHouse_1F_Text_TakeCareHoney, MSGBOX_DEFAULT
    release
    end

PlayersHouse_1F_EventScript_DidYouMeetProfBirch::
    speakername CommonName_Text_Mom
    msgbox PlayersHouse_1F_Text_DidYouMeetProfBirch, MSGBOX_DEFAULT
    release
    end

PlayersHouse_1F_EventScript_Vigoroth1::
    lock
    faceplayer
    waitse
    playmoncry SPECIES_VIGOROTH, CRY_MODE_NORMAL
    setseenmon SPECIES_VIGOROTH
    msgbox PlayersHouse_1F_Text_Vigoroth1, MSGBOX_DEFAULT
    waitmoncry
    release
    end

PlayersHouse_1F_EventScript_Vigoroth2::
    lock
    faceplayer
    waitse
    playmoncry SPECIES_VIGOROTH, CRY_MODE_NORMAL
    setseenmon SPECIES_VIGOROTH
    msgbox PlayersHouse_1F_Text_Vigoroth2, MSGBOX_DEFAULT
    waitmoncry
    release
    end

PlayersHouse_1F_Movement_PlayerApproachTVForGymMale:
    walk_down
    walk_down
    walk_left
    walk_left
    walk_left
    step_end

PlayersHouse_1F_Movement_PlayerMoveToTVMale:
    walk_left
    step_end

PlayersHouse_1F_Movement_PlayerApproachTVForGymFemale:
    walk_down
    walk_down
    walk_right
    walk_right
    walk_right
    step_end

PlayersHouse_1F_Movement_PlayerMoveToTVFemale:
    walk_right
    step_end

PlayersHouse_1F_Movement_MovePlayerAwayFromDoor:
    walk_up
    step_end

PlayersHouse_1F_EventScript_GetSSTicketAndSeeLatiTV::
    lockall
    checkplayergender
    call_if_eq VAR_RESULT, MALE, PlayersHouse_1F_EventScript_SetUpObjectEventVarsMale
    call_if_eq VAR_RESULT, FEMALE, PlayersHouse_1F_EventScript_SetUpObjectEventVarsFemale
    call_if_eq VAR_0x8008, MALE, PlayersHouse_1F_EventScript_PlayerEnterRoomMale
    call_if_eq VAR_0x8008, FEMALE, PlayersHouse_1F_EventScript_PlayerEnterRoomFemale
    applymovement VAR_0x8009, Common_Movement_FacePlayer
    waitmovement 0
    playse SE_PIN
    applymovement VAR_0x8009, Common_Movement_ExclamationMark
    waitmovement 0
    applymovement VAR_0x8009, Common_Movement_Delay48
    waitmovement 0
    delay 20
    call_if_eq VAR_0x8008, MALE, PlayersHouse_1F_EventScript_DadApproachPlayerMale
    call_if_eq VAR_0x8008, FEMALE, PlayersHouse_1F_EventScript_DadApproachPlayerFemale
    speakername CommonName_Text_Dad
    msgbox PlayersHouse_1F_Text_TicketFromBrineyCameForYou, MSGBOX_DEFAULT
    giveitem ITEM_SS_TICKET
    speakername CommonName_Text_Dad
    msgbox PlayersHouse_1F_Text_PortsInSlateportLilycove, MSGBOX_DEFAULT
    closemessage
    delay 20
    call_if_eq VAR_0x8008, MALE, PlayersHouse_1F_EventScript_MomApproachDadMale
    call_if_eq VAR_0x8008, FEMALE, PlayersHouse_1F_EventScript_MomApproachDadFemale
    speakername CommonName_Text_Dad
    msgbox PlayersHouse_1F_Text_BetterGetBackToGym, MSGBOX_DEFAULT
    closemessage
    call_if_eq VAR_0x8008, MALE, PlayersHouse_1F_EventScript_DadExitsMale
    call_if_eq VAR_0x8008, FEMALE, PlayersHouse_1F_EventScript_DadExitsFemale
    playse SE_DOOR
    removeobject VAR_0x8009
    setflag FLAG_RECEIVED_SS_TICKET
    delay 30
    call_if_eq VAR_0x8008, MALE, PlayersHouse_1F_EventScript_MomApproachPlayerMale
    call_if_eq VAR_0x8008, FEMALE, PlayersHouse_1F_EventScript_MomApproachPlayerFemale
    delay 20
    speakername CommonName_Text_Mom
    msgbox PlayersHouse_1F_Text_DadShouldStayLonger, MSGBOX_DEFAULT
    closemessage
    setflag FLAG_SYS_TV_LATIAS_LATIOS
    special TurnOnTVScreen
    delay 60
    call_if_eq VAR_0x8008, MALE, PlayersHouse_1F_EventScript_MomNoticesLatiBroadcastMale
    call_if_eq VAR_0x8008, FEMALE, PlayersHouse_1F_EventScript_MomNoticesLatiBroadcastFemale
    speakername CommonName_Text_Mom
    msgbox PlayersHouse_1F_Text_IsThatABreakingStory, MSGBOX_DEFAULT
    closemessage
    call_if_eq VAR_0x8008, MALE, PlayersHouse_1F_EventScript_PlayerApproachTVForLatiMale
    call_if_eq VAR_0x8008, FEMALE, PlayersHouse_1F_EventScript_PlayerApproachTVForLatiFemale
    speakername 0
    msgbox PlayersHouse_1F_Text_LatiEmergencyNewsFlash, MSGBOX_DEFAULT
    closemessage
    clearflag FLAG_SYS_TV_LATIAS_LATIOS
    setflag FLAG_LATIOS_OR_LATIAS_ROAMING
    special TurnOffTVScreen
    call_if_eq VAR_0x8008, MALE, PlayersHouse_1F_EventScript_MomApproachPlayerAfterTVMale
    call_if_eq VAR_0x8008, FEMALE, PlayersHouse_1F_EventScript_MomApproachPlayerAfterTVFemale
    speakername CommonName_Text_Mom
    msgbox PlayersHouse_1F_Text_WhatColorDidTheySay, MSGBOX_DEFAULT
    multichoice 22, 8, MULTI_TV_LATI, TRUE
    copyvar VAR_0x8004, VAR_RESULT
    special InitRoamer
    copyvar VAR_ROAMER_POKEMON, VAR_RESULT
    speakername CommonName_Text_Mom
    msgbox PlayersHouse_1F_Text_StillUnknownPokemon, MSGBOX_DEFAULT
    closemessage
    setvar VAR_LITTLEROOT_HOUSES_STATE_MAY, 4
    setvar VAR_LITTLEROOT_HOUSES_STATE_BRENDAN, 4
    releaseall
    end

PlayersHouse_1F_EventScript_SetUpObjectEventVarsMale::
    setvar VAR_0x8008, MALE
    setvar VAR_0x8009, LOCALID_DAD
    setvar VAR_0x800A, LOCALID_MOM
    return

PlayersHouse_1F_EventScript_SetUpObjectEventVarsFemale::
    setvar VAR_0x8008, FEMALE
    setvar VAR_0x8009, LOCALID_DAD
    setvar VAR_0x800A, LOCALID_MOM
    return

PlayersHouse_1F_EventScript_DadApproachPlayerMale::
    applymovement VAR_0x8009, PlayersHouse_1F_Movement_DadApproachPlayerMale
    waitmovement 0
    return

PlayersHouse_1F_EventScript_DadApproachPlayerFemale::
    applymovement VAR_0x8009, PlayersHouse_1F_Movement_DadApproachPlayerFemale
    waitmovement 0
    return

PlayersHouse_1F_EventScript_MomApproachDadMale::
    applymovement VAR_0x800A, PlayersHouse_1F_Movement_MomApproachDadMale
    waitmovement 0
    applymovement VAR_0x8009, PlayersHouse_1F_Movement_DadFaceMomMale
    waitmovement 0
    return

PlayersHouse_1F_EventScript_MomApproachDadFemale::
    applymovement VAR_0x800A, PlayersHouse_1F_Movement_MomApproachDadFemale
    waitmovement 0
    applymovement VAR_0x8009, PlayersHouse_1F_Movement_DadFaceMomFemale
    waitmovement 0
    return

PlayersHouse_1F_EventScript_DadExitsMale::
    applymovement OBJ_EVENT_ID_PLAYER, PlayersHouse_1F_Movement_MomAndPlayerWatchDadExit
    applymovement VAR_0x800A, PlayersHouse_1F_Movement_MomAndPlayerWatchDadExit
    applymovement VAR_0x8009, PlayersHouse_1F_Movement_DadExitsMale
    waitmovement 0
    return

PlayersHouse_1F_EventScript_DadExitsFemale::
    applymovement OBJ_EVENT_ID_PLAYER, PlayersHouse_1F_Movement_MomAndPlayerWatchDadExit
    applymovement VAR_0x800A, PlayersHouse_1F_Movement_MomAndPlayerWatchDadExit
    applymovement VAR_0x8009, PlayersHouse_1F_Movement_DadExitsFemale
    waitmovement 0
    return

PlayersHouse_1F_EventScript_PlayerEnterRoomMale::
    applymovement OBJ_EVENT_ID_PLAYER, PlayersHouse_1F_Movement_PlayerEnterRoomMale
    waitmovement 0
    return

PlayersHouse_1F_EventScript_PlayerEnterRoomFemale::
    applymovement OBJ_EVENT_ID_PLAYER, PlayersHouse_1F_Movement_PlayerEnterRoomFemale
    waitmovement 0
    return

PlayersHouse_1F_EventScript_PlayerApproachTVForLatiMale::
    applymovement OBJ_EVENT_ID_PLAYER, PlayersHouse_1F_Movement_PlayerApproachTVForLatiMale
    waitmovement 0
    applymovement VAR_0x800A, Common_Movement_WalkInPlaceFasterLeft
    waitmovement 0
    return

PlayersHouse_1F_EventScript_PlayerApproachTVForLatiFemale::
    applymovement OBJ_EVENT_ID_PLAYER, PlayersHouse_1F_Movement_PlayerApproachTVForLatiFemale
    waitmovement 0
    applymovement VAR_0x800A, Common_Movement_WalkInPlaceFasterRight
    waitmovement 0
    return

PlayersHouse_1F_EventScript_MomApproachPlayerMale::
    applymovement VAR_0x800A, PlayersHouse_1F_Movement_MomApproachPlayerMale
    waitmovement 0
    applymovement OBJ_EVENT_ID_PLAYER, Common_Movement_WalkInPlaceFasterLeft
    waitmovement 0
    return

PlayersHouse_1F_EventScript_MomApproachPlayerFemale::
    applymovement VAR_0x800A, PlayersHouse_1F_Movement_MomApproachPlayerFemale
    waitmovement 0
    applymovement OBJ_EVENT_ID_PLAYER, Common_Movement_WalkInPlaceFasterRight
    waitmovement 0
    return

PlayersHouse_1F_EventScript_MomNoticesLatiBroadcastMale::
    applymovement VAR_0x800A, PlayersHouse_1F_Movement_MomNoticesLatiBroadcastMale
    waitmovement 0
    return

PlayersHouse_1F_EventScript_MomNoticesLatiBroadcastFemale::
    applymovement VAR_0x800A, PlayersHouse_1F_Movement_MomNoticesLatiBroadcastFemale
    waitmovement 0
    return

PlayersHouse_1F_EventScript_MomApproachPlayerAfterTVMale::
    applymovement VAR_0x800A, PlayersHouse_1F_Movement_MomApproachPlayerAfterTVMale
    waitmovement 0
    applymovement OBJ_EVENT_ID_PLAYER, Common_Movement_WalkInPlaceFasterRight
    waitmovement 0
    return

PlayersHouse_1F_EventScript_MomApproachPlayerAfterTVFemale::
    applymovement VAR_0x800A, PlayersHouse_1F_Movement_MomApproachPlayerAfterTVFemale
    waitmovement 0
    applymovement OBJ_EVENT_ID_PLAYER, Common_Movement_WalkInPlaceFasterLeft
    waitmovement 0
    return

PlayersHouse_1F_Movement_DadApproachPlayerMale:
    walk_right
    walk_right
    step_end

PlayersHouse_1F_Movement_DadApproachPlayerFemale:
    walk_left
    walk_left
    step_end

PlayersHouse_1F_Movement_DadFaceMomMale:
    face_left
    step_end

PlayersHouse_1F_Movement_DadFaceMomFemale:
    face_right
    step_end

PlayersHouse_1F_Movement_DadExitsMale:
    walk_down
    walk_right
    walk_down
    delay_8
    step_end

PlayersHouse_1F_Movement_DadExitsFemale:
    walk_down
    walk_left
    walk_down
    delay_8
    step_end

PlayersHouse_1F_Movement_PlayerEnterRoomMale:
    delay_16
    walk_down
    walk_down
    walk_down
    walk_in_place_faster_left
    step_end

PlayersHouse_1F_Movement_MomAndPlayerWatchDadExit:
    delay_8
    delay_16
    delay_16
    walk_in_place_faster_down
    step_end

PlayersHouse_1F_Movement_PlayerEnterRoomFemale:
    delay_16
    walk_down
    walk_down
    walk_down
    walk_in_place_faster_right
    step_end

PlayersHouse_1F_Movement_PlayerApproachTVForLatiMale:
    walk_up
    walk_left
    walk_left
    walk_left
    walk_left
    walk_in_place_faster_up
    step_end

PlayersHouse_1F_Movement_PlayerApproachTVForLatiFemale:
    walk_up
    walk_right
    walk_right
    walk_right
    walk_right
    walk_in_place_faster_up
    step_end

EventScript_RunningShoesManual::
    msgbox PlayersHouse_1F_Text_RunningShoesManual, MSGBOX_SIGN
    end
`

text PlayersHouse_1F_Text_IsntItNiceInHere {
    format("See, {PLAYER}?\n"
    "Isn't it nice in here, too?")
}

text PlayersHouse_1F_Text_GoSetTheClock {
    format("{PLAYER}.\p"
    "Go set the clock in your room, honey.")
}

text PlayersHouse_1F_Text_OhComeQuickly {
    format("Oh! {PLAYER}, {PLAYER}!\n"
    "Quick! Come quickly!")
}

text PlayersHouse_1F_Text_MaybeDadWillBeOn {
    format("Look! It's Petalburg Gym!\n"
    "Maybe your Dad will be on!")
}

text PlayersHouse_1F_Text_ItsOverWeMissedHim {
    format("Oh… It's over.\p"
    "I think your Dad was on, but we missed him.\n"
    "Too bad.")
}

text PlayersHouse_1F_Text_GoIntroduceYourselfNextDoor {
    format("Oh, yes.\n"
    "One of your Dad's friends lives in town.\p"
    "Prof. Birch is his name.\p"
    "He lives right next door, so you should go over and introduce yourself.")
}

text PlayersHouse_1F_Text_SeeYouHoney {
    format("See you, honey!")
}

text PlayersHouse_1F_Text_DidYouMeetProfBirch {
    format("Did you introduce yourself to Prof. Birch?")
}

text PlayersHouse_1F_Text_YouShouldRestABit {
    format("How are you doing, {PLAYER}?\n"
    "You look a little tired.\p"
    "I think you should rest a bit.")
}

text PlayersHouse_1F_Text_TakeCareHoney {
    format("Take care, honey!")
}

text PlayersHouse_1F_Text_GotDadsBadgeHeresSomethingFromMom {
    format("Oh? Did your Dad give you that Badge?\p"
    "Then here's something from your Mom!")
}

text PlayersHouse_1F_Text_DontPushYourselfTooHard {
    format("Don't push yourself too hard, dear.\n"
    "You can always come home.\p"
    "Go for it, honey!")
}

text PlayersHouse_1F_Text_IsThatAPokenav {
    format("What is that, honey? A Pokénav?\n"
    "Someone from Devon gave it to you?\p"
    "Well, honey, how about registering your mom?\p"
    "… … …")
}

text PlayersHouse_1F_Text_RegisteredMom {
    format("Registered Mom in the Pokénav.")
}

text PlayersHouse_1F_Text_Vigoroth1 {
    format("Fugiiiiih!")
}

text PlayersHouse_1F_Text_Vigoroth2 {
    format("Huggoh, uggo uggo…")
}

text PlayersHouse_1F_Text_ReportFromPetalburgGym {
    format("…We brought you this report from in front of Petalburg Gym.")
}

text PlayersHouse_1F_Text_TheresAMovieOnTV {
    format("There is a movie on TV.\p"
    "Two men are dancing on a big piano keyboard.\p"
    "Better get going!")
}

text PlayersHouse_1F_Text_RunningShoesManual {
    format("It's the instruction booklet for the Running Shoes.\p"
    "“Press the B Button to run while wearing your Running Shoes.\p"
    "“Lace up your Running Shoes and hit the road running!”")
}

text PlayersHouse_1F_Text_TicketFromBrineyCameForYou {
    format("Hm?\n"
    "Hey, it's {PLAYER}!\p"
    "It's been a while since I saw you, but you look…stronger, somehow.\p"
    "That's the impression I get.\n"
    "But your old man hasn't given up yet!\p"
    "Oh, yes, I have something for you.\n"
    "This came to you from someone named\l"
    "Mr. Briney.")
}

text PlayersHouse_1F_Text_PortsInSlateportLilycove {
    format("Hm, a Ticket for a ferry?\p"
    "If I recall, there are ferry ports in Slateport and Lilycove.")
}

text PlayersHouse_1F_Text_BetterGetBackToGym {
    format("I'd better get back to Petalburg Gym.\p"
    "Love, thanks for looking after the house while I'm away.")
}

text PlayersHouse_1F_Text_DadShouldStayLonger {
    format("That Dad of yours…\p"
    "He comes home for the first time in a while, but all he talks about is Pokémon.\p"
    "I understand, but he should relax and stay a little longer.")
}

text PlayersHouse_1F_Text_IsThatABreakingStory {
    format("Is that a breaking news story?")
}

text PlayersHouse_1F_Text_LatiEmergencyNewsFlash {
    format("We bring you this emergency news flash!\p"
    "In various Hoenn locales, there have been reports of a BZZT…colored\l"
    "Pokémon in flight.\p"
    "The identity of this Pokémon is currently unknown.\p"
    "We now return you to the regular movie program.")
}

text PlayersHouse_1F_Text_WhatColorDidTheySay {
    format("{PLAYER}, did you catch that?\p"
    "What color did the announcer say that Pokémon was?")
}

text PlayersHouse_1F_Text_StillUnknownPokemon {
    format("Well, isn't that something!\n"
    "There are still unknown Pokémon.")
}

script PlayersHouse_Eevee {
    lock
    faceplayer
    applymovement(LOCALID_MOM, Common_Movement_ExclamationMark)
    waitmovement(0)
    playmoncry(SPECIES_EEVEE, CRY_MODE_NORMAL)
    speakername(CommonName_Text_Mom)
    msgbox(format("Our new neighbors know an Eevee breeder.\p"
        "Don't worry, I won't tell your friends we match."), MSGBOX_DEFAULT)
    waitmoncry
    release
}

script PlayersHouse_Skitty {
    lock
    faceplayer
    applymovement(LOCALID_MOM, Common_Movement_ExclamationMark)
    waitmovement(0)
    playmoncry(SPECIES_SKITTY, CRY_MODE_NORMAL)
    setseenmon(SPECIES_SKITTY)
    speakername(CommonName_Text_Mom)
    msgbox(format("Your Dad keeps worrying I'm lonely by myself.\p"
        "I think he misses the old days."), MSGBOX_DEFAULT)
    waitmoncry
    release
}

script PlayersHouse_Buneary {
    lock
    faceplayer
    applymovement(LOCALID_MOM, Common_Movement_ExclamationMark)
    waitmovement(0)
    playmoncry(SPECIES_BUNEARY, CRY_MODE_NORMAL)
    setseenmon(SPECIES_BUNEARY)
    speakername(CommonName_Text_Mom)
    msgbox(format("Isn't my fluffy little bunbun darling?\p"
        "I found her while going to the Department Store."), MSGBOX_DEFAULT)
    waitmoncry
    release
}

script PlayersHouse_Teddiursa {
    lock
    faceplayer
    applymovement(LOCALID_MOM, Common_Movement_ExclamationMark)
    waitmovement(0)
    playmoncry(SPECIES_TEDDIURSA, CRY_MODE_NORMAL)
    setseenmon(SPECIES_TEDDIURSA)
    speakername(CommonName_Text_Mom)
    msgbox(format("I never thought I'd find one of these sweethearts in Hoenn!\p"
        "Little Brownie likes to help me cook."), MSGBOX_DEFAULT)
    waitmoncry
    applymovement(LOCALID_MOM, Common_Movement_Heart)
    waitmovement(0)
    release
}

script PlayersHouse_Dunsparce {
    lock
    faceplayer
    playmoncry(SPECIES_DUNSPARCE, CRY_MODE_NORMAL)
    setseenmon(SPECIES_DUNSPARCE)
    msgbox(format("It's wearing a name tag…\p"
        "It belongs to your Mom!"), MSGBOX_DEFAULT)
    waitmoncry
    release
}
