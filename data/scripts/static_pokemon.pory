script Common_EventScript_StationaryWild {
    lock
    faceplayer
    applymovement(VAR_LAST_TALKED, Common_Movement_ExclamationMark)
    waitse
    callnative(StartStationaryEncounter)
    playmoncry(VAR_0x8004, CRY_MODE_ENCOUNTER)
	bufferspeciesname(STR_VAR_1, VAR_0x8004)
	msgbox(format("The {STR_VAR_1} meets your eyes!"), MSGBOX_DEFAULT)
	msgbox("The wild Pok√©mon attacked!", MSGBOX_DEFAULT)
    waitmoncry
    waitmessage
    setflag(FLAG_SYS_CTRL_OBJ_DELETE)
	dowildbattle
	clearflag(FLAG_SYS_CTRL_OBJ_DELETE)
	specialvar(VAR_RESULT, GetBattleOutcome)
	goto_if_eq(VAR_RESULT, B_OUTCOME_RAN, Common_EventScript_RanFromStaticPokemon)
    goto(Common_EventScript_RemoveStaticPokemon)
}

script Common_EventScript_RanFromStaticPokemon {
    msgbox(format("You backed away and gave it space."), MSGBOX_DEFAULT)
    release
}

script Common_EventScript_RemoveStaticPokemon {
	fadescreenswapbuffers(FADE_TO_BLACK)
	removeobject(VAR_LAST_TALKED)
	fadescreenswapbuffers(FADE_FROM_BLACK)
	release
}

script Common_EventScript_LegendaryFlewAway {
	fadescreenswapbuffers(FADE_TO_BLACK)
	removeobject(VAR_LAST_TALKED)
	fadescreenswapbuffers(FADE_FROM_BLACK)
	bufferspeciesname(STR_VAR_1, VAR_0x8004)
	msgbox(gText_LegendaryFlewAway, MSGBOX_DEFAULT)
	release
}

script Common_EventScript_PetPokemon {
    lock
    faceplayer
    specialvar(VAR_0x8004, GetSpeciesFromOverworldPokemon)
    setseenmon(VAR_0x8004)
	bufferspeciesname(STR_VAR_1, VAR_0x8004)
	msgbox(format("The {STR_VAR_1} looks friendly.\nPet it?"), MSGBOX_YESNO)
	if (var(VAR_RESULT)) {
	    playmoncry(VAR_0x8004, CRY_MODE_NORMAL)
	    applymovement(VAR_LAST_TALKED, ContestHall_Movement_Heart)
    	waitse
    	waitmovement(0)
	    msgbox("It's very happy!", MSGBOX_DEFAULT)
	    addvar(VAR_TIMES_PET_PEACEFUL_POKEMON, 1)
	    if (flag(FLAG_PET_MANY_DOGS)) {
	        random(5)
	        switch (var(VAR_RESULT)) {
            case 0:
                msgbox(Common_EventScript_PetPokemon_GiftText, MSGBOX_DEFAULT)
                giveitem(ITEM_RARE_BONE)
            case 1:
                msgbox(Common_EventScript_PetPokemon_GiftText, MSGBOX_DEFAULT)
                giveitem(ITEM_PRETTY_FEATHER)
            }
	    } else {
	        if (var(VAR_TIMES_PET_PEACEFUL_POKEMON) == 50) {
        	    setflag(FLAG_PET_MANY_DOGS)
    	    }
	        random(10)
	        switch (var(VAR_RESULT)) {
            case 0:
                msgbox(Common_EventScript_PetPokemon_GiftText, MSGBOX_DEFAULT)
                giveitem(ITEM_RARE_BONE)
            case 1:
                msgbox(Common_EventScript_PetPokemon_GiftText, MSGBOX_DEFAULT)
                giveitem(ITEM_PRETTY_FEATHER)
            }
        }
	} else {
	    playmoncry(VAR_0x8004, CRY_MODE_WEAK)
	    applymovement(VAR_LAST_TALKED, Common_Movement_QuestionMark)
    	waitse
    	waitmovement(0)
	    msgbox("It seems disappointed somehow.", MSGBOX_DEFAULT)
	}
	release
}

text Common_EventScript_PetPokemon_GiftText {
    format("It gave you an item it was carrying!")
}

script Common_EventScript_InvisibleWild {
    lock
    faceplayer
    applymovement(VAR_LAST_TALKED, Movement_PokemonAppears)
    waitse
    callnative(StartStationaryEncounter)
    playmoncry(VAR_0x8004, CRY_MODE_ENCOUNTER)
	bufferspeciesname(STR_VAR_1, VAR_0x8004)
	msgbox(format("You startled the {STR_VAR_1}!"), MSGBOX_DEFAULT)
    waitmoncry
    waitmessage
    setflag(FLAG_SYS_CTRL_OBJ_DELETE)
	dowildbattle
	clearflag(FLAG_SYS_CTRL_OBJ_DELETE)
	specialvar(VAR_RESULT, GetBattleOutcome)
	goto_if_eq(VAR_RESULT, B_OUTCOME_RAN, Common_EventScript_RanFromStaticPokemon)
    goto(Common_EventScript_RemoveStaticPokemon)
}

movement Movement_PokemonAppears {
	set_visible
	face_player
	emote_exclamation_mark
	set_invisible
	delay_4
	set_visible
	delay_4
	set_invisible
	delay_4
	set_visible
	delay_8
	set_invisible
	delay_8
	set_visible
}
